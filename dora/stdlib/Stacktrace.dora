class Stacktrace() {
  var backtrace: Array[Int32] = nil;
  var elements: Array[StacktraceElement] = nil;

  self.retrieveStacktrace();

  fun getStacktrace() -> Array[StacktraceElement] {
    if self.elements !== nil {
      return self.elements;
    }

    if self.backtrace === nil {
      self.elements = Array::empty[StacktraceElement]();
      return self.elements;
    }

    var i = 0L;
    let len = self.backtrace.length() / 2L;
    self.elements = Array[StacktraceElement](len);

    while i < len {
      self.elements.set(i, self.getStacktraceElement(i));
      i = i + 1L;
    }

    return self.elements;
  }

  fun printStacktrace() {
    let x = self.getStacktrace();
    var i = 0L;

    while i < x.length() {
      println(i.toString() + ": " + x.get(i).toString());
      i = i + 1L;
    }
  }

  @internal fun retrieveStacktrace();
  @internal fun getStacktraceElement(idx: Int64) -> StacktraceElement;
}

class StacktraceElement(let name: String, let line: Int32) {
  fun toString() -> String = self.name + ": " + self.line.toString();
}