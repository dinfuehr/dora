class Vec[T] {
  var array: Array[T] = arrayEmpty[T]();
  var len: Int64 = 0L;

  fun get(idx: Int64) -> T {
    if idx < 0L || idx >= self.len {
      fatalError("index out of bounds for vector");
    }

    return self.array.get(idx.toInt32());
  }

  fun set(idx: Int64, val: T) {
    if idx < 0L || idx >= self.len {
      fatalError("index out of bounds for vector");
    }

    self.array.set(idx.toInt32(), val);
  }

  fun reserve(newcap: Int64) {
    assert(newcap >= 0L);

    if self.capacity() >= newcap {
      return;
    }

    let newarray = Array[T](newcap.toInt32());
    arrayCopy[T](self.array, 0L, newarray, 0L, self.len);
    self.array = newarray;
  }

  fun push(val: T) {
    var newcap = self.capacity();

    if self.len == newcap {
      if newcap == 0L {
        newcap = 4L;
      } else {
        newcap = newcap * 2L;
      }

      let newarray = Array[T](newcap.toInt32());
      arrayCopy[T](self.array, 0L, newarray, 0L, self.len);
      self.array = newarray;
    }

    self.array.set(self.len.toInt32(), val);
    self.len = self.len + 1L;
  }

  fun toArray() -> Array[T] {
    let newarray = Array[T](self.len.toInt32());
    arrayCopy[T](self.array, 0L, newarray, 0L, self.len);
    newarray
  }

  fun pop() -> T {
    if self.len == 0L {
      fatalError("no element left to pop");
    }

    let newlength = self.len - 1L;
    let temp = self.array.get(newlength.toInt32());

    // set popped element to nil so that GC can collect object
    // not necessary for primitive types
    self.array.set(newlength.toInt32(), defaultValue[T]());

    self.len = newlength;
    temp
  }

  fun trimToLen() {
    if self.len != self.capacity() {
      if self.len == 0L {
        self.array = arrayEmpty[T]();
      } else {
        let newarray = Array[T](self.len.toInt32());
        arrayCopy[T](self.array, 0L, newarray, 0L, self.len);
        self.array = newarray;
      }
    }
  }

  fun removeAt(ind: Int64) -> T {
    var ind = ind;
    assert(ind < self.len);
    let temp = self.get(ind);
    let len = self.len;

    while ind < len - 1L {
      self.set(ind, self.get(ind+1L));
      ind = ind + 1L;
    }

    self.set(ind, defaultValue[T]());
    self.len = ind;

    temp
  }

  fun length() -> Int64 {
    return self.len;
  }

  fun isEmpty() -> Bool {
    self.len == 0L
  }

  fun capacity() -> Int64 {
    self.array.length()
  }

  fun first() -> Option[T] {
    if self.len == 0L {
      none[T]()
    } else {
      some[T](self.array.get(0))
    }
  }

  fun last() -> Option[T] {
    if self.len == 0L {
      none[T]()
    } else {
      some[T](self.array.get((self.len - 1L).toInt32()))
    }
  }

  fun makeIterator() -> VecIter[T] {
    VecIter[T](self)
  }

  fun makeReverseIterator() -> ReverseVecIter[T] {
    ReverseVecIter[T](self)
  }
}

class VecIter[T](let data: Vec[T]) {
  var idx: Int64 = 0L;

  fun hasNext() -> Bool {
    self.idx < self.data.length()
  }

  fun next() -> T {
    let result = self.data.get(self.idx);
    self.idx = self.idx + 1L;
    result
  }
}

class ReverseVecIter[T](let data: Vec[T]) {
  var idx: Int64 = data.length() - 1L;

  fun hasNext() -> Bool {
    self.idx != -1L
  }

  fun next() -> T {
    let result = self.data.get(self.idx);
    self.idx = self.idx - 1L;
    result
  }
}

fun removeItem[T: Equals](vec: Vec[T], elem: T) {
  var i = 0L;

  while i < vec.length() {
    if vec.get(i).equals(elem) {
      vec.removeAt(i);
    } else {
      i = i + 1L;
    }
  }
}

fun vec[T](values: T...) -> Vec[T] {
  let result = Vec[T]();
  result.array = values;
  result.len = values.length();
  result
}
