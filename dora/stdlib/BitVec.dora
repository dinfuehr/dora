@pub class BitVec {
    var data: Array[Int32] = Array[Int32]::empty();

    @pub fun contains(idx: Int64): Bool {
        if idx < 0L {
            fatalError("index out of bounds");
        }

        if idx >= self.data.size() * 32L {
            return false;
        }

        let entry_idx = idx / 32L;
        let value = self.data.get(entry_idx);
        let value_idx = idx - entry_idx;

        return value & (1I << value_idx.toInt32()) != 0I;
    }

    @pub fun insert(idx: Int64) {
        if idx < 0L {
            fatalError("index out of bounds");
        }

        self.ensureCapacity(idx+1L);

        let entry_idx = idx / 32L;
        var value = self.data.get(entry_idx);
        let value_idx = idx - entry_idx;

        value = value | (1I << value_idx.toInt32());

        self.data.set(entry_idx, value);
    }

    @pub fun remove(idx: Int64) {
        if idx < 0L {
            fatalError("index out of bounds");
        }

        if idx >= self.data.size() * 32L {
            return;
        }

        let entry_idx = idx / 32L;
        var value = self.data.get(entry_idx);
        let value_idx = idx - entry_idx;

        value = value & !(1I << value_idx.toInt32());

        self.data.set(entry_idx, value);
    }

    @pub fun ensureCapacity(newcap: Int64) {
        if newcap < self.data.size() * 32L || newcap == 0L {
            return;
        }

        let newcap = (newcap + 31L) / 32L;

        let newarray = Array[Int32]::zero(newcap);
        Array[Int32]::copy(self.data, 0L, newarray, 0L, self.data.size());
        self.data = newarray;
    }

    @pub fun capacity(): Int64 {
        self.data.size() * 32L
    }
}
