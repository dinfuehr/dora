@pub @internal class String {
  @pub fun equals(rhs: String): Bool {
    var i = 0L;

    if self.size() != rhs.size() {
        return false;
    }

    let len = self.size();

    while i < len {
        if self.getByte(i) != rhs.getByte(i) {
            return false;
        }

        i = i + 1L;
    }

    return true;
  }

  @pub fun toString(): String = self;

  @pub @internal fun compareTo(rhs: String): Int32;

  @pub @internal fun size(): Int64;

  @internal fun toInt32Success(): Bool;
  @internal fun toInt64Success(): Bool;

  @internal fun toInt32OrZero(): Int32;
  @internal fun toInt64OrZero(): Int64;

  @internal fun toFloat32Success(): Bool;
  @internal fun toFloat64Success(): Bool;

  @internal fun toFloat32OrZero(): Float32;
  @internal fun toFloat64OrZero(): Float64;

  @pub fun toInt32(): Option[Int32] {
    if self.toInt32Success() {
      Option[Int32]::Some(self.toInt32OrZero())
    } else {
      Option[Int32]::None
    }
  }

  @pub fun toInt64(): Option[Int64] {
    if self.toInt64Success() {
      Option[Int64]::Some(self.toInt64OrZero())
    } else {
      Option[Int64]::None
    }
  }

  @pub fun toFloat32(): Option[Float32] {
    if self.toFloat32Success() {
      Option[Float32]::Some(self.toFloat32OrZero())
    } else {
      Option[Float32]::None
    }
  }

  @pub fun toFloat64(): Option[Float64] {
    if self.toFloat64Success() {
      Option[Float64]::Some(self.toFloat64OrZero())
    } else {
      Option[Float64]::None
    }
  }

  @pub @internal fun plus(rhs: String): String;

  @pub @internal fun getByte(idx: Int64): UInt8;

  @pub @internal fun clone(): String;

  @pub fun isEmpty(): Bool = self.size() == 0L;

  @pub fun contains(val: String): Bool = self.indexOfFirst(val).isSome();

  @pub fun indexOfFirst(val: String): Option[Int64] {
    let m = val.size();
    if m == 0L {
      return Some(0L);
    }

    let n = self.size();
    if m > n {
      return None;
    }

    var j = 0L;
    var k = 0L;
    var ell = 0L;

    if val.getByte(0L) == val.getByte(1L) {
       k = 2L;
       ell = 1L;
    } else {
       k = 1L;
       ell = 2L;
    }

    j = 0L;
    while j <= n - m {
      if val.getByte(1L) != self.getByte(j + 1L) {
        j = j + k;
      } else {
        if String::compare(val, 2L, self, j + 2L, m - 2L) && val.getByte(0L) == self.getByte(j) {
          return Option[Int64]::Some(j);
        }
        j = j + ell;
      }
    }
    return Option[Int64]::None;
  }

  @static fun compare(a: String, aPos: Int64, b: String, bPos: Int64, len: Int64): Bool {
    var i = 0L;

    while i < len {
      if a.getByte(aPos+i) != b.getByte(bPos+i) {
        return false;
      }
      i = i + 1L;
    }

    return true;
  }

  @pub fun startsWith(val: String): Bool {
    let valLen = val.size();
    if valLen > self.size() {
      return false;
    }
    var i = 0L;
    while i < valLen {
      if self.getByte(i) !== val.getByte(i) {
        return false;
      }
      i = i + 1L;
    }
    return true;
  }

  @pub fun endsWith(val: String): Bool {
    let slfLen = self.size();
    let valLen = val.size();
    if valLen > slfLen {
      return false;
    }
    var iSlf = 0L;
    var iVal = slfLen - valLen;
    while iSlf < valLen {
      if self.getByte(iSlf + iVal) !== val.getByte(iSlf) {
        return false;
      }
      iSlf = iSlf + 1L;
    }
    return true;
  }

  @pub fun codePoints(): CodepointIterator = CodepointIterator(self, 0L);

  @pub @static @internal fun fromBytesPart(val: Array[UInt8], offset: Int64, len: Int64): Option[String] ;

  @pub @static fun fromBytes(val: Array[UInt8]): Option[String] {
    String::fromBytesPart(val, 0L, val.size())
  }

  @pub @static @internal fun fromStringPart(val: String, offset: Int64, len: Int64): Option[String];

  @pub @static fun binary(data: Int64): String {
    var data = data;
    if data == 0L { return "0"; }
    let array = Array[UInt8]::zero(64L);
    var at = array.size();
    while data != 0L {
      array(at - 1L) = ('0'.toInt64() + (data & 1L)).toUInt8();
      data = data.shiftRight(1);
      at = at - 1L;
    }
    return String::fromBytesPart(array, at, array.size() - at + 1L).getOrPanic();
  }

  @pub @static fun hex(data: Int64): String {
    var data = data;
    if data == 0L { return "0"; }
    let array = Array[UInt8]::zero(16L);
    var at = array.size();
    while data != 0L {
      let digit = (data & 0xFL).toInt32();

      if digit < 10 {
        array(at - 1L) = ('0'.toInt32() + digit).toUInt8();
      } else {
        array(at - 1L) = ('A'.toInt32() + digit - 10).toUInt8();
      }

      data = data.shiftRight(4);
      at = at - 1L;
    }
    return String::fromBytesPart(array, at, array.size() - at + 1L).getOrPanic();
  }
}
