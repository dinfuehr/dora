@internal class String {
  fun equals(rhs: String) -> Bool {
    var i = 0L;

    if self.length() != rhs.length() {
        return false;
    }

    let len = self.length();

    while i < len {
        if self.getByte(i.toInt32()) != rhs.getByte(i.toInt32()) {
            return false;
        }

        i = i + 1L;
    }

    return true;
  }

  fun toString() -> String = self;

  @internal fun compareTo(rhs: String) -> Int32;

  @internal fun length() -> Int64;

  @internal fun toInt32Success() -> Bool;
  @internal fun toInt64Success() -> Bool;

  @internal fun toInt32OrZero() -> Int32;
  @internal fun toInt64OrZero() -> Int64;

  fun toInt32() -> Option[Int32] {
    if self.toInt32Success() {
      some[Int32](self.toInt32OrZero())
    } else {
      none[Int32]()
    }
  }

  fun toInt64() -> Option[Int64] {
    if self.toInt64Success() {
      some[Int64](self.toInt64OrZero())
    } else {
      none[Int64]()
    }
  }

  @internal fun plus(rhs: String) -> String;

  @internal fun getByte(idx: Int32) -> UInt8;
  @internal fun clone() -> String;

  @internal @static fun fromBytesPartOrNull(val: Array[UInt8], offset: Int64, len: Int64) -> String;
  @internal @static fun fromStringPartOrNull(val: String, offset: Int64, len: Int64) -> String;

  @static fun fromBytesPart(val: Array[UInt8], offset: Int64, len: Int64) -> Option[String] {
    let value = String::fromBytesPartOrNull(val, offset, len);

    if value === nil {
      none[String]()
    } else {
      some[String](value)
    }
  }

  @static fun fromBytes(val: Array[UInt8]) -> Option[String] {
    let value = String::fromBytesPartOrNull(val, 0L, val.length());
  
    if value === nil {
      none[String]()
    } else {
      some[String](value)
    }
  }

  @static fun fromStringPart(val: String, offset: Int64, len: Int64) -> Option[String] {
    let value = String::fromStringPartOrNull(val, offset, len);

    if value === nil {
      none[String]()
    } else {
      some[String](value)
    }
  }

  @static fun fromString(val: String) -> String = val.clone();

  @static fun binary(data: Int64) -> String {
    var data = data;
    if data == 0L { return "0"; }
    let array = Array[UInt8](64);
    var at = array.length();
    while data != 0L {
      array((at - 1L).toInt32()) = ('0'.toInt64() + (data & 1L)).toUInt8();
      data = data.shiftRight(1);
      at = at - 1L;
    }
    return String::fromBytesPart(array, at, array.length() - at + 1L).unwrap();
  }

  @static fun hex(data: Int64) -> String {
    var data = data;
    if data == 0L { return "0"; }
    let array = Array[UInt8](16);
    var at = array.length();
    while data != 0L {
      let digit = (data & 0xFL).toInt32();

      if digit < 10 {
        array((at - 1L).toInt32()) = ('0'.toInt32() + digit).toUInt8();
      } else {
        array((at - 1L).toInt32()) = ('A'.toInt32() + digit - 10).toUInt8();
      }

      data = data.shiftRight(4);
      at = at - 1L;
    }
    return String::fromBytesPart(array, at, array.length() - at + 1L).unwrap();
  }

  fun isEmpty() -> Bool = self.length() == 0L;

  fun codePoints() -> CodepointIterator = CodepointIterator(self, 0L);
}
