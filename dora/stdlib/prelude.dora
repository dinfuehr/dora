@pub @internal fun fatalError(msg: String);
@pub @internal fun abort();
@pub @internal fun exit(status: Int32);
@pub @internal fun unreachable[T](): T;

@pub fun unimplemented() = fatalError("not yet implemented");

@pub @internal fun print(text: String);
@pub @internal fun println(text: String);
@pub @internal fun assert(val: Bool);
@pub @internal fun debug();
@pub @internal fun argc(): Int32;
@pub @internal fun argv(idx: Int32): String;
@pub @internal fun forceCollect();
@pub @internal fun forceMinorCollect();

@pub @internal fun call(fct: String);

@pub @internal fun timestamp(): Int64;
@pub @internal fun spawn(path: String, arguments: String...);

@pub class Object

fun isValidUtf8(data: Array[UInt8]): Bool {
  var i = 0L;

  while i < data.size() {
    let by = data(i).toInt32();
    var codePoint = 0I;
    var nextUInt8s = 0I;
    var min = 0I;
    var max = 0I;

    if by & 0x80I == 0I {
      nextUInt8s = 0;
      i = i + 1L;
      continue;

    } else if by & 0xC0I == 0x80I {
      return false;

    } else if by & 0xE0I == 0xC0I {
      codePoint = by & 0x1FI;
      nextUInt8s = 1;
      min = 0x80;
      max = 0x7FFI;

    } else if by & 0xF0I == 0xE0I {
      codePoint = by & 0xFI;
      nextUInt8s = 2I;
      min = 0x800I;
      max = 0xFFFFI;

    } else if by & 0xF8I == 0xF0I {
      codePoint = by & 0x7I;
      nextUInt8s = 3;
      min = 0x10000;
      max = 0x10FFFFI;
    }

    while nextUInt8s > 0I {
      nextUInt8s = nextUInt8s - 1I;
      i = i + 1L;

      if i >= data.size() {
        return false;
      }

      let by = data(i).toInt32();

      if by & 0xC0I != 0x80I {
        return false;
      }

      codePoint = (codePoint << 6I) | (by & 0x3FI);
    }

    if codePoint < min || codePoint > max {
      return false;
    }

    i = i + 1L;
  }

  return true;
}

@internal fun unsafeKillRefs[T](arr: Array[T], idx: Int64);

@pub @internal fun sleep(seconds: Int32);
