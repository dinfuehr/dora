@pub @internal class Array[T](values: T...) {
  @pub @internal fun size(): Int64;
  @pub @internal fun get(idx: Int64): T;
  @pub @internal fun set(idx: Int64, val: T);

  @pub fun isEmpty(): Bool = self.size() == 0L;

  @pub fun enumerate(): ArrayEnumerator[T] {
    ArrayEnumerator[T](self)
  }

  @pub @static fun empty(): Array[T] {
    Array[T]::unsafeNew(0L)
  }

  @static @internal fun unsafeNew(size: Int64): Array[T];

  @pub @static fun fill(len: Int64, value: T): Array[T] {
    let array = Array[T]::unsafeNew(len);
    var i = 0L;

    while i < len {
      array(i) = value;
      i = i + 1L;
    }

    return array;
  }

  @pub @static fun copy(src: Array[T], srcPos: Int64, dest: Array[T], destPos: Int64, len: Int64) {
    var i = 0L;

    while i < len {
      dest.set(destPos+i, src.get(srcPos+i));
      i = i + 1L;
    }
  }
}

impl[T: Default] Array[T] {
  @pub @static fun new(len: Int64): Array[T] {
    let array = Array[T]::unsafeNew(len);
    var i = 0L;
    let value = T::default();

    while i < len {
      array.set(i, value);
      i = i + 1L;
    }

    return array;
  }
}

impl[T: Zero] Array[T] {
  @pub @static fun zero(len: Int64): Array[T] {
    Array[T]::unsafeNew(len)
  }
}

@pub class ArrayEnumerator[T](let array: Array[T]) {
  var idx: Int64 = 0L;

  @pub fun hasNext(): Bool {
    self.idx < self.array.size()
  }

  @pub fun next(): (Int64, T) {
    let current_idx = self.idx;
    self.idx = self.idx + 1L;
    (current_idx, self.array(current_idx))
  }
}

impl[T: Equals] Array[T] {
  @pub fun contains(value: T): Bool {
    var i = 0L;

    while i < self.size() {
      let x = self.get(i);
      if x.equals(value) {
        return true;
      }
      i = i + 1L;
    }

    return false;
  }
}

impl[T: Identity] Array[T] {
  @pub fun has(value: T): Bool {
    var i = 0L;

    while i < self.size() {
      if self.get(i).identicalTo(value) {
        return true;
      }
      i = i + 1L;
    }

    return false;
  }
}
