module Assert {
  fun equal[T : Equals + Stringable](actual: T, expected: T) {
    if actual.equals(expected).not() {
      fatalError("values are not equal (actual: ${actual}, expected: ${expected})");
    }
  }
  fun notEqual[T : Equals + Stringable](actual: T, expected: T) {
    if actual.equals(expected) {
      fatalError("values are equal (actual: ${actual}, expected: ${expected})");
    }
  }
  fun identical[T : Identity + Stringable](actual: T, expected: T) {
    if actual !== expected {
      fatalError("values are not identical (actual: ${actual}, expected: ${expected})");
    }
  }
  fun notIdentical[T : Identity + Stringable](actual: T, expected: T) {
    if actual === expected {
      fatalError("values are identical (actual: ${actual}, expected: ${expected})");
    }
  }

  fun string(actual: String) -> StringAssert = StringAssert(actual);
  fun set[T : Equals + Hash](actual: HashSet[T]) -> SetAssert[T] = SetAssert[T](actual);
}

class StringAssert(let value: String) {
  /*
  fun contains(expected: String) -> StringAssert {
    assert(self.value.contains(expected));
    return self;
  }
  */

  fun hasSize(expected: Int64) -> StringAssert {
    if self.value.length() != expected {
      fatalError("unexpected string size (actual: ${self.value.length()}, expected: ${expected})");
    }
    return self;
  }

  fun isEmpty() -> StringAssert {
    if self.value.isEmpty().not() {
      fatalError("string not empty (is \"${self.value}\")");
    }
    return self;
  }

  fun isNotEmpty() -> StringAssert {
    if self.value.isEmpty() {
      fatalError("string is empty");
    }
    return self;
  }
}

class SetAssert[T : Equals + Hash](let value: HashSet[T]) {
  fun contains(expected: T) -> SetAssert[T] {
    assert(self.value.contains(expected));
    return self;
  }

  fun containsAll(expected: T...) -> SetAssert[T] {
    for value in expected {
      assert(self.value.contains(value));
    }
    return self;
  }

  fun hasSize(expected: Int64) -> SetAssert[T] {
    assert(self.value.length() == expected);
    return self;
  }

  fun isEmpty() -> SetAssert[T] {
    assert(self.value.isEmpty());
    return self;
  }
}
