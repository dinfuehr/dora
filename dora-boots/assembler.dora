class AssemblerBuffer {
    let buffer: Vec[UInt8] = Vec[UInt8]();
    var pos: Int64 = 0L;

    fun position(): Int64 {
        self.pos
    }

    fun setPosition(newpos: Int64) {
        self.pos = newpos;
    }

    fun emitByte(value: UInt8) {
        self.buffer.push(value);
    }

    fun emitInt32(value: Int32) {
        self.emitByte((value & 0xFFI).toUInt8());
        self.emitByte(((value >>> 8) & 0xFFI).toUInt8());
        self.emitByte(((value >>> 16) & 0xFFI).toUInt8());
        self.emitByte(((value >>> 24) & 0xFFI).toUInt8());
    }

    fun emitInt64(value: Int64) {
        self.emitInt32(value.toInt32());
        self.emitInt32((value >>> 32).toInt32());
    }

    fun patchUInt8(offset: Int64, value: UInt8) {
        self.buffer(offset) = value;
    }

    fun patchInt32(offset: Int64, value: Int32) {
        self.buffer(offset) = (value & 0xFFI).toUInt8();
        self.buffer(offset + 1L) = ((value >>> 8) & 0xFFI).toUInt8();
        self.buffer(offset + 2L) = ((value >>> 16) & 0xFFI).toUInt8();
        self.buffer(offset + 3L) = ((value >>> 24) & 0xFFI).toUInt8();
    }

    fun size(): Int64 {
        self.buffer.size()
    }

    fun capacity(): Int64 {
        self.buffer.capacity()
    }

    fun toArray(): Array[UInt8] {
        self.buffer.toArray()
    }

    fun createLabel(): Label {
        Label()
    }

    fun bindLabel(lbl: Label) {
        lbl.bindTo(self.size());
    }

    fun createAndBindLabel(): Label {
        let lbl = self.createLabel();
        self.bindLabel(lbl);

        lbl
    }
}

class Label {
    var offset: Int64 = -1L;

    fun bindTo(pos: Int64) {
        assert(pos >= 0L && self.offset < 0L);
        self.offset = pos;
    }

    fun isBound(): Bool {
        self.offset >= 0L
    }
}

@test fun testAssemblerBufferEmitByte(_x: Testing) {
    let buffer = AssemblerBuffer();
    buffer.emitByte(0x01Y);
    buffer.emitByte(0x02Y);
    assert(buffer.size() == 2L);
}

@test fun testAssemblerBufferEmitInt(_x: Testing) {
    let asm = AssemblerBuffer();
    asm.emitInt32(0x11223344);
    let buffer = asm.toArray();
    assert(buffer(0L) == 0x44Y);
    assert(buffer(1L) == 0x33Y);
    assert(buffer(2L) == 0x22Y);
    assert(buffer(3L) == 0x11Y);
    assert(buffer.size() == 4L);
}

@open @abstract class SlowPath {
    @abstract fun emit(asm: Assembler);
}

@open @abstract class Assembler

class Register(let value: Int32) {
    fun equals(rhs: Register): Bool {
        self.value == rhs.value
    }
}

class MachineCode(let bytes: Array[UInt8]) {
    var start: Int64 = 0L;

    fun getInt32(idx: Int64): Int32 {
        self.bytes(idx).toInt32() |
            self.bytes(idx+1L).toInt32() << 8 |
            self.bytes(idx+2L).toInt32() << 16 |
            self.bytes(idx+3L).toInt32() << 24
    }
}

fun assertAsm(code: MachineCode, bytes: UInt8...) {
    assert(code.start + bytes.size() <= code.bytes.size());

    for (idx, exp) in bytes.enumerate() {
        let got = code.bytes(code.start + idx);

        if exp != got {
            println("expected ${exp.toStringHex()} at index ${code.start + idx} but got ${got.toStringHex()}");

            var i = 0L;
            print("buffer:   ");

            while i < bytes.size() {
                if i > 0L { print(", "); }
                let value = bytes(i);
                print("${value.toStringHex()}");
                i = i + 1L;
            }

            i = 0L;
            println("");
            print("expected: ");

            while i < bytes.size() {
                if i > 0L { print(", "); }
                let value = code.bytes(code.start + i);
                print("${value.toStringHex()}");
                i = i + 1L;
            }
            println("");
        }

        assert(exp == got);
    }

    code.start = code.start + bytes.size();
}

fun assertAsm32(code: MachineCode, values: Int32...) {
    assert(code.start % 4L == 0L);
    assert(code.bytes.size() % 4L == 0L);

    assert(code.start + values.size() * 4L <= code.bytes.size());

    var idx = 0L;

    while idx < values.size() {
        let exp = values(idx);
        let got = code.getInt32(code.start + idx * 4L);

        if exp != got {
            println("expected ${exp.toStringHex()} at index ${code.start + idx} but got ${got.toStringHex()}");

            var i = 0L;
            print("buffer:   ");

            for (idx, value) in values.enumerate() {
                if i > 0L { print(", "); }
                let value = values(i);
                print("${value.toStringHex()}");
                i = i + 1L;
            }

            i = 0L;
            println("");
            print("expected: ");

            while i < values.size() {
                if i > 0L { print(", "); }
                let value = code.getInt32(code.start + i * 4L);
                print("${value.toStringHex()}");
                i = i + 1L;
            }
            println("");
        }

        assert(exp == got);

        idx = idx + 1L;
    }

    code.start = code.start + values.size() * 4L;
}

fun assertAsmEnd(code: MachineCode) {
    assert(code.start == code.bytes.size());
}
