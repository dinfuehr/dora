class AssemblerBuffer {
    let buffer: Vec[Byte] = Vec[Byte]();
    let slow_paths: Vec[SlowPath] = Vec[SlowPath]();

    fun emitByte(value: Byte) {
        self.buffer.push(value);
    }

    fun emitInt(value: Int) {
        self.emitByte((value & 0xFF).toByte());
        self.emitByte(((value >>> 8) & 0xFF).toByte());
        self.emitByte(((value >>> 16) & 0xFF).toByte());
        self.emitByte(((value >>> 24) & 0xFF).toByte());
    }

    fun addSlowPath(slow_path: SlowPath) {
        self.slow_paths.push(slow_path);
    }

    fun length() -> Int {
        self.buffer.length()
    }

    fun capacity() -> Int {
        self.buffer.capacity()
    }

    fun toArray() -> Array[Byte] {
        self.buffer.toArray()
    }
}

@test fun testAssemblerBufferEmitByte(_x: Testing) {
    let buffer = AssemblerBuffer();
    buffer.emitByte(0x01Y);
    buffer.emitByte(0x02Y);
    assert(buffer.length() == 2);
}

@test fun testAssemblerBufferEmitInt(_x: Testing) {
    let asm = AssemblerBuffer();
    asm.emitInt(0x11223344);
    let buffer = asm.toArray();
    assert(buffer(0) == 0x44Y);
    assert(buffer(1) == 0x33Y);
    assert(buffer(2) == 0x22Y);
    assert(buffer(3) == 0x11Y);
    assert(buffer.length() == 4);
}

@open @abstract class SlowPath {
    @abstract fun emit(asm: Assembler);
}

@open @abstract class Assembler

class Register(let value: Int) {
    fun lowBits() -> Int {
        self.value & 0b111
    }

    fun needsRexBit() -> Bool {
        self.value > 7
    }

    fun equals(rhs: Register) -> Bool {
        self.value == rhs.value
    }
}

class Immediate(let value: Long) {
    fun isInt8() -> Bool {
        self.value >= -128L && self.value <= 127L
    }

    fun isByte() -> Bool {
        self.value >= Byte::minValue().toLong() && self.value <= Byte::maxValue().toLong()
    }

    fun isInt() -> Bool {
        self.value >= Int::minValue().toLong() && self.value <= Int::maxValue().toLong()
    }

    fun toByte() -> Byte {
        self.value.toByte()
    }

    fun toInt() -> Int {
        self.value.toInt()
    }

    fun toLong() -> Long {
        self.value
    }
}

var RAX: Register;
var RCX: Register;
var RDX: Register;
var RBX: Register;
var RSP: Register;
var RBP: Register;
var RSI: Register;
var RDI: Register;

var R8: Register;
var R9: Register;
var R10: Register;
var R11: Register;
var R12: Register;
var R13: Register;
var R14: Register;
var R15: Register;

fun setupAssemblerRegisters() {
    if RAX !== nil { return; }
    RAX = Register(0);
    RCX = Register(1);
    RDX = Register(2);
    RBX = Register(3);
    RSP = Register(4);
    RBP = Register(5);
    RSI = Register(6);
    RDI = Register(7);

    R8 = Register(8);
    R9 = Register(9);
    R10 = Register(10);
    R11 = Register(11);
    R12 = Register(12);
    R13 = Register(13);
    R14 = Register(14);
    R15 = Register(15);
}

class AssemblerX64: Assembler {
    let buffer: AssemblerBuffer = AssemblerBuffer();

    fun pushqr(reg: Register) {
        self.emitRexOptional(reg);
        self.emitByte((0x50 + reg.lowBits()).toByte());
    }

    fun popqr(reg: Register) {
        self.emitRexOptional(reg);
        self.emitByte((0x58 + reg.lowBits()).toByte());
    }

    fun addqri(reg: Register, imm: Immediate) {
        self.emitAlu64Imm(reg, imm, 0b000, 0x05Y);
    }

    fun subqri(reg: Register, imm: Immediate) {
        self.emitAlu64Imm(reg, imm, 0b101, 0x2dY);
    }

    fun addqrr(dest: Register, src: Register) {
        self.emitRex64ModRm(dest, src);
        self.emitByte(0x01Y);
        self.emitModRmReg(dest.lowBits(), src.lowBits());
    }

    fun subqrr(dest: Register, src: Register) {
        self.emitRex64ModRm(dest, src);
        self.emitByte(0x29Y);
        self.emitModRmReg(dest.lowBits(), src.lowBits());
    }

    fun movqrr(dest: Register, src: Register) {
        self.emitRex64ModRm(src, dest);
        self.emitByte(0x89Y);
        self.emitModRmReg(src.lowBits(), dest.lowBits());
    }

    fun retq() {
        self.emitByte(0xC3Y);
    }

    fun nop() {
        self.emitByte(0x90Y);
    }

    fun emitAlu64Imm(reg: Register, imm: Immediate, modrm_reg: Int, rax_opcode: Byte) {
        assert(imm.isInt());
        self.emitRex64RmField(reg);

        if imm.isInt8() {
            self.emitByte(0x83Y);
            self.emitModRm(0b11, modrm_reg, reg.lowBits());
            self.emitByte(imm.toByte());
        } else if reg == RAX {
            self.emitByte(rax_opcode);
            self.emitInt(imm.toInt());
        } else {
            self.emitByte(0x81Y);
            self.emitModRm(0b11, modrm_reg, reg.lowBits());
            self.emitInt(imm.toInt());
        }
    }

    fun emitRexOptional(reg: Register) {
        if reg.needsRexBit() {
            self.emitRex(false, false, false, true);
        }
    }

    fun emitRex64RmField(modrm_rm: Register) {
        self.emitRex(true, false, false, modrm_rm.needsRexBit());
    }

    fun emitRex64ModRm(modrm_reg: Register, modrm_rm: Register) {
        self.emitRex(true, modrm_reg.needsRexBit(), false, modrm_rm.needsRexBit());
    }

    fun emitRex(w: Bool, r: Bool, x: Bool, b: Bool) {
        // w - 64-bit width
        // r - extension of modrm-reg field
        // x - extension of sib index field
        // b - extension of modrm-rm/sib base/opcode reg field
        let opcode = 0x40 | w.toInt() << 3 | r.toInt() << 2 | x.toInt() << 1 | b.toInt();
        self.emitByte(opcode.toByte());
    }

    fun emitModRmReg(reg: Int, rm: Int) {
        self.emitModRm(0b11, reg, rm);
    }

    fun emitModRm(mode: Int, reg: Int, rm: Int) {
        assert(mode < 4 && reg < 8 && rm < 8);
        self.emitByte((mode << 6 | reg << 3 | rm).toByte());
    }

    fun emitSib(scale: Int, index: Int, base: Int) {
        assert(scale < 4);
        assert(index < 8);
        assert(base < 8);
        self.emitByte((scale << 6 | index << 3 | base).toByte());
    }

    fun emitByte(value: Byte) {
        self.buffer.emitByte(value);
    }

    fun emitInt(value: Int) {
        self.buffer.emitInt(value);
    }

    fun finalize() -> Array[Byte] {
        self.buffer.toArray()
    }

    setupAssemblerRegisters();
}

@test fun testAsmRegisters() {
    setupAssemblerRegisters();

    assert(RAX.lowBits() == 0 && !RAX.needsRexBit());
    assert(RCX.lowBits() == 1 && !RCX.needsRexBit());
    assert(RDX.lowBits() == 2 && !RDX.needsRexBit());
    assert(RBX.lowBits() == 3 && !RBX.needsRexBit());
    assert(RSP.lowBits() == 4 && !RSP.needsRexBit());
    assert(RBP.lowBits() == 5 && !RBP.needsRexBit());
    assert(RSI.lowBits() == 6 && !RSI.needsRexBit());
    assert(RDI.lowBits() == 7 && !RDI.needsRexBit());

    assert(R8.lowBits() == 0 && R8.needsRexBit());
    assert(R9.lowBits() == 1 && R9.needsRexBit());
    assert(R10.lowBits() == 2 && R10.needsRexBit());
    assert(R11.lowBits() == 3 && R11.needsRexBit());
    assert(R12.lowBits() == 4 && R12.needsRexBit());
    assert(R13.lowBits() == 5 && R13.needsRexBit());
    assert(R14.lowBits() == 6 && R14.needsRexBit());
    assert(R15.lowBits() == 7 && R15.needsRexBit());
}

@test fun testAsmPushqr(_x: Testing) {
    let asm = AssemblerX64();
    asm.pushqr(RAX);
    asm.pushqr(RSP);
    asm.pushqr(R8);
    asm.pushqr(R15);
    let buffer = asm.finalize();
    asm1(buffer, 0, 0x50Y);
    asm1(buffer, 1, 0x54Y);
    asm2(buffer, 2, 0x41Y, 0x50Y);
    asm2(buffer, 4, 0x41Y, 0x57Y);
    assert(buffer.length() == 6);
}

@test fun testAsmPopqr(_x: Testing) {
    let asm = AssemblerX64();
    asm.popqr(RAX);
    asm.popqr(RSP);
    asm.popqr(R8);
    asm.popqr(R15);
    let buffer = asm.finalize();
    asm1(buffer, 0, 0x58Y);
    asm1(buffer, 1, 0x5CY);
    asm2(buffer, 2, 0x41Y, 0x58Y);
    asm2(buffer, 4, 0x41Y, 0x5FY);
    assert(buffer.length() == 6);
}

@test fun testAsmMovqrr(_x: Testing) {
    let asm = AssemblerX64();
    asm.movqrr(R15, RAX);
    asm.movqrr(RAX, R15);
    let buffer = asm.finalize();
    asm3(buffer, 0, 0x49Y, 0x89Y, 0xC7Y);
    asm3(buffer, 3, 0x4CY, 0x89Y, 0xF8Y);
    assert(buffer.length() == 6);
}

@test fun testAsmAddqrr(_x: Testing) {
    let asm = AssemblerX64();
    asm.addqrr(RBX, RAX);
    asm.addqrr(R12, RAX);
    asm.addqrr(RAX, R12);
    asm.addqrr(RSP, R15);
    let buffer = asm.finalize();
    asm3(buffer, 0, 0x48Y, 0x01Y, 0xD8Y);
    asm3(buffer, 3, 0x4CY, 0x01Y, 0xE0Y);
    asm3(buffer, 6, 0x49Y, 0x01Y, 0xC4Y);
    asm3(buffer, 9, 0x49Y, 0x01Y, 0xE7Y);
    assert(buffer.length() == 12);
}

@test fun testAsmAddqri(_x: Testing) {
    let asm = AssemblerX64();
    asm.addqri(RAX, Immediate(0x11L));
    asm.addqri(R15, Immediate(0x11L));
    asm.addqri(RAX, Immediate(0x2211L));
    asm.addqri(RCX, Immediate(0x2211L));
    asm.addqri(R15, Immediate(0x2211L));
    let buffer = asm.finalize();
    asm4(buffer, 0, 0x48Y, 0x83Y, 0xC0Y, 0x11Y);
    asm4(buffer, 4, 0x49Y, 0x83Y, 0xC7Y, 0x11Y);
    asm2(buffer, 8, 0x48Y, 0x05Y);
    asmInt(buffer, 10, 0x2211);
    asm3(buffer, 14, 0x48Y, 0x81Y, 0xC1Y);
    asmInt(buffer, 17, 0x2211);
    asm3(buffer, 21, 0x49Y, 0x81Y, 0xC7Y);
    asmInt(buffer, 24, 0x2211);
    assert(buffer.length() == 28);
}

@test fun testAsmSubqrr(_x: Testing) {
    let asm = AssemblerX64();
    asm.subqrr(RBX, RAX);
    asm.subqrr(R15, RCX);
    let buffer = asm.finalize();
    asm3(buffer, 0, 0x48Y, 0x29Y, 0xD8Y);
    asm3(buffer, 3, 0x4CY, 0x29Y, 0xF9Y);
    assert(buffer.length() == 6);
}

@test fun testAsmSubqri(_x: Testing) {
    let asm = AssemblerX64();
    asm.subqri(RAX, Immediate(0x11L));
    asm.subqri(R15, Immediate(0x11L));
    asm.subqri(RAX, Immediate(0x2211L));
    asm.subqri(RCX, Immediate(0x2211L));
    asm.subqri(R15, Immediate(0x2211L));
    let buffer = asm.finalize();
    asm4(buffer, 0, 0x48Y, 0x83Y, 0xE8Y, 0x11Y);
    asm4(buffer, 4, 0x49Y, 0x83Y, 0xEFY, 0x11Y);
    asm2(buffer, 8, 0x48Y, 0x2DY);
    asmInt(buffer, 10, 0x2211);
    asm3(buffer, 14, 0x48Y, 0x81Y, 0xE9Y);
    asmInt(buffer, 17, 0x2211);
    asm3(buffer, 21, 0x49Y, 0x81Y, 0xEFY);
    asmInt(buffer, 24, 0x2211);
    assert(buffer.length() == 28);
}

@test fun testAsmRetq(_x: Testing) {
    let asm = AssemblerX64();
    asm.retq();
    let buffer = asm.finalize();
    asm1(buffer, 0, 0xC3Y);
    assert(buffer.length() == 1);
}

@test fun testAsmNop(_x: Testing) {
    let asm = AssemblerX64();
    asm.nop();
    let buffer = asm.finalize();
    asm1(buffer, 0, 0x90Y);
    assert(buffer.length() == 1);
}

fun asm1(buffer: Array[Byte], index: Int, val: Byte) {
    assert(buffer(index) == val);
}

fun asm2(buffer: Array[Byte], index: Int, val1: Byte, val2: Byte) {
    assert(buffer(index) == val1 && buffer(index+1) == val2);
}

fun asm3(buffer: Array[Byte], index: Int, val1: Byte, val2: Byte, val3: Byte) {
    assert(buffer(index) == val1 && buffer(index+1) == val2 && buffer(index+2) == val3);
}

fun asm4(buffer: Array[Byte], index: Int, val1: Byte, val2: Byte, val3: Byte, val4: Byte) {
    assert(buffer(index) == val1 && buffer(index+1) == val2 && buffer(index+2) == val3 && buffer(index+3) == val4);
}

fun asmInt(buffer: Array[Byte], index: Int, value: Int) {
    assert(buffer(index) == (value & 0xFF).toByte());
    assert(buffer(index+1) == ((value >>> 8) & 0xFF).toByte());
    assert(buffer(index+2) == ((value >>> 16) & 0xFF).toByte());
    assert(buffer(index+3) == ((value >>> 24) & 0xFF).toByte());
}

fun printBuffer(buffer: Array[Byte]) {
    var i = 0;

    while i < buffer.length() {
        let val = buffer(i);
        if i > 0 { print(" "); }
        print(intToHexString(val.toInt(), 2));
        i = i + 1;
    }
}

fun intToHexString(var val: Int, padding: Int) -> String {
    var result = "";

    while val != 0 {
        let digit = val & 0xF;
        val = val >>> 4;

        let digit = if digit < 10 {
            try! (48 + digit).toChar()
        } else {
            try! (65 + digit - 10).toChar()
        };

        result = "${digit}${result}";
    }

    if result.isEmpty() {
        result = "0";
    }

    while result.length() < padding {
        result = "0${result}";
    }

    return result;
}