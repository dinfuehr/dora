class AssemblerBuffer {
    let buffer: Vec[UInt8] = Vec[UInt8]();

    fun emitByte(value: UInt8) {
        self.buffer.push(value);
    }

    fun emitInt32(value: Int) {
        self.emitByte((value & 0xFF).toUInt8());
        self.emitByte(((value >>> 8) & 0xFF).toUInt8());
        self.emitByte(((value >>> 16) & 0xFF).toUInt8());
        self.emitByte(((value >>> 24) & 0xFF).toUInt8());
    }

    fun emitInt64(value: Int64) {
        self.emitInt32(value.toInt());
        self.emitInt32((value >>> 32).toInt());
    }

    fun length() -> Int {
        self.buffer.length()
    }

    fun capacity() -> Int {
        self.buffer.capacity()
    }

    fun toArray() -> Array[UInt8] {
        self.buffer.toArray()
    }
}

@test fun testAssemblerBufferEmitByte(_x: Testing) {
    let buffer = AssemblerBuffer();
    buffer.emitByte(0x01Y);
    buffer.emitByte(0x02Y);
    assert(buffer.length() == 2);
}

@test fun testAssemblerBufferEmitInt(_x: Testing) {
    let asm = AssemblerBuffer();
    asm.emitInt32(0x11223344);
    let buffer = asm.toArray();
    assert(buffer(0) == 0x44Y);
    assert(buffer(1) == 0x33Y);
    assert(buffer(2) == 0x22Y);
    assert(buffer(3) == 0x11Y);
    assert(buffer.length() == 4);
}

@open @abstract class SlowPath {
    @abstract fun emit(asm: Assembler);
}

@open @abstract class Assembler

class Register(let value: Int) {
    fun equals(rhs: Register) -> Bool {
        self.value == rhs.value
    }
}

fun assertAsm(buffer: Array[UInt8], start: Int, bytes: UInt8...) {
    var idx = 0;

    while idx < bytes.length() {
        let exp = bytes(idx);
        let got = buffer(start + idx);

        if exp != got {
            println("expected ${exp.toStringHex()} at index ${start + idx} but got ${got.toStringHex()}")
        }

        assert(exp == got);
        idx = idx + 1;
    }
}

fun assertAsmInt(buffer: Array[UInt8], index: Int, value: Int) {
    let b1 = buffer(index).toInt();
    let b2 = buffer(index+1).toInt();
    let b3 = buffer(index+2).toInt();
    let b4 = buffer(index+3).toInt();

    let got = (b4 << 24) | (b3 << 16) | (b2 << 8) | b1;

    if value != got {
        println("expected ${value.toStringHex()} but got ${got.toStringHex()}");
    }

    assert(value == got);
}

fun assertAsmInt64(buffer: Array[UInt8], index: Int, value: Int64) {
    assertAsmInt(buffer, index, value.toInt());
    assertAsmInt(buffer, index, (value >>> 32).toInt());
}

fun printBuffer(buffer: Array[UInt8]) {
    var i = 0;

    while i < buffer.length() {
        let val = buffer(i);
        if i > 0 { print(" "); }
        print(intToHexString(val.toInt(), 2));
        i = i + 1;
    }
}

fun intToHexString(var val: Int, padding: Int) -> String {
    var result = "";

    while val != 0 {
        let digit = val & 0xF;
        val = val >>> 4;

        let digit = if digit < 10 {
            (48 + digit).toChar().unwrap()
        } else {
            (65 + digit - 10).toChar().unwrap()
        };

        result = "${digit}${result}";
    }

    if result.isEmpty() {
        result = "0";
    }

    while result.length() < padding {
        result = "0${result}";
    }

    return result;
}