class BlockBuilder(let graph: Graph, let bc: BytecodeFunction) extends EmptyBytecodeVisitor {
    var offset: Int32 = 0;
    var currentBlock: Option[Block] = Option[Block]::None;
    var blockTerminated: Bool = false;
    var blocks: HashMap[Int32, Block] = HashMap[Int32, Block]();

    fun run() {
        let blockCreator = SsagenCreateBlocks(self.graph, self.bc, self.blocks);
        blockCreator.run();

        self.currentBlock = Option[Block]::Some(self.graph.getEntryBlock());

        // iterate all bytecodes
        readBytecode(self.bc.code, self);
    }

    fun blockCount(): Int32 {
        self.blocks.size().toInt32()
    }

    fun blockAt(offset: Int32): Option[Block] {
        self.blocks(offset)
    }

    fun nextBlockAt(offset: Int32): Option[Block] {
        var offset = offset.toInt64();
        while offset < self.bc.code.size() {
            let result = self.blockAt(offset.toInt32());
            if result.isSome() { return result; }
            offset = offset + 1L;
        }

        Option[Block]::None
    }

    fun markBlockTerminated() {
        self.blockTerminated = true;
    }

    @override fun visitInstruction(offset: Int32) {
        self.offset = offset;

        let result = self.blocks(offset);

        if result.isSome() {
            let nextBlock = result.unwrap();

            if !self.blockTerminated {
                self.currentBlock.unwrap().addSuccessor(nextBlock);
            }

            self.currentBlock = Option[Block]::Some(nextBlock);
        }

        self.blockTerminated = false;
    }

    @override fun visitJumpLoop(offset: Int32) {
        let targetBlock = self.blockAt(self.offset - offset).unwrap();
        self.currentBlock.unwrap().addSuccessor(targetBlock);
        self.markBlockTerminated();
    }

    @override fun visitJumpIfFalse(opnd: BytecodeRegister, offset: Int32) {
        let targetBlock = self.blockAt(self.offset + offset).unwrap();
        self.currentBlock.unwrap().addSuccessor(targetBlock);
    }
    @override fun visitJumpIfFalseConst(opnd: BytecodeRegister, idx: ConstPoolId) {
        let offset = self.bc.constPool(idx).toInt32().unwrap();
        let targetBlock = self.blockAt(self.offset + offset).unwrap();
        self.currentBlock.unwrap().addSuccessor(targetBlock);
    }
    @override fun visitJumpIfTrue(opnd: BytecodeRegister, offset: Int32) {
        let targetBlock = self.blockAt(self.offset + offset).unwrap();
        self.currentBlock.unwrap().addSuccessor(targetBlock);
    }
    @override fun visitJumpIfTrueConst(opnd: BytecodeRegister, idx: ConstPoolId) {
        let offset = self.bc.constPool(idx).toInt32().unwrap();
        let targetBlock = self.blockAt(self.offset + offset).unwrap();
        self.currentBlock.unwrap().addSuccessor(targetBlock);
    }
    @override fun visitJump(offset: Int32) {
        let targetBlock = self.blockAt(self.offset + offset).unwrap();
        self.currentBlock.unwrap().addSuccessor(targetBlock);
        self.markBlockTerminated();
    }
    @override fun visitJumpConst(idx: ConstPoolId) {
        let offset = self.bc.constPool(idx).toInt32().unwrap();
        let targetBlock = self.blockAt(self.offset + offset).unwrap();
        self.currentBlock.unwrap().addSuccessor(targetBlock);
        self.markBlockTerminated();
    }

    @override fun visitRetVoid() {
        self.currentBlock.unwrap().addSuccessor(self.graph.getExitBlock());
        self.markBlockTerminated();
    }

    @override fun visitRet(opnd: BytecodeRegister) {
        self.currentBlock.unwrap().addSuccessor(self.graph.getExitBlock());
        self.markBlockTerminated();
    }
}

class SsagenCreateBlocks(let graph: Graph, let bc: BytecodeFunction, let blocks: HashMap[Int32, Block]) extends EmptyBytecodeVisitor {
    var offset: Int32 = 0;
    var nextInstructionIsBlockStart: Bool = false;

    fun run() {
        let entryBlock = Block();
        self.graph.addBlock(entryBlock);
        self.graph.setEntryBlock(entryBlock);

        let exitBlock = Block();
        self.graph.addBlock(exitBlock);
        self.graph.setExitBlock(exitBlock);

        // create block for first instruction
        self.ensureBlockAt(0);

        // iterate all bytecodes
        readBytecode(self.bc.code, self);
    }

    fun ensureBlockAt(offset: Int32) {
        let result = self.blocks(offset);
        if result.isSome() { return; }

        let block = Block();
        self.graph.addBlock(block);
        self.blocks.insert(offset, block);
    }

    fun markNextInstructionAsBlockStart() {
        self.nextInstructionIsBlockStart = true;
    }

    @override fun visitInstruction(offset: Int32) {
        self.offset = offset;

        if self.nextInstructionIsBlockStart {
            self.ensureBlockAt(offset);
            self.nextInstructionIsBlockStart = false;
        }
    }

    @override fun visitJumpLoop(offset: Int32) {
        self.ensureBlockAt(self.offset - offset);
    }

    @override fun visitJumpIfFalse(opnd: BytecodeRegister, offset: Int32) {
        self.ensureBlockAt(self.offset + offset);
        self.markNextInstructionAsBlockStart();
    }
    @override fun visitJumpIfFalseConst(opnd: BytecodeRegister, idx: ConstPoolId) {
        let offset = self.bc.constPool(idx).toInt32().unwrap();
        self.ensureBlockAt(self.offset + offset);
        self.markNextInstructionAsBlockStart();
    }
    @override fun visitJumpIfTrue(opnd: BytecodeRegister, offset: Int32) {
        self.ensureBlockAt(self.offset + offset);
        self.markNextInstructionAsBlockStart();
    }
    @override fun visitJumpIfTrueConst(opnd: BytecodeRegister, idx: ConstPoolId) {
        let offset = self.bc.constPool(idx).toInt32().unwrap();
        self.ensureBlockAt(self.offset + offset);
        self.markNextInstructionAsBlockStart();
    }
    @override fun visitJump(offset: Int32) {
        self.ensureBlockAt(self.offset + offset);
        self.markNextInstructionAsBlockStart();
    }
    @override fun visitJumpConst(idx: ConstPoolId) {
        let offset = self.bc.constPool(idx).toInt32().unwrap();
        self.ensureBlockAt(self.offset + offset);
        self.markNextInstructionAsBlockStart();
    }

    @override fun visitRetVoid() {
        self.markNextInstructionAsBlockStart();
    }

    @override fun visitRet(opnd: BytecodeRegister) {
        self.markNextInstructionAsBlockStart();
    }
}