class GraphBuilder {
    let graph: Graph = Graph();
    var insertion: Option[Block] = None;
    var entryBlock: Option[Block] = None;

    fun addBlock(): Block {
        let block = Block();

        if self.entryBlock.isNone() {
            self.entryBlock = Some(block);
            self.graph.setEntryBlock(block);
        }

        self.graph.addBlock(block);
        self.insertion = Some(block);
        block
    }

    fun createInt32Const(value: Int32): Inst {
        let inst = Int32Const(value);
        self.appendInst(inst)
    }

    fun createAdd(lhs: Inst, rhs: Inst, ty: Type): Inst {
        let inst = BinaryInst(BinOp::Add, ty, lhs, rhs);
        self.appendInst(inst)
    }

    fun createReturn(inst: Inst, ty: Type): Inst {
        let inst = ReturnInst(inst, ty);
        self.appendInst(inst)
    }

    fun createReturnVoid(): Inst {
        let inst = ReturnVoidInst();
        self.appendInst(inst)
    }

    fun createGoto(block: Block): Inst {
        let inst = GotoInst(block);
        self.appendInst(inst)
    }

    fun appendInst(inst: Inst): Inst {
        self.insertion.unwrap().appendInst(inst);

        if inst.isTerminator() {
            self.insertion = None;
        }

        inst
    }

    fun build(): Graph {
        assert(self.entryBlock.isSome());

        self.graph
    }
}

@test fun testEmptyGraph(_x: Testing) {
    let builder = GraphBuilder();
    let block = builder.addBlock();
    let value = builder.createInt32Const(12);
    builder.createReturn(value, Type::Int32);

    assertGraph(builder.build(), "
@0:
  %0: Const.Int32(#12) [%1]
  %1: Ret.Int32 %0
");
}
