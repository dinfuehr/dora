fun findBackEdges(graph: Graph) {
    // worklist contains current path in the graph
    let worklist = Vec[Block]();

    // number of successors visited per block
    let successorsVisited = Array::new[Int64](graph.numberBlocks().toInt64());

    // all blocks that have been fully visited (including successors)
    let visited = BitSet(graph.numberBlocks().toInt64());

    // all blocks in worklist
    let visiting = BitSet(graph.numberBlocks().toInt64());

    worklist.push(graph.getEntryBlock());
    visiting.insert(graph.getEntryBlock().id.toInt64());

    while !worklist.isEmpty() {
        let block = worklist.last().unwrap();
        let successorIndex = successorsVisited(block.id.toInt64());

        if successorIndex == block.successors.size() {
            worklist.pop();
            visiting.remove(block.id.toInt64());
            visited.insert(block.id.toInt64());
        } else {
            successorsVisited(block.id.toInt64()) = successorIndex + 1L;
            let successor = block.successors.get(successorIndex);

            if visiting.contains(successor.id.toInt64()) {
                successor.addBackEdge(block);
            } else if visited.contains(successor.id.toInt64()) {
                // already visited
            } else {
                worklist.push(successor);
                visiting.insert(successor.id.toInt64());
            }
        }
    }

    let it = graph.insertionOrderIterator();
    while it.hasNext() {
        let block = it.next();
        // assume for now that all blocks are reachable
        assert(visited.contains(block.id.toInt64()));
    }
}

fun computePostOrder(graph: Graph) {
    let postOrder = Vec[Block]();
    postOrder.reserve(graph.numberBlocks().toInt64());

    // worklist contains current path in the graph
    let worklist = Vec[Block]();

    // number of successors visited per block
    let successorsVisited = Array::new[Int64](graph.numberBlocks().toInt64());

    // all blocks that have been visited
    let visited = BitSet(graph.numberBlocks().toInt64());

    worklist.push(graph.getEntryBlock());

    while !worklist.isEmpty() {
        let block = worklist.last().unwrap();
        let successorIndex = successorsVisited(block.id.toInt64());

        if successorIndex == block.successors.size() {
            worklist.pop();
            postOrder.push(block);
        } else {
            successorsVisited(block.id.toInt64()) = successorIndex + 1L;
            let successor = block.successors.get(successorIndex);

            if visited.contains(successor.id.toInt64()) {
                // block either already visited or back edge
            } else {
                worklist.push(successor);
                visited.insert(successor.id.toInt64());
            }
        }
    }

    graph.setPostOrder(postOrder);
}

fun computeDominators(graph: Graph) {
    // worklist contains current path in the graph
    let worklist = Vec[Block]();

    // number of successors visited per block
    let successorsVisited = Array::new[Int64](graph.numberBlocks().toInt64());

    // all blocks that have been fully visited (including successors)
    let visited = BitSet(graph.numberBlocks().toInt64());

    // all blocks in worklist
    let visiting = BitSet(graph.numberBlocks().toInt64());

    worklist.push(graph.getEntryBlock());
    visiting.insert(graph.getEntryBlock().id.toInt64());

    while !worklist.isEmpty() {
        let block = worklist.last().unwrap();
        let successorIndex = successorsVisited(block.id.toInt64());

        if successorIndex == block.successors.size() {
            worklist.pop();
            visiting.remove(block.id.toInt64());
            visited.insert(block.id.toInt64());
        } else {
            successorsVisited(block.id.toInt64()) = successorIndex + 1L;
            let successor = block.successors.get(successorIndex);

            if visiting.contains(successor.id.toInt64()) {
                // ignore back-edges
            } else if !visited.contains(successor.id.toInt64()) {
                // first time we see this block
                successor.setDominator(block);
                worklist.push(successor);
                visited.insert(successor.id.toInt64());
            } else {
                updateDominator(block, successor);
            }
        }
    }

    // assert that we have a reducible graph
    let it = graph.insertionOrderIterator();
    while it.hasNext() {
        let block = it.next();

        if block.isLoopHeader() {
            let loopHeaderBlock = block;
            let loopHeader = loopHeaderBlock.getLoopHeader().unwrap();

            let it = loopHeader.backEdges.makeIterator();
            while it.hasNext() {
                let block = it.next();
                assert(loopHeaderBlock.dominates(block));
            }
        }
    }

    let it = graph.reversePostOrderIterator();
    while it.hasNext() {
        let block = it.next();

        if block.isEntryBlock() {
            continue;
        }

        let dom = block.getDominator().unwrap();
        dom.addDominatedBlock(block);
    }
}

fun updateDominator(block: Block, successor: Block) -> Bool {
    let old_dominator = successor.getDominator().unwrap();
    let new_dominator = commonDominator(old_dominator, block);

    if old_dominator != new_dominator {
        successor.setDominator(new_dominator);
        true
    } else {
        false
    }
}

fun commonDominator(b1: Block, b2: Block) -> Block {
    var finger1 = b1;
    var finger2 = b2;

    while finger1 != finger2 {
        while finger1.postOrderIdx < finger2.postOrderIdx {
            finger1 = finger1.getDominator().unwrap();
        }

        while finger2.postOrderIdx < finger1.postOrderIdx {
            finger2 = finger2.getDominator().unwrap();
        }
    }

    finger1
}
