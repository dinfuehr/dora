use std::CodepointIterator;
use std::StringBuffer;

use package::assembler::arm64::registerName as registerNameOnArm64;
use package::assembler::x64::registerName as registerNameOnX64;
use package::assembler::arm64::floatRegisterName as floatRegisterNameOnArm64;
use package::assembler::x64::floatRegisterName as floatRegisterNameOnX64;
use package::graph::{Block, Edge, Graph, Input, Inst, Location, LocationData, Operand, Op, Policy};
use package::interface::Architecture;

pub fn dumpGraph(graph: Graph, architecture: Architecture): String {
    GraphDumper::new(graph, architecture).dump()
}

class GraphDumper {
    graph: Graph,
    architecture: Architecture,
    buffer: StringBuffer,
}

impl GraphDumper {
    static fn new(graph: Graph, architecture: Architecture): GraphDumper {
        GraphDumper(graph, architecture, StringBuffer::new())
    }

    fn dump(): String {
        for block in self.graph.insertionOrderIterator() {
            self.dumpBlock(block);
        }

        self.buffer.toString()
    }

    fn dumpBlock(block: Block) {
        self.buffer.append("@${block.id()}:");

        self.dumpEdgeList(true, block.predecessors);
        self.dumpEdgeList(false, block.successors);
        self.dumpLiveIn(block);

        self.buffer.appendNewline();

        for inst in block.phisIterator() {
            self.dumpInst(inst);
        }

        for inst in block.instructionsIterator() {
            self.dumpInst(inst);
        }
    }

    fn dumpLiveIn(block: Block) {
        if !block.hasLiveIn() {
            return;
        }

        let liveIn = block.getLiveIn();
        self.buffer.append(" liveIn = [");
        let mut first: Bool = true;

        for entry in liveIn {
            if !first {
                self.buffer.append(", ");
            }

            self.buffer.append("%${entry}");
            first = false;
        }

        self.buffer.append("]");
    }

    fn dumpEdgeList(is_pred: Bool, edges: Vec[Edge]) {
        if edges.isEmpty() {
            return;
        }

        let name = if is_pred { "pred" } else { "succ" };
        self.buffer.append(" ${name} = [");
        let mut first: Bool = true;

        for edge in edges {
            if !first {
                self.buffer.append(", ");
            }

            let block = if is_pred {
                edge.source
            } else {
                edge.target
            };
            self.buffer.append("@${block.id()}");

            if edge.isBackward() {
                self.buffer.append(" (BACK)");
            }

            first = false;
        }

        self.buffer.append("]");
    }

    fn dumpInst(inst: Inst) {
        self.buffer.append("  %${inst.id()}");

        if inst.hasLinearOrderIndex() {
            self.buffer.append("/#${inst.getLinearOrderIndex()}");
        }

        self.buffer.append(": ");
        self.dumpOutputLocation(inst);
        self.buffer.append("${dumpInstName(inst)}");

        if !inst.getInputs().isEmpty() {
            let mut idx = 0;
            self.buffer.append(" ");

            for inp in inst.getInputs() {
                if idx > 0 {
                    self.buffer.append(", ");
                }

                self.buffer.append("%${inp.value.id()}");
                self.dumpInputLocation(inst, idx);
                idx = idx + 1;
            }
        } else if inst.op() == Op::Move {
            let loc = inst.getLocationData();
            self.buffer.append(" ");
            self.dumpOperand(loc.getInput(0));
        }

        if inst.hasLocationData() {
            let loc = inst.getLocationData();
            if !loc.getTemps().isEmpty() {
                self.buffer.append(" (temps: ");
                let mut first = true;
                for temp in loc.getTemps() {
                    if !first { self.buffer.append(", "); }
                    self.dumpOperand(temp);
                    first = false;
                }
                self.buffer.append(")");
            }

            if !loc.getHint().isNone() {
                self.buffer.append(" (hint ${loc.getHint()})");
            }
        }

        if inst.hasUses() {
            self.buffer.append(" [");
            let mut first = true;

            for input in inst.uses() {
                if !first {
                    self.buffer.append(", ");
                }

                self.buffer.append("%${input.usedBy.id()}");
                first = false;
            }

            self.buffer.append("]");
        }

        if inst.hasLiveRange() {
            let range = inst.getLiveRange();
            self.buffer.append(" (live: #${range.start}-#${range.end})");
        }

        self.buffer.appendNewline();
    }

    fn dumpOutputLocation(inst: Inst) {
        if !inst.hasLocationData() {
            return;
        }

        let loc = inst.getLocationData();

        if loc.hasOutput() {
            self.dumpOperand(loc.getOutput());
            if loc.hasSpillSlot() {
                self.buffer.append(" / FP${loc.getSpillSlot()}");
            }
            self.buffer.append(" <- ");
        }
    }

    fn dumpInputLocation(inst: Inst, idx: Int64) {
        if !inst.hasLocationData() {
            return;
        }

        let loc = inst.getLocationData();

        if !loc.hasInputAt(idx) {
            return;
        }

        self.buffer.append(" in ");
        self.dumpOperand(loc.getInput(idx));
    }

    fn dumpOperand(operand: Operand) {
        let policy = match operand.getPolicy() {
            Policy::None => "NO",
            Policy::AnyReg => "GP",
            Policy::AnyFloatReg => "FP",
            Policy::FixedReg => "FIX_GP",
            Policy::FixedFloatReg => "FIX_FP",
            Policy::SameAsFirstInput => "SAFI",
            Policy::FixedStack => "FIX_STACK",
            Policy::FixedFloatStack => "FIX_FLOAT_STACK",
        };

        let name = match operand.getLocation() {
            Location::Reg(reg) => {
                match self.architecture {
                    Architecture::Arm64 => registerNameOnArm64(reg),
                    Architecture::X64 => registerNameOnX64(reg),
                }
            },
            Location::FloatReg(reg) => {
                match self.architecture {
                    Architecture::Arm64 => floatRegisterNameOnArm64(reg),
                    Architecture::X64 => floatRegisterNameOnX64(reg),
                }
            },

            Location::Stack(slot) => {
                if slot.isFp() {
                    "FP:${slot.getOffset()}"
                } else {
                    "SP:${slot.getOffset()}"
                }
            },

            Location::None => "NONE",
        };

        self.buffer.append("${policy}:${name}");
    }
}

fn dumpInstName(inst: Inst): String {
    let op = inst.op();
    match op {
        Op::NullConst => "Const.Null",
        Op::TrueConst => "Const.True",
        Op::FalseConst => "Const.False",
        Op::UInt8Const => "Const.UInt8(#${inst.getValueAsUInt8()})",
        Op::Int32Const => "Const.Int32(#${inst.getValueAsInt32()})",
        Op::Int64Const => "Const.Int64(#${inst.getValueAsInt64()})",
        Op::Float32Const => "Const.Float32(#${inst.getValueAsFloat32()})",
        Op::Float64Const => "Const.Float64(#${inst.getValueAsFloat64()})",
        Op::StringConst => "Const.String('${inst.getValueAsString()}')",
        Op::EnsureGlobalInitialized => "EnsureGlobalInitialized(${inst.getGlobalId().value})",
        Op::LoadGlobal => "LoadGlobal.${inst.getValueType()}(${inst.getGlobalId().value})",
        Op::StoreGlobal => "StoreGlobal.${inst.getValueType()}(${inst.getGlobalId().value})",
        Op::MarkGlobalInitialized => "MarkGlobalInitialized(${inst.getGlobalId().value})",
        Op::Undef => "Undef",
        Op::BoundsCheck => "BoundsCheck",
        Op::Goto => {
            "Goto(@${inst.getTargetBlock().id()})"
        },
        Op::If => {
            "If(@${inst.getTrueBlock().id()}, @${inst.getFalseBlock().id()})"
        },
        Op::Return => {
            if !inst.hasInput() {
                "Ret.Void"
            } else {
                "Ret.${inst.getOperationType()}"
            }
        },
        Op::Neg => "Neg.${inst.getValueType()}",
        Op::CheckedNeg => "CheckedNeg.${inst.getValueType()}",
        Op::Not => "Not.${inst.getValueType()}",
        Op::Add => "Add.${inst.getValueType()}",
        Op::CheckedAdd => "CheckedAdd.${inst.getValueType()}",
        Op::Sub => "Sub.${inst.getValueType()}",
        Op::CheckedSub => "CheckedSub.${inst.getValueType()}",
        Op::Mul => "Mul.${inst.getValueType()}",
        Op::CheckedMul => "CheckedMul.${inst.getValueType()}",
        Op::Div => "Div.${inst.getValueType()}",
        Op::CheckedDiv => "CheckedDiv.${inst.getValueType()}",
        Op::Mod => "Mod.${inst.getValueType()}",
        Op::CheckedMod => "CheckedMod.${inst.getValueType()}",
        Op::And => "And.${inst.getValueType()}",
        Op::Or => "Or.${inst.getValueType()}",
        Op::Xor => "Xor.${inst.getValueType()}",
        Op::Shl => "Shl.${inst.getValueType()}",
        Op::Shr => "Shr.${inst.getValueType()}",
        Op::Sar => "Sar.${inst.getValueType()}",
        Op::InvokeStatic => "InvokeStatic.${inst.getValueType()}",
        Op::InvokeDirect => "InvokeDirect.${inst.getValueType()}",
        Op::InvokeVirtual => "InvokeVirtual.${inst.getValueType()}",
        Op::InvokeLambda => "InvokeLambda.${inst.getValueType()}",
        Op::TestIdentity => "TestIdentity.${inst.getValueType()}",
        Op::Equal => "Equal.${inst.getOperationType()}",
        Op::NotEqual => "NotEqual.${inst.getOperationType()}",
        Op::Greater => "Greater.${inst.getOperationType()}",
        Op::GreaterOrEqual => "GreaterOrEqual.${inst.getOperationType()}",
        Op::Less => "Less.${inst.getOperationType()}",
        Op::LessOrEqual => "LessOrEqual.${inst.getOperationType()}",
        Op::Phi => "Phi.${inst.getValueType()}",
        Op::DivZeroCheck => "DivZeroCheck",
        Op::ArrayLength => "ArrayLength",
        Op::LoadField => "LoadField.${inst.getValueType()}",
        Op::StoreField => "StoreField",
        Op::LoadArray => "LoadArray.${inst.getValueType()}",
        Op::StoreArray => "StoreArray",
        Op::Arg => "Arg.${inst.getValueType()}(${inst.getArgIndex()})",
        Op::Move => "Move.${inst.getValueType()}",
        Op::MoveImmediate => "MoveImmediate.${inst.getValueType()}",
        Op::NewObject => "NewObject",
        Op::NewArray => "NewArray",
        Op::NewLambda => "NewLambda",
        Op::AllocateStack => "AllocateStack",
        Op::LoadMemory => "LoadMemory",
        Op::StoreMemory => "StoreMemory(${inst.getOffset()})",
        Op::ThreadCurrent => "ThreadCurrent",
    }
}

pub fn assertGraph(graph: Graph, output: String) {
    let graphOutput = dumpGraph(graph, Architecture::Arm64);

    if !compareStrings(graphOutput, output) {
        println("got: \"${graphOutput}\"");
        println("expected: \"${output}\"");

        for idx in std::range(0, graphOutput.size()) {
            let value = graphOutput.getByte(idx);
            print("${value.toInt32().toStringHex()} ");
        }

        println("");

        for idx in std::range(0, output.size()) {
            let value = output.getByte(idx);
            print("${value.toInt32().toStringHex()} ");
        }

        println("");

        std::fatalError("output does not match");
    }
}

fn compareStrings(lhs: String, rhs: String): Bool {
    let mut lhs = Output::new(lhs);
    let mut rhs = Output::new(rhs);

    skipWhite(lhs);
    skipWhite(rhs);

    while true {
        if lhs.end() && rhs.end() {
            return true;
        } else if lhs.end() || rhs.end() {
            return false;
        }

        if lhs.current == rhs.current {
            lhs.advance();
            rhs.advance();
            continue;
        }

        if lhs.current == 0xDu8.toChar() && lhs.next == 0xAu8.toChar() {
            lhs.advance();
        }

        if rhs.current == 0xDu8.toChar() && rhs.next == 0xAu8.toChar() {
            rhs.advance();
        }

        if lhs.current != rhs.current {
            return false;
        }

        lhs.advance();
        rhs.advance();
    }

    false
}

class Output {
    iterator: CodepointIterator,
    current: Char,
    next: Char,
}

impl Output {
    static fn new(text: String): Output {
        let output = Output(
            CodepointIterator::new(text),
            '\0',
            '\0',
        );

        output.advance();
        output.advance();

        output
    }

    fn advance() {
        self.current = self.next;
        self.next = self.iterator.next().unwrapOr('\0');
    }

    fn end(): Bool {
        self.current == '\0'
    }
}

fn skipWhite(it: Output) {
    while isWhite(it.current) {
        it.advance();
    }
}

fn isWhite(ch: Char): Bool {
    ch == ' ' || ch == '\t' || ch == '\n' || ch == '\r'
}
