use std::CodepointIterator;
use std::StringBuffer;

use package::graph::Graph;
use package::graph::Block;
use package::graph::Inst;
use package::graph::Op;

@pub fn dumpGraph(graph: Graph): String {
    GraphDumper::new(graph).dump()
}

class_new GraphDumper {
    graph: Graph,
    buffer: StringBuffer,
}

impl GraphDumper {
    @static fn new(graph: Graph): GraphDumper {
        GraphDumper(graph, StringBuffer::new())
    }

    fn dump(): String {
        for block in self.graph.insertionOrderIterator() {
            self.dumpBlock(block);
        }

        self.buffer.toString()
    }

    fn dumpBlock(block: Block) {
        self.buffer.append("@${block.id()}:");

        if !block.predecessors.isEmpty() {
            self.buffer.append(" pred = [");
            var first: Bool = true;

            for pred in block.predecessors {
                if first {
                    self.buffer.append("@${pred.source.id()}");
                } else {
                    self.buffer.append(", @${pred.source.id()}");
                }

                first = false;
            }

            self.buffer.append("]");
        }

        if !block.successors.isEmpty() {
            self.buffer.append(" succ = [");
            var first: Bool = true;

            for succ in block.successors {
                if first {
                    self.buffer.append("@${succ.target.id()}");
                } else {
                    self.buffer.append(", @${succ.target.id()}");
                }

                first = false;
            }

            self.buffer.append("]");
        }

        self.buffer.appendNewline();

        for inst in block.phisIterator() {
            self.dumpInst(inst);
        }

        for inst in block.instructionsIterator() {
            self.dumpInst(inst);
        }
    }

    fn dumpInst(inst: Inst) {
        self.buffer.append("  %${inst.id()}: ${dumpInstName(inst)}");

        if !inst.getInputs().isEmpty() {
            var first = true;

            for inp in inst.getInputs() {
                if first {
                    self.buffer.append(" %${inp.value.id()}");
                } else {
                    self.buffer.append(", %${inp.value.id()}");
                }

                first = false;
            }
        }

        if inst.hasUses() {
            self.buffer.append(" [");
            var first = true;

            for input in inst.uses() {
                if first {
                    self.buffer.append("%${input.usedBy.id()}");
                } else {
                    self.buffer.append(", %${input.usedBy.id()}");
                }

                first = false;
            }

            self.buffer.append("]");
        }

        self.buffer.appendNewline();
    }
}

fn dumpInstName(inst: Inst): String {
    let op = inst.op();
    match op {
        Op::NullConst => "Const.Null",
        Op::Int32Const => "Const.Int32(#${inst.auxAsInt32()})",
        Op::Int64Const => "Const.Int64(#${inst.auxAsInt64()})",
        Op::Float32Const => "Const.Float32(#${inst.auxAsFloat32()})",
        Op::Float64Const => "Const.Float64(#${inst.auxAsFloat64()})",
        Op::LoadGlobal => "LoadGlobal.${inst.getType()}(${inst.auxAsInt32()})",
        Op::StoreGlobal => "StoreGlobal.${inst.getType()}(${inst.auxAsInt32()})",
        Op::Undef => "Undef",
        Op::BoundsCheck => "BoundsCheck",
        Op::Goto => {
            "Goto(@${inst.getTargetBlock().id()})"
        },
        Op::If => {
            "If(@${inst.getTrueBlock().id()}, @${inst.getFalseBlock().id()})"
        },
        Op::Return => {
            if inst.getType().isUnit() {
                "Ret.Void"
            } else {
                "Ret.${inst.getType()}"
            }
        },
        Op::Neg => "Neg.${inst.getType()}",
        Op::Not => "Not.${inst.getType()}",
        Op::Add => "Add.${inst.getType()}",
        Op::Sub => "Sub.${inst.getType()}",
        Op::Mul => "Mul.${inst.getType()}",
        Op::Div => "Div.${inst.getType()}",
        Op::Mod => "Mod.${inst.getType()}",
        Op::And => "And.${inst.getType()}",
        Op::Or => "Or.${inst.getType()}",
        Op::Xor => "Xor.${inst.getType()}",
        Op::Shl => "Shl.${inst.getType()}",
        Op::Shr => "Shr.${inst.getType()}",
        Op::Sar => "Sar.${inst.getType()}",
        Op::TestIdentity => "TestIdentity.${inst.getType()}",
        Op::Equal => "Equal.${inst.getType()}",
        Op::NotEqual => "NotEqual.${inst.getType()}",
        Op::Greater => "Greater.${inst.getType()}",
        Op::GreaterOrEqual => "GreaterOrEqual.${inst.getType()}",
        Op::Less => "Less.${inst.getType()}",
        Op::LessOrEqual => "LessOrEqual.${inst.getType()}",
        Op::Phi => "Phi",
        Op::DivZeroCheck => "DivZeroCheck",
        Op::ArrayGet => "ArrayGet.${inst.getType()}",
        Op::ArraySet => "ArraySet",
        Op::ArrayLength => "ArrayLength",
        Op::FieldGet => "FieldGet.${inst.getType()}",
        Op::FieldSet => "FieldSet",
        Op::Arg => "Arg.${inst.getType()}(${inst.auxAsInt32()})",
    }
}

@pub fn assertGraph(graph: Graph, output: String) {
    let graphOutput = dumpGraph(graph);

    if !compareStrings(graphOutput, output) {
        println("got: \"${graphOutput}\"");
        println("expected: \"${output}\"");

        for idx in std::range(0i32, graphOutput.size().toInt32()) {
            let value = graphOutput.getByte(idx.toInt64());
            print("${value.toInt32().toStringHex()} ");
        }

        println("");

        for idx in std::range(0i32, output.size().toInt32()) {
            let value = output.getByte(idx.toInt64());
            print("${value.toInt32().toStringHex()} ");
        }

        println("");

        std::fatalError("output does not match");
    }
}

fn compareStrings(lhs: String, rhs: String): Bool {
    var lhs = Output::new(lhs);
    var rhs = Output::new(rhs);

    skipWhite(lhs);
    skipWhite(rhs);

    while true {
        if lhs.end() && rhs.end() {
            return true;
        } else if lhs.end() || rhs.end() {
            return false;
        }

        if lhs.current == rhs.current {
            lhs.advance();
            rhs.advance();
            continue;
        }

        if lhs.current == 0xDu8.toChar() && lhs.next == 0xAu8.toChar() {
            lhs.advance();
        }

        if rhs.current == 0xDu8.toChar() && rhs.next == 0xAu8.toChar() {
            rhs.advance();
        }

        if lhs.current != rhs.current {
            return false;
        }

        lhs.advance();
        rhs.advance();
    }

    false
}

class_new Output {
    iterator: CodepointIterator,
    current: Char,
    next: Char,
}

impl Output {
    @static fn new(text: String): Output {
        let output = Output(
            CodepointIterator::new(text),
            '\0',
            '\0',
        );

        output.advance();
        output.advance();

        output
    }

    fn advance() {
        self.current = self.next;
        self.next = self.iterator.next().unwrapOr('\0');
    }

    fn end(): Bool {
        self.current == '\0'
    }
}

fn skipWhite(it: Output) {
    while isWhite(it.current) {
        it.advance();
    }
}

fn isWhite(ch: Char): Bool {
    ch == ' ' || ch == '\t' || ch == '\n' || ch == '\r'
}
