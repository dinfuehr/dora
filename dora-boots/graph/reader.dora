use std::StringBuffer;
use package::graph::Graph;

fn readGraph(data: String): Graph {
    unreachable[Graph]()
}

class GraphReader(let content: String) {
    let lexer: Lexer = Lexer(content);

    fn read() {

    }
}

class Lexer(let content: String) {
    var idx: Int64 = 0i64;

    fn look(): Option[Char] {
        if self.idx == self.content.size() {
            return None;
        }

        Some(self.content.getByte(self.idx).toChar())
    }

    fn readChar() {
        if self.idx == self.content.size() {
            std::fatalError("already at end of file");
        } else {
            self.idx = self.idx + 1i64;
        }
    }

    fn readToken(): Token {
        while true {
            self.skipWhitespace();

            let char = self.look();

            if char.isNone() {
                return Token::End;
            }

            if isIdentifierStart(char) {
                return self.readIdentifier();
            } else if isNewline(char) {
                return self.readNewline();
            } else if isOperator(char) {
                return self.readOperator();
            } else if isDigit(char) {
                return self.readNumber();
            } else {
                std::fatalError("unknown character ${char.getOrPanic()}");
            }
        }

        unreachable[Token]()
    }

    fn skipWhitespace() {
        while isWhitespace(self.look()) {
            self.readChar();
        }
    }

    fn readIdentifier(): Token {
        let buffer = StringBuffer::empty();

        while isIdentifierStart(self.look()) || isDigit(self.look()) {
            let look = self.look().getOrPanic();
            buffer.appendChar(look);
            self.readChar();
        }

        Token::Ident(buffer.toString())
    }

    fn readNewline(): Token {
        let ch = self.look().getOrPanic();
        assert(ch == '\n');
        self.readChar();

        Token::Newline
    }

    fn readOperator(): Token {
        let ch = self.look().getOrPanic();
        self.readChar();

        if ch == ':' {
            Token::Colon
        } else if ch == '=' {
            Token::Eq
        } else if ch == '%' {
            Token::Percent
        } else if ch == '@' {
            Token::At
        } else {
            unreachable[Token]()
        }
    }

    fn readNumber(): Token {
        let buffer = StringBuffer::empty();

        while isDigit(self.look()) {
            let look = self.look().getOrPanic();
            buffer.appendChar(look);
            self.readChar();
        }

        let value = buffer.toString().toInt64().getOrPanic();
        Token::Number(value)
    }
}

fn isWhitespace(ch: Option[Char]): Bool {
    if ch.isNone() {
        return false;
    }

    let ch = ch.getOrPanic();

    ch == ' ' || ch == '\t'
}

fn isOperator(ch: Option[Char]): Bool {
    if ch.isNone() {
        return false;
    }

    let ch = ch.getOrPanic();

    ch == ':' || ch == '=' || ch == '%' || ch == '@'
}

fn isIdentifierStart(ch: Option[Char]): Bool {
    if ch.isNone() {
        return false;
    }

    let ch = ch.getOrPanic();

    (ch >= 'a' && ch <= 'z') || (ch >= 'A' && ch <= 'Z') || ch == '_'
}

fn isDigit(ch: Option[Char]): Bool {
    if ch.isNone() {
        return false;
    }

    let ch = ch.getOrPanic();

    (ch >= '0' && ch <= '9')
}

fn isNewline(ch: Option[Char]): Bool {
    if ch.isNone() {
        return false;
    }

    ch.getOrPanic() == '\n'
}

enum Token {
    End,
    Ident(String),
    Number(Int64),
    Newline,
    Colon,
    Eq,
    At,
    Percent,
}

mod tests {
    use super::Token;
    use super::Lexer;

    @Test
    fn readSingleIdentifier() {
        let lexer = Lexer("foo");
        assertIdent(lexer, "foo");
        assertEnd(lexer);
    }

    @Test
    fn readMultipleIdentifiers() {
        let lexer = Lexer("foo bar baz");
        assertIdent(lexer, "foo");
        assertIdent(lexer, "bar");
        assertIdent(lexer, "baz");
        assertEnd(lexer);
    }

    @Test
    fn readMultipleLines() {
        let lexer = Lexer("foo\nbar\nbaz");
        assertIdent(lexer, "foo");
        assertNewline(lexer);
        assertIdent(lexer, "bar");
        assertNewline(lexer);
        assertIdent(lexer, "baz");
        assertEnd(lexer);
    }

    @Test
    fn readOperators() {
        let lexer = Lexer(":=@%");
        assertColon(lexer);
        assertEq(lexer);
        assertAt(lexer);
        assertPercent(lexer);
        assertEnd(lexer);
    }

    @Test
    fn readNumbers() {
        let lexer = Lexer("11212 17");
        assertNumber(lexer, 11212i64);
        assertNumber(lexer, 17i64);
        assertEnd(lexer);
    }

    fn assertIdent(lexer: Lexer, ident: String) {
        let token = lexer.readToken();

        match token {
            Token::Ident(value) => {
                assert(value == ident);
            },

            _ => {
                std::fatalError("identifier expected");
            }
        }
    }

    fn assertNumber(lexer: Lexer, number: Int64) {
        let token = lexer.readToken();

        match token {
            Token::Number(value) => {
                assert(value == number);
            },

            _ => {
                std::fatalError("number expected");
            }
        }
    }

    fn assertNewline(lexer: Lexer) {
        let token = lexer.readToken();

        match token {
            Token::Newline => {},
            _ => {
                std::fatalError("newline expected");
            }
        }
    }

    fn assertEq(lexer: Lexer) {
        let token = lexer.readToken();

        match token {
            Token::Eq => {},
            _ => {
                std::fatalError("eq expected");
            }
        }
    }

    fn assertColon(lexer: Lexer) {
        let token = lexer.readToken();

        match token {
            Token::Colon => {},
            _ => {
                std::fatalError("eq expected");
            }
        }
    }

    fn assertAt(lexer: Lexer) {
        let token = lexer.readToken();

        match token {
            Token::At => {},
            _ => {
                std::fatalError("at expected");
            }
        }
    }

    fn assertPercent(lexer: Lexer) {
        let token = lexer.readToken();

        match token {
            Token::Percent => {},
            _ => {
                std::fatalError("percent expected");
            }
        }
    }

    fn assertEnd(lexer: Lexer) {
        let token = lexer.readToken();

        match token {
            Token::End => {},
            _ => {
                std::fatalError("end expected");
            }
        }
    }
}