use std::io::{File, OpenFile};

use package::bytecode::{BytecodeDumper, BytecodeFunction, BytecodeInstructionInfo, BytecodeIterator, BytecodeRegister, ConstPoolId, dumpInstruction, GlobalId};
use package::graph::{Block, Graph, Inst, Op};
use package::graph_builder::BytecodeAnalysis;
use package::compilation::CompilationInfo;

pub fn createHtmlWriter(ci: CompilationInfo): HtmlWriter {
    let file = File::create("ssa.html");
    let html = HtmlWriter(ci, file);
    html.start();
    html
}

pub class HtmlWriter {
    ci: CompilationInfo,
    file: OpenFile,
}

impl HtmlWriter {
    fn start() {
        self.write("<!doctype html>
<html>
    <head>
        <meta charset=\"utf-8\">
        <title>Graph Viewer</title>
        <style>
div.block {
    margin-left: 40px;
}

div.inst {
    padding-left: 1.5em;
    text-indent: -1.5em;
}

table {
    table-layout: fixed;
}

#main {
    display: flex;
    width: fit-content;
    margin: 10px;
}

.constpool {
    margin-top: 20px;
    margin-left: 40px;
}

.registers {
    margin-top: 20px;
    margin-left: 40px;
}

div.pass-name {
    border: 1px solid black;
    text-align: right;
    transform: rotate(180deg);
    writing-mode: vertical-lr;
    white-space: pre;
    padding: 10px;
}

div.graph {
    border-width: 1px 0px;
    border-style: solid;
    border-color: black;
    padding: 5px;
    width: 400px;
}

.value {
    cursor: pointer;
}

.inst-value-type {
    font-style: italic;
    font-size: small;
}

.line-number {
    font-style: italic;
    font-size: small;
}
        </style>
        <script>
window.onload = function() {
    let elements = document.getElementsByClassName(\"value\");
    for (element of elements) {
        element.addEventListener('click', graphValueClicked);
    }
}

function graphValueClicked(e) {
    console.log(e);
}
        </script>
    </head>
    <body>

    <div id=\"main\">
");
    }

    pub fn dumpBytecode(bc: BytecodeFunction, analysis: BytecodeAnalysis) {
        self.write("<div class=\"pass-name\">bytecode</div>\n");
        self.write("<div class=\"graph\"><code>");
        let dumper = HtmlBytecodeDumper(self.file) as BytecodeDumper;

        for blockStart in analysis.starts {
            self.write("<div class=\"block-name\">block at ${blockStart}:</div>");
            self.write("<div class=\"block\">");

            for inst in BytecodeIterator::newAtPos(bc.code, blockStart) {
                if inst.start > blockStart && analysis.starts.contains(inst.start) {
                    break;
                }

                self.write("<div>${inst.start}: ");
                dumpInstruction(inst, dumper);
                self.write("</div>");
            }

            self.write("</div>");
        }

        let mut i = 0;
        self.write("<div class=\"constpool\">");
        for entry in bc.const_pool {
            self.write("<div>@${i} = ${entry.toString()}</div>");
            i = i + 1i64;
        }
        self.write("</div>");

        i = 0;
        self.write("<div class=\"registers\">");
        for register in bc.registers {
            self.write("<div>r${i} = ${register}</div>");
            i = i + 1;
        }
        self.write("</div>");

        self.write("</code></div>\n");
    }

    pub fn dumpGraph(title: String, graph: Graph) {
        self.write("<div class=\"pass-name\">${title}</div>\n");
        self.write("<div class=\"graph\"><code>");

        for block in graph.insertionOrderIterator() {
            self.dumpBlock(block);
        }

        self.write("</code></div>\n");
    }

    fn dumpBlock(block: Block) {
        self.write("<div class=\"block-name\">@${block.id()}:</div>");
        self.write("<div class=\"block\">");

        for inst in block.phisIterator() {
            self.dumpInst(inst);
        }

        for inst in block.instructionsIterator() {
            self.dumpInst(inst);
        }

        self.write("</div>");
    }

    fn dumpInst(inst: Inst) {
        self.write("<div class=\"inst\">%${inst.id()}");

        let ty = inst.getMaybeValueType();

        if ty.isSome() {
            let ty = ty.getOrPanic();
            self.write("&nbsp;<span class=\"inst-value-type\">&lt;${ty}&gt;</span>");
        }

        if inst.hasInlinedLocation() {
            let inlined_location = inst.getInlinedLocation();
            let loc = inlined_location.location;
            self.write("&nbsp;<span class=\"line-number\">&lt;${loc.line}:${loc.column}&gt;</span>");
        }

        self.write(": ${inst.op()}");

        match inst.op() {
            Op::Goto => {
                self.write("&nbsp;<span class=\"block-use\">${inst.getTargetBlock()}</span>");
            }

            Op::If => {
                self.write("&nbsp;<span class=\"block-use\">${inst.getTrueBlock()}</span>");
                self.write("&nbsp;<span class=\"block-use\">${inst.getFalseBlock()}</span>");
            }

            Op::UInt8Const => {
                self.write("&nbsp;${inst.getValueAsUInt8()}");
            }

            Op::Int32Const => {
                self.write("&nbsp;${inst.getValueAsInt32()}");
            }

            Op::Int64Const => {
                self.write("&nbsp;${inst.getValueAsInt64()}");
            }

            Op::Float32Const => {
                self.write("&nbsp;${inst.getValueAsFloat32()}");
            }

            Op::Float64Const => {
                self.write("&nbsp;${inst.getValueAsFloat64()}");
            }

            Op::StringConst => {
                self.write("&nbsp;\"${inst.getValueAsString()}\"");
            }

            _ => {
                if !inst.getInputs().isEmpty() {
                    for input in inst.getInputs() {
                        let id = input.getValue().id();
                        self.write("&nbsp;<span class=\"value inst${id}\">%${id}</span>");
                    }
                }
            }
        }

        self.write("</div>");
    }

    pub fn close() {
        self.write("
    </div>
    </body>
</html>
");
        self.file.close();
    }

    fn write(text: String) {
        self.file.writeString(text);
    }
}

class HtmlBytecodeDumper {
    file: OpenFile,
}

impl BytecodeDumper for HtmlBytecodeDumper {
    fn emitInst(inst: BytecodeInstructionInfo, name: String) {
        self.file.writeString(name);
    }

    fn emitReg3(inst: BytecodeInstructionInfo, name: String, r1: BytecodeRegister, r2: BytecodeRegister, r3: BytecodeRegister) {
        self.file.writeString("${name} ${r1}, ${r2}, ${r3}");
    }

    fn emitReg2(inst: BytecodeInstructionInfo, name: String, r1: BytecodeRegister, r2: BytecodeRegister) {
        self.file.writeString("${name} ${r1}, ${r2}");
    }

    fn emitReg2AndIdx(inst: BytecodeInstructionInfo, name: String, r1: BytecodeRegister, r2: BytecodeRegister, idx: ConstPoolId) {
        self.file.writeString("${name} ${r1}, ${r2}, @${idx.value}");
    }

    fn emitReg1(inst: BytecodeInstructionInfo, name: String, r1: BytecodeRegister) {
        self.file.writeString("${name} ${r1}")
    }

    fn emitReg1AndInt(inst: BytecodeInstructionInfo, name: String, r1: BytecodeRegister, value: Int32) {
        self.file.writeString("${name} ${r1}, ${value}")
    }

    fn emitReg1AndIdx(inst: BytecodeInstructionInfo, name: String, r1: BytecodeRegister, idx: ConstPoolId) {
        self.file.writeString("${name} ${r1}, @${idx.value}")
    }

    fn emitConditionalJump(inst: BytecodeInstructionInfo, name: String, r: BytecodeRegister, offset: Int32) {
        self.file.writeString("${name} ${r}, ${inst.start + offset.toInt64()} (+${offset})");
    }

    fn emitBackwardJump(inst: BytecodeInstructionInfo, name: String, offset: Int32) {
        self.file.writeString("${name} ${inst.start - offset.toInt64()} (-${offset})");
    }

    fn emitJump(inst: BytecodeInstructionInfo, name: String, offset: Int32) {
        self.file.writeString("${name} ${inst.start + offset.toInt64()} (+${offset})");
    }

    fn emitGlobal(inst: BytecodeInstructionInfo, name: String, r1: BytecodeRegister, gid: GlobalId) {
        self.file.writeString("${name} ${r1}, ${gid.value}");
    }
}
