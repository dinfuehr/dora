fun createGraph(bc: BytecodeFunction) -> Graph {
    let graph = Graph();

    let blockBuilder = BlockBuilder(graph, bc);
    blockBuilder.run();

    let ssagen = SsaGen(graph, bc, blockBuilder);
    ssagen.run();

    graph
}

class SsaGen(let graph: Graph, let bc: BytecodeFunction, let blockBuilder: BlockBuilder): BytecodeVisitor {
    var currentBlock: Block;
    var offset: Int32 = 0;
    var currentDef: Array[HashMap[Block, Inst]];
    var blockTerminated: Bool = false;

    // a block is considered filled when all instructions are inserted
    var filledBlocks: BitSet = BitSet(0L);

    // block is considered sealed when the set of predecessors is final
    var sealedBlocks: BitSet = BitSet(0L);

    // tracks all incomplete phi instructions inserted into unsealed blocks
    let incompletePhis: HashMap[Block, HashMap[BytecodeRegister, PhiInst]] = HashMap[Block, HashMap[BytecodeRegister, PhiInst]]();

    fun run() {
        self.prepare();
        self.setupArguments();

        readBytecode(self.bc.code, self);
        assert(self.blockTerminated);
        self.blockEndReached(nil);
    }

    fun prepare() {
        self.currentBlock = self.graph.getEntryBlock();

        self.currentDef = Array[HashMap[Block, Inst]](self.bc.registers.length());

        let blockCount = self.graph.numberBlocks();

        self.filledBlocks = BitSet(blockCount.toInt64());
        self.sealedBlocks = BitSet(blockCount.toInt64());

        for i in range(0, self.currentDef.length().toInt32()) {
            self.currentDef(i.toInt64()) = HashMap[Block, Inst]();
        }
    }

    fun setupArguments() {
        var i = 0;
        let entryBlock = self.graph.getEntryBlock();
        while i < self.bc.arguments {
            let ty = Type::fromBytecodeType(self.bc.registers.get(i.toInt64()));
            let argInst = ArgInst(i, ty);
            entryBlock.appendInst(argInst);
            self.writeVariable(BytecodeRegister(i), entryBlock, argInst);
            i = i + 1;
        }
    }

    fun writeVariable(register: BytecodeRegister, block: Block, value: Inst) {
        self.currentDef.get(register.value.toInt64()).insert(block, value);
    }

    fun readVariable(register: BytecodeRegister, block: Block) -> Inst {
        if self.currentDef.get(register.value.toInt64()).contains(block) {
            self.currentDef.get(register.value.toInt64()).get(block).unwrap()
        } else {
            self.readVariableRecursive(register, block)
        }
    }

    fun readVariableRecursive(register: BytecodeRegister, block: Block) -> Inst {
        let value: Inst = if !self.sealedBlocks.contains(block.id.toInt64()) {
            // While all blocks are created with predecessors and successors before
            // this pass in BlockBuilder already, we still need to handle unsealed blocks.
            // E.g. Register is accessed in while header and updated in the while body.
            // In this case the while header is filled before the while body. If we wouldn't
            // handle unsealed blocks we wouldn't create a Phi instruction, since the
            // while body predecessor is still empty.
            let incomplete = PhiInst();
            block.appendPhi(incomplete);

            if self.incompletePhis.contains(block) {
                self.incompletePhis.get(block).unwrap().insert(register, incomplete);
            } else {
                let map = HashMap[BytecodeRegister, PhiInst]();
                map.insert(register, incomplete);
                self.incompletePhis.insert(block, map);
            }

            incomplete as Inst
        } else if block.predecessors.length() == 1L {
            self.readVariable(register, block.predecessors.first().unwrap())
        } else {
            let phi = PhiInst();
            block.appendPhi(phi);
            self.writeVariable(register, block, phi);
            self.addPhiOperands(register, phi)
        };

        self.writeVariable(register, block, value);
        value
    }

    fun addPhiOperands(register: BytecodeRegister, phi: PhiInst) -> Inst {
        let it = phi.block.unwrap().predecessors.makeIterator();
        while it.hasNext() {
            let pred = it.next();
            let inst = self.readVariable(register, pred);
            phi.addInput(inst);
        }
        phi.registerUses();
        self.tryRemoveTrivialPhi(phi)
    }

    fun tryRemoveTrivialPhi(phi: PhiInst) -> Inst {
        var same = Option::none[Inst]();

        let it = phi.inputs.makeIterator();
        while it.hasNext() {
            let op = it.next().value;

            if (same.isSome() && same.unwrap() === op) || op === phi {
                continue;
            }

            if same.isSome() {
                return phi;
            }

            same = Option::some[Inst](op);
        }

        if same.isNone() {
            same = Option::some[Inst](UndefInst());
        }

        let users = phi.users();

        phi.replaceWith(same.unwrap());
        phi.remove();

        for i in range(0, users.length().toInt32()) {
            let user = users(i.toInt64());

            if user === phi {
                continue;
            }

            if user is PhiInst {
                self.tryRemoveTrivialPhi(user as PhiInst);
            }
        }

        same.unwrap()
    }

    fun markBlockTerminated() {
        self.blockTerminated = true;
    }

    @override fun visitInstruction(offset: Int32) {
        self.offset = offset;

        let block = self.blockBuilder.blockAt(offset);

        if block.isSome() {
            self.blockEndReached(block.unwrap());
        }

        self.blockTerminated = false;
    }

    fun blockEndReached(next: Block) {
        let block = self.currentBlock;

        if !self.blockTerminated {
            let gotoInst = GotoInst(next);
            block.appendInst(gotoInst);
        }

        // We change the current block, that means all instructions
        // are inserted. The block is now filled.
        self.fillBlock(block);

        // We don't really know when to seal a block from the bytecode
        // Try to seal this block if all predecessors are filled.
        self.trySealBlock(block);

        // This block might have a back edge to a loop header. Since this
        // block is now filled, we might be able to seal another block.
        let it = block.successors.makeIterator();

        while it.hasNext() {
            let succ = it.next();
            self.trySealBlock(succ);
        }

        self.currentBlock = next;
    }

    fun fillBlock(block: Block) {
        assert(!self.filledBlocks.contains(block.id.toInt64()));
        self.filledBlocks.insert(block.id.toInt64());
    }

    fun trySealBlock(block: Block) {
        if self.sealedBlocks.contains(block.id.toInt64()) {
            return;
        }

        // all predecessors need to be filled
        let it = block.predecessors.makeIterator();

        while it.hasNext() {
            let pred = it.next();
            if !self.filledBlocks.contains(pred.id.toInt64()) {
                return;
            }
        }

        self.sealBlock(block);
    }

    fun sealBlock(block: Block) {
        assert(!self.sealedBlocks.contains(block.id.toInt64()));
        self.sealedBlocks.insert(block.id.toInt64());

        let map = self.incompletePhis.get(block);
        if map.isNone() { return; }

        let it = map.unwrap().makeIterator();

        while it.hasNext() {
            let variableAndPhi = it.next();
            self.addPhiOperands(variableAndPhi.0, variableAndPhi.1);
        }
    }

    @override fun visitAddInt32(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::Add, Type::int32());
    }
    @override fun visitAddInt64(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::Add, Type::int64());
    }
    @override fun visitAddFloat(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::Add, Type::float());
    }
    @override fun visitAddDouble(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::Add, Type::double());
    }

    fun emitBin(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister, op: BinOp, ty: Type) {
        let lhsInst = self.readVariable(lhs, self.currentBlock);
        let rhsInst = self.readVariable(rhs, self.currentBlock);
        let destInst = BinaryInst(op, ty, lhsInst, rhsInst);
        self.currentBlock.appendInst(destInst);
        self.writeVariable(dest, self.currentBlock, destInst);
    }

    @override fun visitSubInt32(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::Sub, Type::int32());
    }
    @override fun visitSubInt64(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::Sub, Type::int64());
    }
    @override fun visitSubFloat(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::Sub, Type::float());
    }
    @override fun visitSubDouble(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::Sub, Type::double());
    }

    @override fun visitNegInt32(dest: BytecodeRegister, src: BytecodeRegister) {
        self.emitUn(dest, src, UnOp::Neg, Type::int32());
    }
    @override fun visitNegInt64(dest: BytecodeRegister, src: BytecodeRegister) {
        self.emitUn(dest, src, UnOp::Neg, Type::int64());
    }
    @override fun visitNegFloat(dest: BytecodeRegister, src: BytecodeRegister) {
        self.emitUn(dest, src, UnOp::Neg, Type::float());
    }
    @override fun visitNegDouble(dest: BytecodeRegister, src: BytecodeRegister) {
        self.emitUn(dest, src, UnOp::Neg, Type::double());
    }

    fun emitUn(dest: BytecodeRegister, src: BytecodeRegister, op: UnOp, ty: Type) {
        let srcInst = self.readVariable(src, self.currentBlock);
        let destInst = UnaryInst(op, ty, srcInst);
        self.currentBlock.appendInst(destInst);
        self.writeVariable(dest, self.currentBlock, destInst);
    }

    @override fun visitMulInt32(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::Mul, Type::int32());
    }
    @override fun visitMulInt64(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::Mul, Type::int64());
    }
    @override fun visitMulFloat(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::Mul, Type::float());
    }
    @override fun visitMulDouble(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::Mul, Type::double());
    }

    @override fun visitDivInt32(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::Div, Type::int32());
    }
    @override fun visitDivInt64(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::Div, Type::int64());
    }
    @override fun visitDivFloat(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::Div, Type::float());
    }
    @override fun visitDivDouble(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::Div, Type::double());
    }

    @override fun visitModInt32(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::Mod, Type::int32());
    }
    @override fun visitModInt64(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::Mod, Type::int64());
    }

    @override fun visitAndInt32(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::And, Type::int32());
    }
    @override fun visitAndInt64(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::And, Type::int64());
    }

    @override fun visitOrInt32(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::Or, Type::int32());
    }
    @override fun visitOrInt64(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::Or, Type::int64());
    }

    @override fun visitXorInt32(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::Xor, Type::int32());
    }
    @override fun visitXorInt64(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::Xor, Type::int64());
    }

    @override fun visitNotBool(dest: BytecodeRegister, src: BytecodeRegister) {
        self.emitUn(dest, src, UnOp::Not, Type::bool());
    }
    @override fun visitNotInt32(dest: BytecodeRegister, src: BytecodeRegister) {
        self.emitUn(dest, src, UnOp::Not, Type::int32());
    }
    @override fun visitNotInt64(dest: BytecodeRegister, src: BytecodeRegister) {
        self.emitUn(dest, src, UnOp::Not, Type::int64());
    }

    @override fun visitShlInt32(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::Shl, Type::int32());
    }
    @override fun visitShrInt32(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::Shr, Type::int32());
    }
    @override fun visitSarInt32(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::Sar, Type::int32());
    }

    @override fun visitShlInt64(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::Shl, Type::int64());
    }
    @override fun visitShrInt64(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::Shr, Type::int64());
    }
    @override fun visitSarInt64(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitBin(dest, lhs, rhs, BinOp::Sar, Type::int64());
    }

    @override fun visitMovBool(dest: BytecodeRegister, src: BytecodeRegister) {
        self.emitMov(dest, src);
    }
    @override fun visitMovUInt8(dest: BytecodeRegister, src: BytecodeRegister) {
        self.emitMov(dest, src);
    }
    @override fun visitMovChar(dest: BytecodeRegister, src: BytecodeRegister) {
        self.emitMov(dest, src);
    }
    @override fun visitMovInt32(dest: BytecodeRegister, src: BytecodeRegister) {
        self.emitMov(dest, src);
    }
    @override fun visitMovInt64(dest: BytecodeRegister, src: BytecodeRegister) {
        self.emitMov(dest, src);
    }
    @override fun visitMovFloat(dest: BytecodeRegister, src: BytecodeRegister) {
        self.emitMov(dest, src);
    }
    @override fun visitMovDouble(dest: BytecodeRegister, src: BytecodeRegister) {
        self.emitMov(dest, src);
    }
    @override fun visitMovPtr(dest: BytecodeRegister, src: BytecodeRegister) {
        self.emitMov(dest, src);
    }

    fun emitMov(dest: BytecodeRegister, src: BytecodeRegister) {
        let srcInst = self.readVariable(src, self.currentBlock);
        self.writeVariable(dest, self.currentBlock, srcInst);
    }

    @override fun visitLoadFieldBool(dest: BytecodeRegister, obj: BytecodeRegister, cls: ClassDefId, field: FieldId) { unimplemented(); }
    @override fun visitLoadFieldUInt8(dest: BytecodeRegister, obj: BytecodeRegister, cls: ClassDefId, field: FieldId) { unimplemented(); }
    @override fun visitLoadFieldChar(dest: BytecodeRegister, obj: BytecodeRegister, cls: ClassDefId, field: FieldId) { unimplemented(); }
    @override fun visitLoadFieldInt32(dest: BytecodeRegister, obj: BytecodeRegister, cls: ClassDefId, field: FieldId) { unimplemented(); }
    @override fun visitLoadFieldInt64(dest: BytecodeRegister, obj: BytecodeRegister, cls: ClassDefId, field: FieldId) { unimplemented(); }
    @override fun visitLoadFieldFloat(dest: BytecodeRegister, obj: BytecodeRegister, cls: ClassDefId, field: FieldId) { unimplemented(); }
    @override fun visitLoadFieldDouble(dest: BytecodeRegister, obj: BytecodeRegister, cls: ClassDefId, field: FieldId) { unimplemented(); }
    @override fun visitLoadFieldPtr(dest: BytecodeRegister, obj: BytecodeRegister, cls: ClassDefId, field: FieldId) { unimplemented(); }

    @override fun visitStoreFieldBool(src: BytecodeRegister, obj: BytecodeRegister, cls: ClassDefId, field: FieldId) { unimplemented(); }
    @override fun visitStoreFieldUInt8(src: BytecodeRegister, obj: BytecodeRegister, cls: ClassDefId, field: FieldId) { unimplemented(); }
    @override fun visitStoreFieldChar(src: BytecodeRegister, obj: BytecodeRegister, cls: ClassDefId, field: FieldId) { unimplemented(); }
    @override fun visitStoreFieldInt32(src: BytecodeRegister, obj: BytecodeRegister, cls: ClassDefId, field: FieldId) { unimplemented(); }
    @override fun visitStoreFieldInt64(src: BytecodeRegister, obj: BytecodeRegister, cls: ClassDefId, field: FieldId) { unimplemented(); }
    @override fun visitStoreFieldFloat(src: BytecodeRegister, obj: BytecodeRegister, cls: ClassDefId, field: FieldId) { unimplemented(); }
    @override fun visitStoreFieldDouble(src: BytecodeRegister, obj: BytecodeRegister, cls: ClassDefId, field: FieldId) { unimplemented(); }
    @override fun visitStoreFieldPtr(src: BytecodeRegister, obj: BytecodeRegister, cls: ClassDefId, field: FieldId) { unimplemented(); }

    @override fun visitLoadGlobalBool(dest: BytecodeRegister, glob: GlobalId) {
        self.emitLoadGlobal(dest, glob, Type::bool());
    }
    @override fun visitLoadGlobalUInt8(dest: BytecodeRegister, glob: GlobalId) {
        self.emitLoadGlobal(dest, glob, Type::uint8());
    }
    @override fun visitLoadGlobalChar(dest: BytecodeRegister, glob: GlobalId) {
        self.emitLoadGlobal(dest, glob, Type::char());
    }
    @override fun visitLoadGlobalInt32(dest: BytecodeRegister, glob: GlobalId) {
        self.emitLoadGlobal(dest, glob, Type::int32());
    }
    @override fun visitLoadGlobalInt64(dest: BytecodeRegister, glob: GlobalId) {
        self.emitLoadGlobal(dest, glob, Type::int64());
    }
    @override fun visitLoadGlobalFloat(dest: BytecodeRegister, glob: GlobalId) {
        self.emitLoadGlobal(dest, glob, Type::float());
    }
    @override fun visitLoadGlobalDouble(dest: BytecodeRegister, glob: GlobalId) {
        self.emitLoadGlobal(dest, glob, Type::double());
    }
    @override fun visitLoadGlobalPtr(dest: BytecodeRegister, glob: GlobalId) {
        self.emitLoadGlobal(dest, glob, Type::ptr());
    }

    fun emitLoadGlobal(dest: BytecodeRegister, glob: GlobalId, ty: Type) {
        let globInst = LoadGlobalInst(ty, glob);
        self.currentBlock.appendInst(globInst);
        self.writeVariable(dest, self.currentBlock, globInst);
    }

    @override fun visitStoreGlobalBool(src: BytecodeRegister, glob: GlobalId) {
        self.emitStoreGlobal(src, glob, Type::bool());
    }
    @override fun visitStoreGlobalUInt8(src: BytecodeRegister, glob: GlobalId) {
        self.emitStoreGlobal(src, glob, Type::uint8());
    }
    @override fun visitStoreGlobalChar(src: BytecodeRegister, glob: GlobalId) {
        self.emitStoreGlobal(src, glob, Type::char());
    }
    @override fun visitStoreGlobalInt32(src: BytecodeRegister, glob: GlobalId) {
        self.emitStoreGlobal(src, glob, Type::int32());
    }
    @override fun visitStoreGlobalInt64(src: BytecodeRegister, glob: GlobalId) {
        self.emitStoreGlobal(src, glob, Type::int64());
    }
    @override fun visitStoreGlobalFloat(src: BytecodeRegister, glob: GlobalId) {
        self.emitStoreGlobal(src, glob, Type::float());
    }
    @override fun visitStoreGlobalDouble(src: BytecodeRegister, glob: GlobalId) {
        self.emitStoreGlobal(src, glob, Type::double());
    }
    @override fun visitStoreGlobalPtr(src: BytecodeRegister, glob: GlobalId) {
        self.emitStoreGlobal(src, glob, Type::ptr());
    }

    fun emitStoreGlobal(src: BytecodeRegister, glob: GlobalId, ty: Type) {
        let srcInst = self.readVariable(src, self.currentBlock);
        let globInst = StoreGlobalInst(ty, glob, srcInst);
        self.currentBlock.appendInst(globInst);
    }

    @override fun visitPushRegister(src: BytecodeRegister) { unimplemented(); }

    @override fun visitConstNil(dest: BytecodeRegister) { unimplemented(); }
    @override fun visitConstTrue(dest: BytecodeRegister) { unimplemented(); }
    @override fun visitConstFalse(dest: BytecodeRegister) { unimplemented(); }
    @override fun visitConstZeroUInt8(dest: BytecodeRegister) { unimplemented(); }
    @override fun visitConstZeroChar(dest: BytecodeRegister) { unimplemented(); }
    @override fun visitConstZeroInt32(dest: BytecodeRegister) {
        let inst = Int32Const(0);
        self.currentBlock.appendInst(inst);
        self.writeVariable(dest, self.currentBlock, inst);
    }
    @override fun visitConstZeroInt64(dest: BytecodeRegister) {
        let inst = Int64Const(0L);
        self.currentBlock.appendInst(inst);
        self.writeVariable(dest, self.currentBlock, inst);
    }
    @override fun visitConstZeroFloat(dest: BytecodeRegister) {
        let inst = FloatConst(0.0F);
        self.currentBlock.appendInst(inst);
        self.writeVariable(dest, self.currentBlock, inst);
    }
    @override fun visitConstZeroDouble(dest: BytecodeRegister) {
        let inst = DoubleConst(0.0);
        self.currentBlock.appendInst(inst);
        self.writeVariable(dest, self.currentBlock, inst);
    }
    @override fun visitConstUInt8(dest: BytecodeRegister, value: UInt8) { unimplemented(); }
    @override fun visitConstChar(dest: BytecodeRegister, idx: ConstPoolId) { unimplemented(); }
    @override fun visitConstInt32(dest: BytecodeRegister, idx: ConstPoolId) {
        let value = self.bc.constPool(idx).toInt32().unwrap();
        let inst = Int32Const(value);
        self.currentBlock.appendInst(inst);
        self.writeVariable(dest, self.currentBlock, inst);
    }
    @override fun visitConstInt64(dest: BytecodeRegister, idx: ConstPoolId) {
        let value = self.bc.constPool(idx).toInt64().unwrap();
        let inst = Int64Const(value);
        self.currentBlock.appendInst(inst);
        self.writeVariable(dest, self.currentBlock, inst);
    }
    @override fun visitConstFloat(dest: BytecodeRegister, idx: ConstPoolId) {
        let value = self.bc.constPool(idx).toFloat().unwrap();
        let inst = FloatConst(value);
        self.currentBlock.appendInst(inst);
        self.writeVariable(dest, self.currentBlock, inst);
    }
    @override fun visitConstDouble(dest: BytecodeRegister, idx: ConstPoolId) {
        let value = self.bc.constPool(idx).toDouble().unwrap();
        let inst = DoubleConst(value);
        self.currentBlock.appendInst(inst);
        self.writeVariable(dest, self.currentBlock, inst);
    }
    @override fun visitConstString(dest: BytecodeRegister, idx: ConstPoolId) { unimplemented(); }

    @override fun visitTestEqPtr(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitTest(dest, lhs, rhs, TestOp::Equal, Type::ptr());
    }
    @override fun visitTestNePtr(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitTest(dest, lhs, rhs, TestOp::NotEqual, Type::ptr());
    }

    @override fun visitTestEqInt32(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitTest(dest, lhs, rhs, TestOp::Equal, Type::int32());
    }
    @override fun visitTestNeInt32(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitTest(dest, lhs, rhs, TestOp::NotEqual, Type::int32());
    }
    @override fun visitTestGtInt32(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitTest(dest, lhs, rhs, TestOp::Greater, Type::int32());
    }
    @override fun visitTestGeInt32(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitTest(dest, lhs, rhs, TestOp::GreaterOrEqual, Type::int32());
    }
    @override fun visitTestLtInt32(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitTest(dest, lhs, rhs, TestOp::Less, Type::int32());
    }
    @override fun visitTestLeInt32(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) {
        self.emitTest(dest, lhs, rhs, TestOp::LessOrEqual, Type::int32());
    }

    @override fun visitTestEqInt64(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) { unimplemented(); }
    @override fun visitTestNeInt64(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) { unimplemented(); }
    @override fun visitTestGtInt64(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) { unimplemented(); }
    @override fun visitTestGeInt64(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) { unimplemented(); }
    @override fun visitTestLtInt64(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) { unimplemented(); }
    @override fun visitTestLeInt64(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) { unimplemented(); }

    @override fun visitTestEqFloat(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) { unimplemented(); }
    @override fun visitTestNeFloat(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) { unimplemented(); }
    @override fun visitTestGtFloat(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) { unimplemented(); }
    @override fun visitTestGeFloat(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) { unimplemented(); }
    @override fun visitTestLtFloat(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) { unimplemented(); }
    @override fun visitTestLeFloat(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) { unimplemented(); }

    @override fun visitTestEqDouble(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) { unimplemented(); }
    @override fun visitTestNeDouble(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) { unimplemented(); }
    @override fun visitTestGtDouble(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) { unimplemented(); }
    @override fun visitTestGeDouble(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) { unimplemented(); }
    @override fun visitTestLtDouble(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) { unimplemented(); }
    @override fun visitTestLeDouble(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister) { unimplemented(); }

    fun emitTest(dest: BytecodeRegister, lhs: BytecodeRegister, rhs: BytecodeRegister, op: TestOp, ty: Type) {
        let lhsInst = self.readVariable(lhs, self.currentBlock);
        let rhsInst = self.readVariable(rhs, self.currentBlock);
        let destInst = TestInst(op, ty, lhsInst, rhsInst);
        self.currentBlock.appendInst(destInst);
        self.writeVariable(dest, self.currentBlock, destInst);
    }

    @override fun visitJumpLoop(offset: Int32) {
        let targetBlock = self.blockBuilder.blockAt(self.offset - offset).unwrap();
        let gotoInst = GotoInst(targetBlock);
        self.currentBlock.appendInst(gotoInst);
        self.markBlockTerminated();
    }
    
    @override fun visitJumpIfFalse(opnd: BytecodeRegister, offset: Int32) {
        self.emitConditionalJump(opnd, offset, false);
    }
    @override fun visitJumpIfFalseConst(opnd: BytecodeRegister, idx: ConstPoolId) {
        let offset = self.bc.constPool(idx).toInt32().unwrap();
        self.emitConditionalJump(opnd, offset, false);
    }
    @override fun visitJumpIfTrue(opnd: BytecodeRegister, offset: Int32) {
        self.emitConditionalJump(opnd, offset, true);
    }
    @override fun visitJumpIfTrueConst(opnd: BytecodeRegister, idx: ConstPoolId) {
        let offset = self.bc.constPool(idx).toInt32().unwrap();
        self.emitConditionalJump(opnd, offset, true);
    }
    @override fun visitJump(offset: Int32) {
        self.emitJump(offset);
    }
    @override fun visitJumpConst(idx: ConstPoolId) {
        let offset = self.bc.constPool(idx).toInt32().unwrap();
        self.emitJump(offset);
    }

    fun emitJump(offset: Int32) {
        let targetBlock = self.blockBuilder.blockAt(self.offset + offset).unwrap();
        let gotoInst = GotoInst(targetBlock);
        self.currentBlock.appendInst(gotoInst);
        self.markBlockTerminated();
    }

    fun emitConditionalJump(opnd: BytecodeRegister, offset: Int32, value: Bool) {
        let opndInst = self.readVariable(opnd, self.currentBlock);
        let targetBlock = self.blockBuilder.blockAt(self.offset + offset).unwrap();
        let fallthroughBlock = self.blockBuilder.nextBlockAt(self.offset+1).unwrap();

        let cond = if value {
            IfInst(opndInst, targetBlock, fallthroughBlock)
        } else {
            IfInst(opndInst, fallthroughBlock, targetBlock)
        };

        self.currentBlock.appendInst(cond);
        self.markBlockTerminated();
    }

    @override fun visitInvokeDirectVoid(fct: FctId) { unimplemented(); }
    @override fun visitInvokeDirectBool(dest: BytecodeRegister, fct: FctId) { unimplemented(); }
    @override fun visitInvokeDirectUInt8(dest: BytecodeRegister, fct: FctId) { unimplemented(); }
    @override fun visitInvokeDirectChar(dest: BytecodeRegister, fct: FctId) { unimplemented(); }
    @override fun visitInvokeDirectInt32(dest: BytecodeRegister, fct: FctId) { unimplemented(); }
    @override fun visitInvokeDirectInt64(dest: BytecodeRegister, fct: FctId) { unimplemented(); }
    @override fun visitInvokeDirectFloat(dest: BytecodeRegister, fct: FctId) { unimplemented(); }
    @override fun visitInvokeDirectDouble(dest: BytecodeRegister, fct: FctId) { unimplemented(); }
    @override fun visitInvokeDirectPtr(dest: BytecodeRegister, fct: FctId) { unimplemented(); }

    @override fun visitInvokeVirtualVoid(fct: FctId) { unimplemented(); }
    @override fun visitInvokeVirtualBool(dest: BytecodeRegister, fct: FctId) { unimplemented(); }
    @override fun visitInvokeVirtualUInt8(dest: BytecodeRegister, fct: FctId) { unimplemented(); }
    @override fun visitInvokeVirtualChar(dest: BytecodeRegister, fct: FctId) { unimplemented(); }
    @override fun visitInvokeVirtualInt32(dest: BytecodeRegister, fct: FctId) { unimplemented(); }
    @override fun visitInvokeVirtualInt64(dest: BytecodeRegister, fct: FctId) { unimplemented(); }
    @override fun visitInvokeVirtualFloat(dest: BytecodeRegister, fct: FctId) { unimplemented(); }
    @override fun visitInvokeVirtualDouble(dest: BytecodeRegister, fct: FctId) { unimplemented(); }
    @override fun visitInvokeVirtualPtr(dest: BytecodeRegister, fct: FctId) { unimplemented(); }

    @override fun visitInvokeStaticVoid(fct: FctId) { unimplemented(); }
    @override fun visitInvokeStaticBool(dest: BytecodeRegister, fct: FctId) { unimplemented(); }
    @override fun visitInvokeStaticUInt8(dest: BytecodeRegister, fct: FctId) { unimplemented(); }
    @override fun visitInvokeStaticChar(dest: BytecodeRegister, fct: FctId) { unimplemented(); }
    @override fun visitInvokeStaticInt32(dest: BytecodeRegister, fct: FctId) { unimplemented(); }
    @override fun visitInvokeStaticInt64(dest: BytecodeRegister, fct: FctId) { unimplemented(); }
    @override fun visitInvokeStaticFloat(dest: BytecodeRegister, fct: FctId) { unimplemented(); }
    @override fun visitInvokeStaticDouble(dest: BytecodeRegister, fct: FctId) { unimplemented(); }
    @override fun visitInvokeStaticPtr(dest: BytecodeRegister, fct: FctId) { unimplemented(); }

    @override fun visitNewObject(dest: BytecodeRegister, cls: ClassDefId) { unimplemented(); }

    @override fun visitRetVoid() {
        let inst = ReturnVoidInst();
        self.currentBlock.appendInst(inst);
        self.markBlockTerminated();
    }

    @override fun visitRetBool(opnd: BytecodeRegister) {
        self.emitRet(opnd, Type::bool());
    }
    @override fun visitRetUInt8(opnd: BytecodeRegister) {
        self.emitRet(opnd, Type::uint8());
    }
    @override fun visitRetChar(opnd: BytecodeRegister) {
        self.emitRet(opnd, Type::char());
    }
    @override fun visitRetInt32(opnd: BytecodeRegister) {
        self.emitRet(opnd, Type::int32());
    }
    @override fun visitRetInt64(opnd: BytecodeRegister) {
        self.emitRet(opnd, Type::int64());
    }
    @override fun visitRetFloat(opnd: BytecodeRegister) {
        self.emitRet(opnd, Type::float());
    }
    @override fun visitRetDouble(opnd: BytecodeRegister) {
        self.emitRet(opnd, Type::double());
    }
    @override fun visitRetPtr(opnd: BytecodeRegister) {
        self.emitRet(opnd, Type::ptr());
    }

    fun emitRet(opnd: BytecodeRegister, ty: Type) {
        let value = self.readVariable(opnd, self.currentBlock);
        let inst = ReturnInst(value, ty);
        self.currentBlock.appendInst(inst);
        self.markBlockTerminated();
    }
}
