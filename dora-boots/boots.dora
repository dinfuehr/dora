use std::collections::HashMap;

use package::assembler::{RegMap, RegSet, Register};
use package::codegen::CodeGen;
use package::codegen::{createCodeGen, generateCode};
use package::compilation::CompilationInfo;
use package::dead_code_elimination::performDeadCodeElimination;
use package::dominator::buildDominatorTree;
use package::graph::{createGotoInst, Block, Edge, Graph, Inst, LocationData, Op, opName};
use package::graph::dump::dumpGraph;
use package::graph::verifier::verifyGraph;
use package::graph_builder::createGraph;
use package::inlining::performInlining;
use package::interface::{Architecture, CodeDescriptor, CommentTable, GcPointTable, GcPoint, LazyCompilationData, LazyCompilationSite, LazyCompilationSiteDirect, LazyCompilationSiteVirtual, LazyCompilationSiteLambda, LocationTable};
use package::interface::{compile, config, getFunctionAddressRaw, getFieldOffsetRaw, getClassSizeRaw, getClassPointerRaw, getFunctionVtableIndexRaw, getClassPointerForLambdaRaw, getGlobalValueAddressRaw, getGlobalStateAddressRaw, hasGlobalInitialValueRaw};
use package::regalloc::performRegisterAllocation;
use package::liveness::computeLiveness;

mod assembler;
mod bytecode;
mod codegen;
mod compilation;
mod dead_code_elimination;
mod deserializer;
mod dominator;
mod graph;
mod graph_builder;
mod inlining;
mod interface;
mod liveness;
mod location;
mod regalloc;
mod resolver;
mod serializer;

pub fn compileFunction(ci: CompilationInfo): CodeDescriptor {
    let graph = createGraph(ci);

    let codegen = createCodeGen(ci, graph);

    if ci.emitHtml {
        unimplemented[()]();
    }

    if ci.emitGraph {
        println("${ci.getDisplayName()} after graph building:");
        println(dumpGraph(Some[CompilationInfo](ci), graph, config.architecture));
    }

    verify(graph);

    performInlining(ci, graph);

    if ci.emitGraph {
        println("${ci.getDisplayName()} after inlining:");
        println(dumpGraph(Some[CompilationInfo](ci), graph, config.architecture));
    }

    verify(graph);

    performDeadCodeElimination(graph);

    if ci.emitGraph {
        println("${ci.getDisplayName()} after dead code elimination:");
        println(dumpGraph(Some[CompilationInfo](ci), graph, config.architecture));
    }

    verify(graph);

    splitCriticalEdges(graph);
    buildDominatorTree(graph);
    computeLiveness(graph);

    if ci.emitGraph {
        println("${ci.getDisplayName()} before register allocation:");
        println(dumpGraph(Some[CompilationInfo](ci), graph, config.architecture));
    }

    performRegisterAllocation(graph, codegen, ci);

    if ci.emitGraph {
        println("${ci.getDisplayName()} after register allocation:");
        println(dumpGraph(Some[CompilationInfo](ci), graph, config.architecture));
    }

    generateCode(graph, codegen, ci)
}

fn verify(graph: Graph) {
    if config.isDebug {
        verifyGraph(graph);
    }
}

fn splitCriticalEdges(graph: Graph) {
    for block in graph.insertionOrderIterator() {
        if block.predecessors.size() <= 1 {
            continue;
        }

        for predecessorEdge in block.predecessors {
            let predecessor = predecessorEdge.source;

            if predecessor.successors.size() == 1 {
                continue;
            }

            // Found a critical edge.
            splitCriticalEdge(graph, predecessorEdge);
        }
    }
}

fn splitCriticalEdge(graph: Graph, edge: Edge) {
    let predecessor = edge.source;
    let successor = edge.target;

    let intermediate = Block::new();
    graph.addBlock(intermediate);

    let inst = createGotoInst(successor);
    intermediate.appendInst(inst);

    let terminator = predecessor.lastInst();
    assert(terminator.op() == Op::If);
    if terminator.getTrueBlock() === successor {
        terminator.setTrueBlock(intermediate);
    } else {
        assert(terminator.getFalseBlock() === successor);
        terminator.setFalseBlock(intermediate);
    }

    let predecessorToIntermediateEdge = Edge::new(edge.source, edge.sourceIdx, intermediate, 0);
    assert(intermediate.predecessors.isEmpty());
    intermediate.predecessors.push(predecessorToIntermediateEdge);

    predecessor.successors(edge.sourceIdx) = predecessorToIntermediateEdge;

    let intermediateToSuccessorEdge = edge;
    intermediateToSuccessorEdge.source = intermediate;
    intermediateToSuccessorEdge.sourceIdx = 0;
    assert(intermediate.successors.isEmpty());
    intermediate.successors.push(intermediateToSuccessorEdge);
}
