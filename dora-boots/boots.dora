use package::assembler::RegSet;
use package::codegen::CodeGen;
use package::codegen::x64::CodeGenX64;
use package::codegen::arm64::CodeGenArm64;
use package::codegen::CodeDescriptor;
use package::graph::{Graph, Location, LocationData, Op, opName};
use package::graph::dump::dumpGraph;
use package::graph_builder::createGraph;
use package::interface::{Architecture, CompilationInfo};
use package::interface::compile;

mod assembler;
mod bytecode;
mod codegen;
mod dce;
mod deserializer;
mod dominator_test;
mod dominator;
mod graph;
mod interface;
mod graph_builder_tests;
mod graph_builder;
mod serializer;

pub fn compileFunction(ci: CompilationInfo): CodeDescriptor {
    let graph = createGraph(ci.bc);
    let codegen = createCodeGen(ci);

    if (ci.emitGraph) {
        println("after graph building:");
        println(dumpGraph(graph, ci.arch));
    }

    ensureLocationData(graph, codegen);
    allocateRegisters(graph, codegen);

    if (ci.emitGraph) {
        println("after register allocation:");
        println(dumpGraph(graph, ci.arch));
    }

    generate(graph, codegen)
}

fn ensureLocationData(graph: Graph, codegen: CodeGen) {
    for block in graph.insertionOrderIterator() {
        for inst in block.instructionsIterator() {
            codegen.setLocationConstraints(inst);
        }
    }
}

fn allocateRegisters(graph: Graph, codegen: CodeGen) {
    let available_registers = codegen.allocatableRegisters();

    for block in graph.insertionOrderIterator() {
        for inst in block.instructionsIterator() {
            let loc = inst.getLocationData();
            let mut idx = 0;

            println("op = ${opName(inst.op())}");

            for input in inst.getInputs() {
                let value = input.getValue();
                let output = value.getLocationData().getOutput();
                loc.getInput(idx).setRegister(output.getRegister());
                idx = idx + 1;
            }

            if loc.hasOutput() {
                let output = loc.getOutput();

                if output.hasRegister() {
                    // fixed register was already assigned
                    assert(available_registers.contains(output.getRegister()));
                    available_registers.remove(output.getRegister());
                } else {
                    // pick a register
                    let register = available_registers.acquire().getOrPanic();
                    output.setRegister(register);
                }
            }
        }
    }
}

fn generate(graph: Graph, codegen: CodeGen): CodeDescriptor {
    codegen.prolog();

    for block in graph.insertionOrderIterator() {
        for inst in block.instructionsIterator() {
            codegen.emit(inst);
        }
    }

    codegen.finalize()
}

fn createCodeGen(ci: CompilationInfo): CodeGen {
    match ci.arch {
        Architecture::X64 => CodeGenX64::new(ci) as CodeGen,
        Architecture::Arm64 => CodeGenArm64::new(ci) as CodeGen,
    }
}