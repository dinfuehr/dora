use std::Stringable;
use std::Equals;
use std::Hash;

use package::graph::Block;
use package::ssagen_block::BlockMap;

@pub class BytecodeFunction(let code: Array[UInt8], let const_pool: Array[ConstPoolEntry], let registers: Array[BytecodeType], let arguments: Int32) {
    @pub fn constPool(idx: ConstPoolId): ConstPoolEntry {
        self.const_pool(idx.value.toInt64())
    }

    @pub fn codeAt(idx: Int32): Int32 {
        self.code(idx.toInt64()).toInt32()
    }

    @pub fn codeInt32At(idx: Int32): Int32 {
        let byte1 = self.codeAt(idx);
        let byte2 = self.codeAt(idx+1i32);
        let byte3 = self.codeAt(idx+2i32);
        let byte4 = self.codeAt(idx+3i32);

        (byte4 << 24i32) | (byte3 << 16i32) | (byte2 << 8i32) | byte1
    }
}

@pub enum ConstPoolEntry {
    Int32(Int32),
    Int64(Int64),
    Float32(Float32),
    Float64(Float64),
    Char(Char),
    String(String),
}

impl ConstPoolEntry {
    @pub fn toInt32(): Option[Int32] {
        match self {
            ConstPoolEntry::Int32(value) => Some(value),
            _ => None,
        }
    }

    @pub fn toInt64(): Option[Int64] {
        match self {
            ConstPoolEntry::Int64(value) => Some(value),
            _ => None,
        }
    }

    @pub fn toFloat32(): Option[Float32] {
        match self {
            ConstPoolEntry::Float32(value) => Some(value),
            _ => None,
        }
    }

    @pub fn toFloat64(): Option[Float64] {
        match self {
            ConstPoolEntry::Float64(value) => Some(value),
            _ => None,
        }
    }
}

impl Stringable for ConstPoolEntry {
    fn toString(): String {
        match self {
            ConstPoolEntry::Int32(value) => "Int32 ${value}",
            ConstPoolEntry::Int64(value) => "Int64 ${value}",
            ConstPoolEntry::Float32(value) => "Float32 ${value}",
            ConstPoolEntry::Float64(value) => "Float64 ${value}",
            ConstPoolEntry::Char(value) => "Char ${value}",
            ConstPoolEntry::String(value) => "String ${value}",
        }
    }
}

@pub enum SourceType {
    Unit,
    UInt8,
    Bool,
    Char,
    Int32,
    Int64,
    Float32,
    Float64,
    Class(Int32, Array[SourceType]),
    Struct(Int32, Array[SourceType]),
    Trait(Int32, Array[SourceType]),
    Tuple(Array[SourceType]),
    TypeParam(Int32),
}

@pub struct BytecodeRegister(@pub value: Int32)

impl Equals for BytecodeRegister {
    fn equals(other: BytecodeRegister): Bool {
        self.value == other.value
    }
}

impl Hash for BytecodeRegister {
    fn hash(): Int32 {
        self.value
    }
}

impl Stringable for BytecodeRegister {
    fn toString(): String = "r${self.value}";
}

@pub struct ClassDefId(@pub value: Int32)
@pub struct FieldId(@pub value: Int32)
@pub struct GlobalId(@pub value: Int32)
@pub struct ConstPoolId(@pub value: Int32)

impl ConstPoolId {
    fn equals(rhs: ConstPoolId): Bool {
        self.value == rhs.value
    }
}

@pub struct FctId(@pub value: Int32)

@pub enum BytecodeType {
    Ptr,
    UInt8,
    Bool,
    Char,
    Int32,
    Int64,
    Float32,
    Float64,
    Struct(Int32, Array[SourceType]),
    Tuple(Array[SourceType]),
    TypeParam(Int32),
}

impl Stringable for BytecodeType {
    fn toString(): String {
        match self {
            BytecodeType::Ptr => "Ptr",
            BytecodeType::UInt8 => "UInt8",
            BytecodeType::Bool => "Bool",
            BytecodeType::Char => "Char",
            BytecodeType::Int32 => "Int32",
            BytecodeType::Int64 => "Int64",
            BytecodeType::Float32 => "Float32",
            BytecodeType::Float64 => "Float64",
            BytecodeType::Struct(id, params) => "Struct(${id}, <type params>)",
            BytecodeType::Tuple(types) => "Tuple(<type params>)",
            BytecodeType::TypeParam(idx) => "TypeParam(${idx})",
        }
    }
}
