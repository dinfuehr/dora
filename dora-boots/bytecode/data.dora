use std::Stringable;
use std::StringBuffer;
use std::Equals;
use std::Hash;

use package::graph::Block;

pub class BytecodeFunction {
    pub code: Array[UInt8],
    pub const_pool: Array[ConstPoolEntry],
    pub registers: Array[BytecodeType],
    pub locations: Array[(Int32, Location)],
    pub arguments: Int32,
}

impl BytecodeFunction {
    static fn new(
            code: Array[UInt8],
            const_pool: Array[ConstPoolEntry],
            registers: Array[BytecodeType],
            locations: Array[(Int32, Location)],
            arguments: Int32): BytecodeFunction {
        BytecodeFunction(code, const_pool, registers, locations, arguments)
    }

    pub fn constPool(idx: ConstPoolId): ConstPoolEntry {
        self.const_pool(idx.value.toInt64())
    }

    pub fn codeAt(idx: Int32): Int32 {
        self.code(idx.toInt64()).toInt32()
    }

    pub fn codeInt32At(idx: Int32): Int32 {
        let byte1 = self.codeAt(idx);
        let byte2 = self.codeAt(idx+1i32);
        let byte3 = self.codeAt(idx+2i32);
        let byte4 = self.codeAt(idx+3i32);

        (byte4 << 24i32) | (byte3 << 16i32) | (byte2 << 8i32) | byte1
    }

    pub fn getLocationAt(at: Int32): Location {
        let size = self.locations.size();

        if size == 0 || at < self.locations(0).0 {
            return unreachable[Location]();
        }

        let mut i = 1;

        while i < size {
            if at < self.locations(i).0 {
                return self.locations(i-1).1;
            }

            i = i + 1;
        }

        self.locations(size-1).1
    }
}

pub enum ConstPoolEntry {
    String(String),
    Float32(Float32),
    Float64(Float64),
    Int32(Int32),
    Int64(Int64),
    Char(Char),
    Class(ClassId, Array[BytecodeType]),
    Field(ClassId, Array[BytecodeType], FieldId),
    Fct(FctId, Array[BytecodeType]),
    TraitObjectMethod(BytecodeType, FctId, Array[BytecodeType]),
    Generic(Int32, FctId, Array[BytecodeType]),
    Enum(EnumId, Array[BytecodeType]),
    EnumVariant(EnumId, Array[BytecodeType], Int32),
    EnumElement(EnumId, Array[BytecodeType], Int32, Int32),
    Struct(StructId, Array[BytecodeType]),
    StructField(StructId, Array[BytecodeType], StructFieldId),
    Trait(TraitId, Array[BytecodeType], BytecodeType),
    TupleElement(BytecodeType, Int32),
    Tuple(Array[BytecodeType]),
    Lambda(Array[BytecodeType], BytecodeType),
}

impl ConstPoolEntry {
    pub fn toChar(): Option[Char] {
        match self {
            ConstPoolEntry::Char(value) => Some(value),
            _ => None,
        }
    }

    pub fn toString(): Option[String] {
        match self {
            ConstPoolEntry::String(value) => Some(value),
            _ => None,
        }
    }

    pub fn toInt32(): Option[Int32] {
        match self {
            ConstPoolEntry::Int32(value) => Some(value),
            _ => None,
        }
    }

    pub fn toInt64(): Option[Int64] {
        match self {
            ConstPoolEntry::Int64(value) => Some(value),
            _ => None,
        }
    }

    pub fn toFloat32(): Option[Float32] {
        match self {
            ConstPoolEntry::Float32(value) => Some(value),
            _ => None,
        }
    }

    pub fn toFloat64(): Option[Float64] {
        match self {
            ConstPoolEntry::Float64(value) => Some(value),
            _ => None,
        }
    }
}

impl Stringable for ConstPoolEntry {
    fn toString(): String {
        match self {
            ConstPoolEntry::Int32(value) => "Int32 ${value}",
            ConstPoolEntry::Int64(value) => "Int64 ${value}",
            ConstPoolEntry::Float32(value) => "Float32 ${value}",
            ConstPoolEntry::Float64(value) => "Float64 ${value}",
            ConstPoolEntry::Char(value) => "Char ${value}",
            ConstPoolEntry::String(value) => "String ${value}",
            _ => unreachable[String](),
        }
    }
}

pub struct BytecodeRegister(pub value: Int32)

impl Equals for BytecodeRegister {
    fn equals(other: BytecodeRegister): Bool {
        self.value == other.value
    }
}

impl Hash for BytecodeRegister {
    fn hash(): Int32 {
        self.value
    }
}

impl Stringable for BytecodeRegister {
    fn toString(): String { "r${self.value}" }
}

pub struct EnumId(pub value: Int32)
pub struct ClassId(pub value: Int32)
pub struct FieldId(pub value: Int32)
pub struct GlobalId(pub value: Int32)
pub struct ConstPoolId(pub value: Int32)
pub struct StructId(pub value: Int32)
pub struct StructFieldId(pub value: Int32)
pub struct TraitId(pub value: Int32)
pub struct AliasId(pub value: Int32)

impl ConstPoolId {
    fn equals(rhs: ConstPoolId): Bool {
        self.value == rhs.value
    }
}

pub struct FctId(pub value: Int32)

pub enum BytecodeType {
    Unit,
    Ptr,
    UInt8,
    Bool,
    Char,
    Int32,
    Int64,
    Float32,
    Float64,
    This,
    Struct(StructId, Array[BytecodeType]),
    Enum(EnumId, Array[BytecodeType]),
    Class(ClassId, Array[BytecodeType]),
    Trait(TraitId, Array[BytecodeType]),
    Tuple(Array[BytecodeType]),
    TypeParam(Int32),
    Lambda(Array[BytecodeType], BytecodeType),
    TypeAlias(AliasId),
}

impl BytecodeType {
    pub fn isAnyFloat(): Bool {
        match self {
            BytecodeType::Float32 | BytecodeType::Float64 => true,
            _ => false,
        }
    }

    pub fn isPtr(): Bool {
        match self {
            BytecodeType::Ptr => true,
            _ => false,
        }
    }

    pub fn isGeneric(): Bool {
        match self {
            BytecodeType::Unit
            | BytecodeType::Ptr
            | BytecodeType::UInt8
            | BytecodeType::Bool
            | BytecodeType::Char
            | BytecodeType::Int32
            | BytecodeType::Int64
            | BytecodeType::Float32
            | BytecodeType::Float64
            | BytecodeType::This => false,
            BytecodeType::Struct(struct_id, type_params) => type_params.isGeneric(),
            BytecodeType::Enum(enum_id, type_params) => type_params.isGeneric(),
            BytecodeType::Class(class_id, type_params) => type_params.isGeneric(),
            BytecodeType::Trait(trait_id, type_params) => type_params.isGeneric(),
            BytecodeType::Tuple(subtypes) => subtypes.isGeneric(),
            BytecodeType::TypeParam(idx) => true,
            BytecodeType::Lambda(params, ret) => params.isGeneric() || ret.isGeneric(),
            BytecodeType::TypeAlias(alias_id) => unreachable[Bool](),
        }
    }

    pub fn isUnit(): Bool {
        match self {
            BytecodeType::Unit => true,
            _ => false,
        }
    }

    pub fn isStruct(): Bool {
        match self {
            BytecodeType::Struct(_, _) => true,
            _ => false,
        }
    }

    pub fn isTuple(): Bool {
        match self {
            BytecodeType::Tuple(_) => true,
            _ => false,
        }
    }

    pub fn isEnum(): Bool {
        match self {
            BytecodeType::Enum(_, _) => true,
            _ => false,
        }
    }

    pub fn specialize(type_params: Array[BytecodeType]): BytecodeType {
        if type_params.isEmpty() || !self.isGeneric() {
            return self;
        }

        match self {
            BytecodeType::Unit
            | BytecodeType::Ptr
            | BytecodeType::UInt8
            | BytecodeType::Bool
            | BytecodeType::Char
            | BytecodeType::Int32
            | BytecodeType::Int64
            | BytecodeType::Float32
            | BytecodeType::Float64
            | BytecodeType::This => self,
            BytecodeType::Struct(struct_id, struct_type_params) => {
                BytecodeType::Struct(struct_id, struct_type_params.specialize(type_params))
            },
            BytecodeType::Enum(enum_id, enum_type_params) => {
                BytecodeType::Enum(enum_id, enum_type_params.specialize(type_params))
            },
            BytecodeType::Class(class_id, class_type_params) => {
                BytecodeType::Class(class_id, class_type_params.specialize(type_params))
            },
            BytecodeType::Trait(trait_id, trait_type_params) => {
                BytecodeType::Trait(trait_id, trait_type_params.specialize(type_params))
            },
            BytecodeType::Tuple(subtypes) => {
                BytecodeType::Tuple(subtypes.specialize(type_params))
            },
            BytecodeType::TypeParam(idx) => type_params(idx.toInt64()),
            BytecodeType::Lambda(params, ret) => {
                BytecodeType::Lambda(params.specialize(type_params), ret.specialize(type_params))
            },
            BytecodeType::TypeAlias(alias_id) => unreachable[BytecodeType](),
        }
    }
}

impl Stringable for BytecodeType {
    fn toString(): String {
        match self {
            BytecodeType::Unit => "Unit",
            BytecodeType::Ptr => "Ptr",
            BytecodeType::UInt8 => "UInt8",
            BytecodeType::Bool => "Bool",
            BytecodeType::Char => "Char",
            BytecodeType::Int32 => "Int32",
            BytecodeType::Int64 => "Int64",
            BytecodeType::Float32 => "Float32",
            BytecodeType::Float64 => "Float64",
            BytecodeType::This => "This",
            BytecodeType::Struct(id, params) => "Struct(${id.value}, ${params})",
            BytecodeType::Enum(id, params) => "Enum(${id.value}, ${params})",
            BytecodeType::Class(id, params) => "Class(${id.value}, ${params})",
            BytecodeType::Trait(id, params) => "Trait(${id.value}, ${params})",
            BytecodeType::Tuple(types) => "Tuple(${types})",
            BytecodeType::TypeParam(idx) => "TypeParam(${idx})",
            BytecodeType::Lambda(params, ty) => "Lambda(${params}, ${ty})",
            BytecodeType::TypeAlias(id) => "TypeAlias(${id.value})",
        }
    }
}

impl Stringable for Array[BytecodeType] {
    fn toString(): String {
        let buffer = StringBuffer::new();
        buffer.append("[");
        let mut first = true;
        for ty in self {
            if !first {
                buffer.append(", ");
            }
            buffer.append(ty.toString());
            first = false;
        }
        buffer.append("]");
        buffer.toString()
    }
}

impl Array[BytecodeType] {
    pub fn isGeneric(): Bool {
        for value in self {
            if value.isGeneric() {
                return true;
            }
        }

        false
    }

    pub fn specialize(type_params: Array[BytecodeType]): Array[BytecodeType] {
        if type_params.isEmpty() || !self.isGeneric() {
            return self;
        }

        let result = Array[BytecodeType]::fill(self.size(), BytecodeType::Unit);

        for idx in std::range(0, self.size()) {
            result(idx) = self(idx).specialize(type_params);
        }

        result
    }
}

pub struct Location {
    pub line: Int32,
    pub column: Int32,
}
