use std::Stringable;
use std::Equals;
use std::Hash;

use package::graph::Block;
use package::ssagen_block::BlockMap;

@pub class BytecodeFunction {
    @pub code: Array[UInt8],
    @pub const_pool: Array[ConstPoolEntry],
    @pub registers: Array[BytecodeType],
    @pub arguments: Int32,
}

impl BytecodeFunction {
    @static fun new(
            code: Array[UInt8],
            const_pool: Array[ConstPoolEntry],
            registers: Array[BytecodeType],
            arguments: Int32): BytecodeFunction {
        BytecodeFunction(code, const_pool, registers, arguments)
    }

    @pub fun constPool(idx: ConstPoolId): ConstPoolEntry {
        self.const_pool(idx.value.toInt64())
    }

    @pub fun codeAt(idx: Int32): Int32 {
        self.code(idx.toInt64()).toInt32()
    }

    @pub fun codeInt32At(idx: Int32): Int32 {
        let byte1 = self.codeAt(idx);
        let byte2 = self.codeAt(idx+1i32);
        let byte3 = self.codeAt(idx+2i32);
        let byte4 = self.codeAt(idx+3i32);

        byte4.shiftLeft(24i32) | byte3.shiftLeft(16i32) | byte2.shiftLeft(8i32) | byte1
    }
}

@pub enum ConstPoolEntry {
    Int32(Int32),
    Int64(Int64),
    Float32(Float32),
    Float64(Float64),
    Char(Char),
    String(String),
}

impl ConstPoolEntry {
    @pub fun toInt32(): Option[Int32] {
        match self {
            ConstPoolEntry::Int32(value) => Some(value),
            _ => None,
        }
    }

    @pub fun toInt64(): Option[Int64] {
        match self {
            ConstPoolEntry::Int64(value) => Some(value),
            _ => None,
        }
    }

    @pub fun toFloat32(): Option[Float32] {
        match self {
            ConstPoolEntry::Float32(value) => Some(value),
            _ => None,
        }
    }

    @pub fun toFloat64(): Option[Float64] {
        match self {
            ConstPoolEntry::Float64(value) => Some(value),
            _ => None,
        }
    }
}

impl Stringable for ConstPoolEntry {
    fun toString(): String {
        match self {
            ConstPoolEntry::Int32(value) => "Int32 ${value}",
            ConstPoolEntry::Int64(value) => "Int64 ${value}",
            ConstPoolEntry::Float32(value) => "Float32 ${value}",
            ConstPoolEntry::Float64(value) => "Float64 ${value}",
            ConstPoolEntry::Char(value) => "Char ${value}",
            ConstPoolEntry::String(value) => "String ${value}",
        }
    }
}

@pub enum SourceType {
    Unit,
    UInt8,
    Bool,
    Char,
    Int32,
    Int64,
    Float32,
    Float64,
    Class(Int32, Array[SourceType]),
    Struct(Int32, Array[SourceType]),
    Trait(Int32, Array[SourceType]),
    Tuple(Array[SourceType]),
    TypeParam(Int32),
}

@pub struct BytecodeRegister(@pub value: Int32)

impl Equals for BytecodeRegister {
    fun equals(other: BytecodeRegister): Bool {
        self.value == other.value
    }
}

impl Hash for BytecodeRegister {
    fun hash(): Int32 {
        self.value
    }
}

impl Stringable for BytecodeRegister {
    fun toString(): String = "r${self.value}";
}

@pub struct ClassDefId(@pub value: Int32)
@pub struct FieldId(@pub value: Int32)
@pub struct GlobalId(@pub value: Int32)
@pub struct ConstPoolId(@pub value: Int32)

impl ConstPoolId {
    fun equals(rhs: ConstPoolId): Bool {
        self.value == rhs.value
    }
}

@pub struct FctId(@pub value: Int32)

@pub enum BytecodeType {
    Unit,
    Ptr,
    UInt8,
    Bool,
    Char,
    Int32,
    Int64,
    Float32,
    Float64,
    Struct(Int32, Array[SourceType]),
    Tuple(Array[SourceType]),
    TypeParam(Int32),
}

impl Stringable for BytecodeType {
    fun toString(): String {
        match self {
            BytecodeType::Unit => "Unit",
            BytecodeType::Ptr => "Ptr",
            BytecodeType::UInt8 => "UInt8",
            BytecodeType::Bool => "Bool",
            BytecodeType::Char => "Char",
            BytecodeType::Int32 => "Int32",
            BytecodeType::Int64 => "Int64",
            BytecodeType::Float32 => "Float32",
            BytecodeType::Float64 => "Float64",
            BytecodeType::Struct(id, params) => "Struct(${id}, <type params>)",
            BytecodeType::Tuple(types) => "Tuple(<type params>)",
            BytecodeType::TypeParam(idx) => "TypeParam(${idx})",
        }
    }
}
