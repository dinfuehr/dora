class TreeNode(var left: TreeNode, var right: TreeNode, var value: Int32)

const MEG: Int32 = 1_000_000;
const INSIGNIFICANT: Int64 = 999L;
const BYTES_PER_WORD: Int32 = 8;
const BYTES_PER_NODE: Int32 = 40;
const WORDS_DEAD: Int32 = 100;

const treeHeight: Int32 = 14;
var treeSize: Int64;

var youngBytes: Int64;
var nodes: Int64;
var actuallyMut: Int64;
var mutatorSum: Int64;
var aexport: Array[Int32];

var trees: Array[TreeNode];
var where: Int32;
var rnd: Random;

var size: Int32;
var workUnits: Int32;
var promoteRate: Int32;
var ptrMutRate: Int32;
var steps: Int32;

fun height(t: TreeNode) -> Int32 {
    if t === nil {
        return 0;
    } else {
        return 1 + Int32::max(height(t.left), height(t.right));
    }
}

fun shortestPath(t: TreeNode) -> Int32 {
    if t === nil {
        return 0;
    } else {
        return 1 + Int32::min(shortestPath(t.left), shortestPath(t.right));
    }
}

fun heightToNodes(h: Int32) -> Int64 {
    var h = h;
    if (h == 0) {
        return 0L;
    }

    var n: Int64 = 1L;

    while h > 1 {
        n = n + n;
        h = h - 1;
    }

    return n + n - 1L;
}

fun heightToBytes(h: Int32) -> Int64 {
    return BYTES_PER_NODE.toInt64() * heightToNodes(h);
}

fun nodesToHeight(nnodes: Int64) -> Int32 {
    var h = 1;
    var n = 1L;

    while n + n - 1L <= nnodes {
        n = n + n;
        h = h + 1;
    }

    return h - 1;
}

fun bytesToHeight(bytes: Int64) -> Int32 {
    return nodesToHeight(bytes / BYTES_PER_NODE.toInt64());
}

fun makeTree(h: Int32) -> TreeNode {
    if h == 0 {
        return nil;
    }

    let res = TreeNode(nil, nil, 0);
    nodes = nodes + 1L;
    res.left = makeTree(h-1);
    res.right = makeTree(h-1);
    res.value = h;
    return res;
}

fun initialize() {
    let ntrees = (size * MEG).toInt64() / treeSize;
    trees = Array[TreeNode](ntrees);

    println("Allocating " + ntrees.toString() + " trees.");
    println("  (" + (ntrees * treeSize).toString() + " bytes)");

    var i = 0L;

    while i < ntrees {
        trees(i.toInt32()) = makeTree(treeHeight);
        i = i + 1L;
    }

    println("  (" + nodes.toString() + " nodes)");
}

fun checkTrees() {
    let ntrees = trees.length();

    var i = 0L;

    while i < ntrees {
        let t = trees(i.toInt32());

        let h1 = height(t);
        let h2 = shortestPath(t);

        if (h1 != treeHeight) || (h2 != treeHeight) {
            println("**** BUG: " + h1.toString() + " " + h2.toString());
        }

        i = i + 1L;
    }
}

fun replaceTreeWork(full: TreeNode, partial: TreeNode, dir: Bool) {
    let canGoLeft = full.left !== nil && full.left.value > partial.value;
    let canGoRight = full.right !== nil && full.right.value > partial.value;

    if canGoLeft && canGoRight {
        if dir {
            replaceTreeWork(full.left, partial, !dir);
        } else {
            replaceTreeWork(full.right, partial, !dir);
        }
    } else if !canGoLeft && !canGoRight {
        if dir {
            full.left = partial;
        } else {
            full.right = partial;
        }
    } else if !canGoLeft {
        full.left = partial;
    } else {
        full.right = partial;
    }
}

fun replaceTree(full: TreeNode, partial: TreeNode) {
    let dir = (partial.value % 2) == 0;
    actuallyMut = actuallyMut + 1L;
    replaceTreeWork(full, partial, dir);
}

fun oldGenAlloc(n: Int64) {
    let full = n / treeSize;
    var partial = n % treeSize;

    var i = 0L;
    while i < full {
        trees(where) = makeTree(treeHeight);
        where = where + 1;

        if where.toInt64() == trees.length() {
            where = 0;
        }

        i = i + 1L;
    }

    while partial > INSIGNIFICANT {
        let h = bytesToHeight(partial);
        let newTree = makeTree(h);
        replaceTree(trees(where), newTree);
        where = where + 1;

        if where.toInt64() == trees.length() {
            where = 0;
        }

        partial = partial - heightToBytes(h);
    }
}

fun oldGenSwapSubtrees() {
    let index1 = rnd.nextInt32WithBound(trees.length().toInt32());
    let index2 = rnd.nextInt32WithBound(trees.length().toInt32());
    let depth = rnd.nextInt32WithBound(treeHeight);
    var path = rnd.nextInt32();

    var tn1 = trees(index1);
    var tn2 = trees(index2);

    var i = 0;

    while i < depth {
        if (path & 1) == 0 {
            tn1 = tn1.left;
            tn2 = tn2.left;
        } else {
            tn1 = tn1.right;
            tn2 = tn2.right;
        }

        path = path >> 1;
        i = i + 1;
    }

    if (path & 1) == 0 {
        let tmp = tn1.left;
        tn1.left = tn2.left;
        tn2.left = tmp;
    } else {
        let tmp = tn1.right;
        tn1.right = tn2.right;
        tn2.right = tmp;
    }
    actuallyMut = actuallyMut + 2L;
}

fun oldGenMut(n: Int64) {
    var i = 0L;

    while i < n/2L {
        oldGenSwapSubtrees();
        i = i + 1L;
    }
}

fun doMutWork(n: Int64) {
    var sum = 0;
    let limit = workUnits.toInt64() * n / 10L;

    var k = 0L;

    while k < limit {
        sum = sum + 1;
        k = k + 1L;
    }

    mutatorSum = mutatorSum + sum.toInt64();
}

fun doYoungGenAlloc(n: Int64, nwords: Int32) {
    let nbytes = nwords * BYTES_PER_WORD;
    var allocated = 0L;

    while allocated < n {
        aexport = Array[Int64](nwords.toInt64());
        allocated = allocated + nbytes.toInt64();
    }

    youngBytes = youngBytes + allocated;
}

fun doStep(n: Int64) {
    let mutations = actuallyMut;

    doYoungGenAlloc(n, WORDS_DEAD);
    doMutWork(n);
    oldGenAlloc(n / promoteRate.toInt64());
    oldGenMut(Int64::max(0L, (mutations + ptrMutRate.toInt64()) - actuallyMut));
}

fun main() {
    rnd = Random(1234567890L);

    if argc() != 5 {
        println("Usage: java GCOld <size> <work> <ratio> <mutation> <steps>");
        println("  where <size> is the live storage in megabytes");
        println("        <work> is the mutator work per step (arbitrary units)");
        println("        <ratio> is the ratio of short-lived to long-lived allocation");
        println("        <mutation> is the mutations per step");
        println("        <steps> is the number of steps");
        exit(1);
    }

    treeSize = heightToBytes(treeHeight);

    size = argv(0).toInt32().unwrap();
    workUnits = argv(1).toInt32().unwrap();
    promoteRate = argv(2).toInt32().unwrap();
    ptrMutRate = argv(3).toInt32().unwrap();
    steps = argv(4).toInt32().unwrap();

    println("GCOld: version 1.0");
    println(size.toString() + " megabytes of live storage");
    println(workUnits.toString() + " work units per step");
    println("promotion ratio is 1:" + promoteRate.toString());
    println("pointer mutation rate is " + ptrMutRate.toString());
    println(steps.toString() + " steps");

    initialize();
    checkTrees();
    youngBytes = 0L;
    nodes = 0L;

    println("Initialization complete...");

    let start = timestamp();

    var step = 0;

    while step < steps {
        doStep(MEG.toInt64());
        step = step + 1;
    }

    let end = timestamp();
    let secs = (end - start).toFloat() / 1000.0F / 1000.0F / 1000.0F;

    checkTrees();

    println("\nTook " + secs.toString() + " sec in steady state.");
    println("Allocated " + steps.toString() + " Mb of young gen garabge"
            + " (= " + (steps.toFloat() / secs).toString() + " Mb/sec)");
    println("    (actually allocated "
            + (youngBytes.toFloat() / MEG.toFloat()).toString() + " megabytes)");
    let promoted = steps.toFloat() / promoteRate.toFloat();
    println("Promoted " + promoted.toString()
            + " Mb (= " + (promoted / secs).toString() + " Mb/sec)");
    println("    (actually promoted " + ((nodes * BYTES_PER_NODE.toInt64()).toFloat() / MEG.toFloat()).toString() + " megabytes)");

    if ptrMutRate != 0 {
        println("Mutated " + actuallyMut.toString() +
                " pointers (= " + (actuallyMut.toFloat() / secs).toString() + " ptrs/sec)");
    }

    println("Checksum = " + (mutatorSum + aexport.length()).toString());
}
