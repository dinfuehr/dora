class TreeNode(var left: Option[TreeNode], var right: Option[TreeNode], var value: Int)

const MEG: Int = 1_000_000;
const INSIGNIFICANT: Long = 999L;
const BYTES_PER_WORD: Int = 8;
const BYTES_PER_NODE: Int = 40;
const WORDS_DEAD: Int = 100;

const treeHeight: Int = 14;
var treeSize: Long;

var youngBytes: Long;
var nodes: Long;
var actuallyMut: Long;
var mutatorSum: Long;
var aexport: Array[Int];

var trees: Array[TreeNode];
var where: Int;
var rnd: Random;

var size: Int;
var workUnits: Int;
var promoteRate: Int;
var ptrMutRate: Int;
var steps: Int;

fun height(t: Option[TreeNode]) -> Int {
    if t.isNone() {
        return 0;
    } else {
        return 1 + Int::max(height(t.unwrap().left), height(t.unwrap().right));
    }
}

fun shortestPath(t: Option[TreeNode]) -> Int {
    if t.isNone() {
        return 0;
    } else {
        return 1 + Int::min(shortestPath(t.unwrap().left), shortestPath(t.unwrap().right));
    }
}

fun heightToNodes(var h: Int) -> Long {
    if (h == 0) {
        return 0L;
    }

    var n: Long = 1L;

    while h > 1 {
        n = n + n;
        h = h - 1;
    }

    return n + n - 1L;
}

fun heightToBytes(h: Int) -> Long {
    return BYTES_PER_NODE.toLong() * heightToNodes(h);
}

fun nodesToHeight(nnodes: Long) -> Int {
    var h = 1;
    var n = 1L;

    while n + n - 1L <= nnodes {
        n = n + n;
        h = h + 1;
    }

    return h - 1;
}

fun bytesToHeight(bytes: Long) -> Int {
    return nodesToHeight(bytes / BYTES_PER_NODE.toLong());
}

fun makeTree(h: Int) -> Option[TreeNode] {
    if h == 0 {
        return none[TreeNode]();
    }

    let res = TreeNode(none[TreeNode](), none[TreeNode](), 0);
    nodes = nodes + 1L;
    res.left = makeTree(h-1);
    res.right = makeTree(h-1);
    res.value = h;
    return some[TreeNode](res);
}

fun initialize() {
    let ntrees = ((size * MEG).toLong() / treeSize).toInt();
    trees = Array[TreeNode](ntrees);

    println("Allocating " + ntrees.toString() + " trees.");
    println("  (" + (ntrees.toLong() * treeSize).toString() + " bytes)");

    var i = 0;

    while i < ntrees {
        trees(i) = makeTree(treeHeight).unwrap();
        i = i + 1;
    }

    println("  (" + nodes.toString() + " nodes)");
}

fun checkTrees() {
    let ntrees = trees.length();

    var i = 0;

    while i < ntrees {
        let t = trees(i);

        let h1 = height(some[TreeNode](t));
        let h2 = shortestPath(some[TreeNode](t));

        if (h1 != treeHeight) || (h2 != treeHeight) {
            println("**** BUG: " + h1.toString() + " " + h2.toString());
        }

        i = i + 1;
    }
}

fun replaceTreeWork(full: TreeNode, partial: TreeNode, dir: Bool) {
    let canGoLeft = full.left.isSome() && full.left.unwrap().value > partial.value;
    let canGoRight = full.right.isSome() && full.right.unwrap().value > partial.value;

    if canGoLeft && canGoRight {
        if dir {
            replaceTreeWork(full.left.unwrap(), partial, !dir);
        } else {
            replaceTreeWork(full.right.unwrap(), partial, !dir);
        }
    } else if !canGoLeft && !canGoRight {
        if dir {
            full.left = some[TreeNode](partial);
        } else {
            full.right = some[TreeNode](partial);
        }
    } else if !canGoLeft {
        full.left = some[TreeNode](partial);
    } else {
        full.right = some[TreeNode](partial);
    }
}

fun replaceTree(full: TreeNode, partial: TreeNode) {
    let dir = (partial.value % 2) == 0;
    actuallyMut = actuallyMut + 1L;
    replaceTreeWork(full, partial, dir);
}

fun oldGenAlloc(n: Long) {
    let full = (n / treeSize).toInt();
    var partial = n % treeSize;

    var i = 0;
    while i < full {
        trees(where) = makeTree(treeHeight).unwrap();
        where = where + 1;

        if where == trees.length() {
            where = 0;
        }

        i = i + 1;
    }

    while partial > INSIGNIFICANT {
        let h = bytesToHeight(partial);
        let newTree = makeTree(h);
        replaceTree(trees(where), newTree.unwrap());
        where = where + 1;

        if where == trees.length() {
            where = 0;
        }

        partial = partial - heightToBytes(h);
    }
}

fun oldGenSwapSubtrees() {
    let index1 = rnd.nextIntWithBound(trees.length());
    let index2 = rnd.nextIntWithBound(trees.length());
    let depth = rnd.nextIntWithBound(treeHeight);
    var path = rnd.nextInt();

    var tn1 = trees(index1);
    var tn2 = trees(index2);

    var i = 0;

    while i < depth {
        if (path & 1) == 0 {
            tn1 = tn1.left.unwrap();
            tn2 = tn2.left.unwrap();
        } else {
            tn1 = tn1.right.unwrap();
            tn2 = tn2.right.unwrap();
        }

        path = path >> 1;
        i = i + 1;
    }

    if (path & 1) == 0 {
        let tmp = tn1.left;
        tn1.left = tn2.left;
        tn2.left = tmp;
    } else {
        let tmp = tn1.right;
        tn1.right = tn2.right;
        tn2.right = tmp;
    }
    actuallyMut = actuallyMut + 2L;
}

fun oldGenMut(n: Long) {
    var i = 0L;

    while i < n/2L {
        oldGenSwapSubtrees();
        i = i + 1L;
    }
}

fun doMutWork(n: Long) {
    var sum = 0;
    let limit = workUnits.toLong() * n / 10L;

    var k = 0L;

    while k < limit {
        sum = sum + 1;
        k = k + 1L;
    }

    mutatorSum = mutatorSum + sum.toLong();
}

fun doYoungGenAlloc(n: Long, nwords: Int) {
    let nbytes = nwords * BYTES_PER_WORD;
    var allocated = 0L;

    while allocated < n {
        aexport = Array[Long](nwords);
        allocated = allocated + nbytes.toLong();
    }

    youngBytes = youngBytes + allocated;
}

fun doStep(n: Long) {
    let mutations = actuallyMut;

    doYoungGenAlloc(n, WORDS_DEAD);
    doMutWork(n);
    oldGenAlloc(n / promoteRate.toLong());
    oldGenMut(Long::max(0L, (mutations + ptrMutRate.toLong()) - actuallyMut));
}

fun main() {
    rnd = Random(1234567890L);

    if argc() != 5 {
        println("Usage: java GCOld <size> <work> <ratio> <mutation> <steps>");
        println("  where <size> is the live storage in megabytes");
        println("        <work> is the mutator work per step (arbitrary units)");
        println("        <ratio> is the ratio of short-lived to long-lived allocation");
        println("        <mutation> is the mutations per step");
        println("        <steps> is the number of steps");
        exit(1);
    }

    treeSize = heightToBytes(treeHeight);

    size = argv(0).parseInt();
    workUnits = argv(1).parseInt();
    promoteRate = argv(2).parseInt();
    ptrMutRate = argv(3).parseInt();
    steps = argv(4).parseInt();

    println("GCOld: version 1.0");
    println(size.toString() + " megabytes of live storage");
    println(workUnits.toString() + " work units per step");
    println("promotion ratio is 1:" + promoteRate.toString());
    println("pointer mutation rate is " + ptrMutRate.toString());
    println(steps.toString() + " steps");

    initialize();
    checkTrees();
    youngBytes = 0L;
    nodes = 0L;

    println("Initialization complete...");

    let start = timestamp();

    var step = 0;

    while step < steps {
        doStep(MEG.toLong());
        step = step + 1;
    }

    let end = timestamp();
    let secs = (end - start).toFloat() / 1000.0F / 1000.0F / 1000.0F;

    checkTrees();

    println("\nTook " + secs.toString() + " sec in steady state.");
    println("Allocated " + steps.toString() + " Mb of young gen garabge"
            + " (= " + (steps.toFloat() / secs).toString() + " Mb/sec)");
    println("    (actually allocated "
            + (youngBytes.toFloat() / MEG.toFloat()).toString() + " megabytes)");
    let promoted = steps.toFloat() / promoteRate.toFloat();
    println("Promoted " + promoted.toString()
            + " Mb (= " + (promoted / secs).toString() + " Mb/sec)");
    println("    (actually promoted " + ((nodes * BYTES_PER_NODE.toLong()).toFloat() / MEG.toFloat()).toString() + " megabytes)");

    if ptrMutRate != 0 {
        println("Mutated " + actuallyMut.toString() +
                " pointers (= " + (actuallyMut.toFloat() / secs).toString() + " ptrs/sec)");
    }

    println("Checksum = " + (mutatorSum + aexport.length().toLong()).toString());
}
