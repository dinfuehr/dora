class TreeNode {
    left: Option[TreeNode],
    right: Option[TreeNode],
    value: Int32,
}

const MEG: Int32 = 1_000_000i32;
const INSIGNIFICANT: Int64 = 999i64;
const BYTES_PER_WORD: Int32 = 8i32;
const BYTES_PER_NODE: Int32 = 40i32;
const WORDS_DEAD: Int32 = 100i32;

const treeHeight: Int32 = 14i32;
let mut treeSize: Int64 = 0i64;

let mut youngBytes: Int64 = 0i64;
let mut nodes: Int64 = 0i64;
let mut actuallyMut: Int64 = 0i64;
let mut mutatorSum: Int64 = 0i64;
let mut aexport: Array[Int64] = Array[Int64]::new();

let mut trees: Array[TreeNode] = Array[TreeNode]::new();
let mut where: Int32 = 0i32;
let mut rnd: std::Random = std::Random::new(0i64);

let mut size: Int32 = 0i32;
let mut workUnits: Int32 = 0i32;
let mut promoteRate: Int32 = 0i32;
let mut ptrMutRate: Int32 = 0i32;
let mut steps: Int32 = 0i32;

fun height(t: Option[TreeNode]): Int32 {
    if t.isNone() {
        return 0i32;
    } else {
        return 1i32 + Int32::max(height(t.getOrPanic().left), height(t.getOrPanic().right));
    }
}

fun shortestPath(t: Option[TreeNode]): Int32 {
    if t.isNone() {
        return 0i32;
    } else {
        return 1i32 + Int32::min(shortestPath(t.getOrPanic().left), shortestPath(t.getOrPanic().right));
    }
}

fun heightToNodes(h: Int32): Int64 {
    let mut h = h;
    if (h == 0i32) {
        return 0i64;
    }

    let mut n: Int64 = 1i64;

    while h > 1i32 {
        n = n + n;
        h = h - 1i32;
    }

    return n + n - 1i64;
}

fun heightToBytes(h: Int32): Int64 {
    return BYTES_PER_NODE.toInt64() * heightToNodes(h);
}

fun nodesToHeight(nnodes: Int64): Int32 {
    let mut h = 1i32;
    let mut n = 1i64;

    while n + n - 1i64 <= nnodes {
        n = n + n;
        h = h + 1i32;
    }

    return h - 1i32;
}

fun bytesToHeight(bytes: Int64): Int32 {
    return nodesToHeight(bytes / BYTES_PER_NODE.toInt64());
}

fun makeTree(h: Int32): TreeNode {
    let res = TreeNode(Option[TreeNode]::None, Option[TreeNode]::None, 0i32);
    nodes = nodes + 1i64;
    if h > 1i32 {
        res.left = Option[TreeNode]::Some(makeTree(h-1i32));
        res.right = Option[TreeNode]::Some(makeTree(h-1i32));
    }
    res.value = h;
    res
}

fun initialize(): Unit {
    let ntrees = (size * MEG).toInt64() / treeSize;
    let default = TreeNode(Option[TreeNode]::None, Option[TreeNode]::None, 0i32);
    trees = Array[TreeNode]::fill(ntrees, default);

    println("Allocating " + ntrees.toString() + " trees.");
    println("  (" + (ntrees * treeSize).toString() + " bytes)");

    let mut i = 0i64;

    while i < ntrees {
        trees(i) = makeTree(treeHeight);
        i = i + 1i64;
    }

    println("  (" + nodes.toString() + " nodes)");
}

fun checkTrees(): Unit {
    let ntrees = trees.size();

    let mut i = 0i64;

    while i < ntrees {
        let t = trees(i);

        let h1 = height(Option[TreeNode]::Some(t));
        let h2 = shortestPath(Option[TreeNode]::Some(t));

        if (h1 != treeHeight) || (h2 != treeHeight) {
            println("**** BUG: " + h1.toString() + " " + h2.toString());
        }

        i = i + 1i64;
    }
}

fun replaceTreeWork(full: TreeNode, partial: TreeNode, dir: Bool): Unit {
    let canGoLeft = full.left.isSome() && full.left.getOrPanic().value > partial.value;
    let canGoRight = full.right.isSome() && full.right.getOrPanic().value > partial.value;

    if canGoLeft && canGoRight {
        if dir {
            replaceTreeWork(full.left.getOrPanic(), partial, dir.not());
        } else {
            replaceTreeWork(full.right.getOrPanic(), partial, dir.not());
        }
    } else if canGoLeft.not() && canGoRight.not() {
        if dir {
            full.left = Option[TreeNode]::Some(partial);
        } else {
            full.right = Option[TreeNode]::Some(partial);
        }
    } else if canGoLeft.not() {
        full.left = Option[TreeNode]::Some(partial);
    } else {
        full.right = Option[TreeNode]::Some(partial);
    }
}

fun replaceTree(full: TreeNode, partial: TreeNode): Unit {
    let dir = partial.value.remainder(2i32) == 0i32;
    actuallyMut = actuallyMut + 1i64;
    replaceTreeWork(full, partial, dir);
}

fun oldGenAlloc(n: Int64): Unit {
    let full = n / treeSize;
    let mut partial = n.remainder(treeSize);

    let mut i = 0i64;
    while i < full {
        trees(where.toInt64()) = makeTree(treeHeight);
        where = where + 1i32;

        if where.toInt64() == trees.size() {
            where = 0i32;
        }

        i = i + 1i64;
    }

    while partial > INSIGNIFICANT {
        let h = bytesToHeight(partial);
        let newTree = makeTree(h);
        replaceTree(trees(where.toInt64()), newTree);
        where = where + 1i32;

        if where.toInt64() == trees.size() {
            where = 0i32;
        }

        partial = partial - heightToBytes(h);
    }
}

fun oldGenSwapSubtrees(): Unit {
    let index1 = rnd.nextInt32WithBound(trees.size().toInt32());
    let index2 = rnd.nextInt32WithBound(trees.size().toInt32());
    let depth = rnd.nextInt32WithBound(treeHeight);
    let mut path = rnd.nextInt32();

    let mut tn1 = trees(index1.toInt64());
    let mut tn2 = trees(index2.toInt64());

    let mut i = 0i32;

    while i < depth {
        if (path & 1i32) == 0i32 {
            tn1 = tn1.left.getOrPanic();
            tn2 = tn2.left.getOrPanic();
        } else {
            tn1 = tn1.right.getOrPanic();
            tn2 = tn2.right.getOrPanic();
        }

        path = path.shiftRightSigned(1i32);
        i = i + 1i32;
    }

    if (path & 1i32) == 0i32 {
        let tmp = tn1.left;
        tn1.left = tn2.left;
        tn2.left = tmp;
    } else {
        let tmp = tn1.right;
        tn1.right = tn2.right;
        tn2.right = tmp;
    }
    actuallyMut = actuallyMut + 2i64;
}

fun oldGenMut(n: Int64): Unit {
    let mut i = 0i64;

    while i < n/2i64 {
        oldGenSwapSubtrees();
        i = i + 1i64;
    }
}

fun doMutWork(n: Int64): Unit {
    let mut sum = 0i32;
    let limit = workUnits.toInt64() * n / 10i64;

    let mut k = 0i64;

    while k < limit {
        sum = sum + 1i32;
        k = k + 1i64;
    }

    mutatorSum = mutatorSum + sum.toInt64();
}

fun doYoungGenAlloc(n: Int64, nwords: Int32): Unit {
    let nbytes = nwords * BYTES_PER_WORD;
    let mut allocated = 0i64;

    while allocated < n {
        aexport = Array[Int64]::zero(nwords.toInt64());
        allocated = allocated + nbytes.toInt64();
    }

    youngBytes = youngBytes + allocated;
}

fun doStep(n: Int64): Unit {
    let mutations = actuallyMut;

    doYoungGenAlloc(n, WORDS_DEAD);
    doMutWork(n);
    oldGenAlloc(n / promoteRate.toInt64());
    oldGenMut(Int64::max(0i64, (mutations + ptrMutRate.toInt64()) - actuallyMut));
}

fun main(): Unit {
    rnd = std::Random::new(1234567890i64);

    if std::argc() != 5i32 {
        println("Usage: java GCOld <size> <work> <ratio> <mutation> <steps>");
        println("  where <size> is the live storage in megabytes");
        println("        <work> is the mutator work per step (arbitrary units)");
        println("        <ratio> is the ratio of short-lived to long-lived allocation");
        println("        <mutation> is the mutations per step");
        println("        <steps> is the number of steps");
        std::exit(1i32);
    }

    treeSize = heightToBytes(treeHeight);

    size = std::argv(0i32).toInt32().getOrPanic();
    workUnits = std::argv(1i32).toInt32().getOrPanic();
    promoteRate = std::argv(2i32).toInt32().getOrPanic();
    ptrMutRate = std::argv(3i32).toInt32().getOrPanic();
    steps = std::argv(4i32).toInt32().getOrPanic();

    println("GCOld: version 1.0");
    println(size.toString() + " megabytes of live storage");
    println(workUnits.toString() + " work units per step");
    println("promotion ratio is 1:" + promoteRate.toString());
    println("pointer mutation rate is " + ptrMutRate.toString());
    println(steps.toString() + " steps");

    initialize();
    checkTrees();
    youngBytes = 0i64;
    nodes = 0i64;

    println("Initialization complete...");

    let start = std::timestamp();

    let mut step = 0i32;

    while step < steps {
        doStep(MEG.toInt64());
        step = step + 1i32;
    }

    let end = std::timestamp();
    let secs = (end - start).toFloat32() / 1000.0f32 / 1000.0f32 / 1000.0f32;

    checkTrees();

    println("\nTook " + secs.toString() + " sec in steady state.");
    println("Allocated " + steps.toString() + " Mb of young gen garbage"
            + " (= " + (steps.toFloat32() / secs).toString() + " Mb/sec)");
    println("    (actually allocated "
            + (youngBytes.toFloat32() / MEG.toFloat32()).toString() + " megabytes)");
    let promoted = steps.toFloat32() / promoteRate.toFloat32();
    println("Promoted " + promoted.toString()
            + " Mb (= " + (promoted / secs).toString() + " Mb/sec)");
    println("    (actually promoted " + ((nodes * BYTES_PER_NODE.toInt64()).toFloat32() / MEG.toFloat32()).toString() + " megabytes)");

    if ptrMutRate != 0i32 {
        println("Mutated " + actuallyMut.toString() +
                " pointers (= " + (actuallyMut.toFloat32() / secs).toString() + " ptrs/sec)");
    }

    println("Checksum = " + (mutatorSum + aexport.size()).toString());
}
