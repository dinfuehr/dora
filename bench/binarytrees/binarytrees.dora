fun main() {
  var maxDepth = 6;

  if argc() > 0 {
    maxDepth = argv(0).toInt32().unwrap();
  }

  if 6 > maxDepth {
    maxDepth = 6;
  }

  let stretchDepth = maxDepth + 1;
  println("stretch tree of depth " + stretchDepth.toString() +
          "\t check: " + checkTree(createTree(stretchDepth)).toString());

  trees(maxDepth);
}

fun trees(maxDepth: Int32) {
  let longLastingNode = createTree(maxDepth);
  var depth = 4;

  while depth <= maxDepth {
    let iterations = 16 << (maxDepth - depth);

    loops(iterations, depth);
    depth = depth + 2;
  }

  println("long lived tree of depth " + maxDepth.toString()
    + "\t check: " + checkTree(longLastingNode).toString());
}

fun loops(iterations: Int32, depth: Int32) {
  var check = 0;
  var item = 0;

  while item < iterations {
    check = check + checkTree(createTree(depth));
    item = item + 1;
  }

  println(iterations.toString() + "\t trees of depth " +
    depth.toString() + "\t check: " + check.toString());
}

fun createTree(depth: Int32) -> TreeNode {
  var depth = depth;
  let node = TreeNode(0, nil, nil);

  if depth > 0 {
    depth = depth - 1;
    node.left = createTree(depth);
    node.right = createTree(depth);
  }

  return node;
}

fun checkTree(node: TreeNode) -> Int32 {
  if node.left === nil {
    return 1;
  }

  return checkTree(node.left) + checkTree(node.right) + 1;
}

class TreeNode(let item: Int32, var left: TreeNode, var right: TreeNode)
