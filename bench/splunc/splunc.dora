import std::argc;
import std::argv;
import std::exit;
import std::Random;

@open @abstract class Node {
    var birthday: Int32 = 0;
    var value: Int32 = 0;
    var left: Option[Node] = Option[Node]::None;
    var right: Option[Node] = Option[Node]::None;

    fun printAge(date: Int32) {
        println((date - self.birthday).toString());
    }

    fun printAges(date: Int32) {
        self.printAge(date);

        if self.left.isSome() {
            self.left.unwrap().printAges(date);
        }

        if self.right.isSome() {
            self.right.unwrap().printAges(date);
        }
    }

    fun printTree(date: Int32, prefix: String) {
        print(prefix);
        print("age: " + (date - self.birthday).toString());
        println(" value: " + self.value.toString());

        if self.left.isSome() {
            self.left.unwrap().printTree(date, prefix + "  ");
        }

        if self.right.isSome() {
            self.right.unwrap().printTree(date, prefix + "  ");
        }
    }
}

module Node {
    fun splay(tree: Option[Node], node: Node): Option[Node] {
        if tree.isNone() {
            return Option[Node]::None;
        }
        var tree = tree.unwrap();

        node.left = Option[Node]::None;
        node.right = Option[Node]::None;

        var l: Node = node;
        var r: Node = node;

        while true {
            if node.value <= tree.value {
                if tree.left.isNone() {
                    break;
                }

                if node.value <= tree.left.unwrap().value {
                    // rotate right
                    let y = tree.left.unwrap();
                    tree.left = y.right;
                    y.right = Option[Node]::Some(tree);
                    tree = y;
                    if tree.left.isNone() {
                        break;
                    }
                }

                // link right
                r.left = Option[Node]::Some(tree);
                r = tree;
                tree = tree.left.unwrap();

            } else if node.value > tree.value {
                if tree.right.isNone() {
                    break;
                }

                if node.value > tree.right.unwrap().value {
                    // rotate left
                    let y = tree.right.unwrap();
                    tree.right = y.left;
                    y.left = Option[Node]::Some(tree);
                    tree = y;
                    if tree.right.isNone() {
                        break;
                    }
                }

                // link left
                l.right = Option[Node]::Some(tree);
                l = tree;
                tree = tree.right.unwrap();

            } else {
                break;
            }
        }

        l.right = tree.left;
        r.left = tree.right;
        tree.left = node.right;
        tree.right = node.left;
        Option[Node]::Some(tree)
    }

    fun insert(tree: Option[Node], node: Node): Node {
        if tree.isNone() {
            return node;
        }

        let tree = Node::splay(tree, node).unwrap();

        if node.value <= tree.value {
            node.left = tree.left;
            node.right = Option[Node]::Some(tree);
            tree.left = Option[Node]::None;
        } else { // if (node.value > true.value)
            node.right = tree.right;
            node.left = Option[Node]::Some(tree);
            tree.right = Option[Node]::None;
        }

        return node;
    }

    fun trunc(date: Int32, tree: Node, depth: Int32) {
        if tree.left.isSome() {
            if depth == 0 {
                /*if tree.left !== nil {
                    println("truncate left");
                }*/
                tree.left = Option[Node]::None;
            } else {
                Node::trunc(date, tree.left.unwrap(), depth-1);
            }
        }

        if tree.right.isSome() {
            if depth == 0 {
                /*if tree.right !== nil {
                    println("truncate right");
                }*/
                tree.right = Option[Node]::None;
            } else {
                Node::trunc(date, tree.right.unwrap(), depth-1);
            }
        }
    }

    fun countNodes(node: Option[Node]): Int32 {
        if node.isNone() {
            return 0;
        }
        let node = node.unwrap();
        return 1 + Node::countNodes(node.left) + Node::countNodes(node.right);
    }
}

class NodeA extends Node {}
class NodeB extends Node { let x1: Int64 = 0L; }
class NodeC extends Node { let x1: Int64 = 0L; let x2: Int64 = 0L; }
class NodeD extends Node { let x1: Float64 = 0.0; let x2: Float64 = 0.0; let x3: Float64 = 0.0; }

class Node40 extends Node {}
class Node48 extends Node { let x1: Int64 = 0L; }
class Node56 extends Node { let x1: Int64 = 0L; let x2: Int64 = 0L; }
class Node64 extends Node { let x1: Int64 = 0L; let x2: Int64 = 0L; let x3: Int64 = 0L; }
class Node72 extends Node { let x1: Int64 = 0L; let x2: Int64 = 0L; let x3: Int64 = 0L; let x4: Int64 = 0L; }
class Node80 extends Node { let x1: Int64 = 0L; let x2: Int64 = 0L; let x3: Int64 = 0L; let x4: Int64 = 0L; let x5: Int64 = 0L; }

fun main() {
    if argc() < 4 {
        println("usage: splunc <seed> <max-size> <iterations> <trunc-depth> [<iterations> <trunc-depth>]");
        exit(1);
    }

    let rnd = Random(argv(0).toInt64().unwrap());
    let max_size = argv(1).toInt32().unwrap();
    var date = 0;

    var tree: Node = NodeA();
    tree.birthday = date;
    date = date + 1;
    tree.value = rnd.nextInt32WithBound(max_size);

    var j = 2;

    while j + 2 <= argc() {
        let n = argv(j).toInt32().unwrap();
        let depth = argv(j).toInt32().unwrap();

        var i = 0;
        while i < n {
            let value = rnd.nextInt32WithBound(4);

            let node = if value == 0 {
                NodeA() as Node
            } else if value == 1 {
                NodeB() as Node
            } else if value == 2 {
                NodeC() as Node
            } else {
                assert(value == 3);
                NodeD() as Node
            };

            node.birthday = date;
            date = date + 1;
            node.value = rnd.nextInt32WithBound(max_size);

            tree = Node::insert(Option[Node]::Some(tree), node);
            Node::trunc(date, tree, depth);

            // println("nodes in tree: " + Node::countNodes(tree).toString());

            i = i + 1;
        }

        j = j + 2;
    }
}
