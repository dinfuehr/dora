import std::argc;
import std::argv;
import std::exit;

@open @abstract class Node {
    var birthday: Int32 = 0I;
    var value: Int32 = 0I;
    var left: Option[Node] = Option[Node]::None;
    var right: Option[Node] = Option[Node]::None;

    fun printAge(date: Int32) {
        println((date - self.birthday).toString());
    }

    fun printAges(date: Int32) {
        self.printAge(date);

        if self.left.isSome() {
            self.left.getOrPanic().printAges(date);
        }

        if self.right.isSome() {
            self.right.getOrPanic().printAges(date);
        }
    }

    fun printTree(date: Int32, prefix: String) {
        print(prefix);
        print("age: " + (date - self.birthday).toString());
        println(" value: " + self.value.toString());

        if self.left.isSome() {
            self.left.getOrPanic().printTree(date, prefix + "  ");
        }

        if self.right.isSome() {
            self.right.getOrPanic().printTree(date, prefix + "  ");
        }
    }

    @static fun splay(tree: Option[Node], node: Node): Option[Node] {
        if tree.isNone() {
            return Option[Node]::None;
        }
        var tree = tree.getOrPanic();

        node.left = Option[Node]::None;
        node.right = Option[Node]::None;

        var l: Node = node;
        var r: Node = node;

        while true {
            if node.value <= tree.value {
                if tree.left.isNone() {
                    break;
                }

                if node.value <= tree.left.getOrPanic().value {
                    // rotate right
                    let y = tree.left.getOrPanic();
                    tree.left = y.right;
                    y.right = Option[Node]::Some(tree);
                    tree = y;
                    if tree.left.isNone() {
                        break;
                    }
                }

                // link right
                r.left = Option[Node]::Some(tree);
                r = tree;
                tree = tree.left.getOrPanic();

            } else if node.value > tree.value {
                if tree.right.isNone() {
                    break;
                }

                if node.value > tree.right.getOrPanic().value {
                    // rotate left
                    let y = tree.right.getOrPanic();
                    tree.right = y.left;
                    y.left = Option[Node]::Some(tree);
                    tree = y;
                    if tree.right.isNone() {
                        break;
                    }
                }

                // link left
                l.right = Option[Node]::Some(tree);
                l = tree;
                tree = tree.right.getOrPanic();

            } else {
                break;
            }
        }

        l.right = tree.left;
        r.left = tree.right;
        tree.left = node.right;
        tree.right = node.left;
        Option[Node]::Some(tree)
    }

    @static fun insert(tree: Option[Node], node: Node): Node {
        if tree.isNone() {
            return node;
        }

        let tree = Node::splay(tree, node).getOrPanic();

        if node.value <= tree.value {
            node.left = tree.left;
            node.right = Option[Node]::Some(tree);
            tree.left = Option[Node]::None;
        } else { // if (node.value > true.value)
            node.right = tree.right;
            node.left = Option[Node]::Some(tree);
            tree.right = Option[Node]::None;
        }

        return node;
    }

    @static fun trunc(date: Int32, tree: Node, depth: Int32) {
        if tree.left.isSome() {
            if depth == 0I {
                /*if tree.left !== nil {
                    println("truncate left");
                }*/
                tree.left = Option[Node]::None;
            } else {
                Node::trunc(date, tree.left.getOrPanic(), depth-1I);
            }
        }

        if tree.right.isSome() {
            if depth == 0I {
                /*if tree.right !== nil {
                    println("truncate right");
                }*/
                tree.right = Option[Node]::None;
            } else {
                Node::trunc(date, tree.right.getOrPanic(), depth-1I);
            }
        }
    }

    @static fun countNodes(node: Option[Node]): Int32 {
        if node.isNone() {
            return 0I;
        }
        let node = node.getOrPanic();
        return 1I + Node::countNodes(node.left) + Node::countNodes(node.right);
    }
}

class NodeA extends Node {}
class NodeB extends Node { let x1: Int64 = 0L; }
class NodeC extends Node { let x1: Int64 = 0L; let x2: Int64 = 0L; }
class NodeD extends Node { let x1: Float64 = 0.0; let x2: Float64 = 0.0; let x3: Float64 = 0.0; }

class Node40 extends Node {}
class Node48 extends Node { let x1: Int64 = 0L; }
class Node56 extends Node { let x1: Int64 = 0L; let x2: Int64 = 0L; }
class Node64 extends Node { let x1: Int64 = 0L; let x2: Int64 = 0L; let x3: Int64 = 0L; }
class Node72 extends Node { let x1: Int64 = 0L; let x2: Int64 = 0L; let x3: Int64 = 0L; let x4: Int64 = 0L; }
class Node80 extends Node { let x1: Int64 = 0L; let x2: Int64 = 0L; let x3: Int64 = 0L; let x4: Int64 = 0L; let x5: Int64 = 0L; }

fun main() {
    if argc() < 4I {
        println("usage: splunc <seed> <max-size> <iterations> <trunc-depth> [<iterations> <trunc-depth>]");
        exit(1I);
    }

    let rnd = std::Random(argv(0I).toInt64().getOrPanic());
    let max_size = argv(1I).toInt32().getOrPanic();
    var date = 0I;

    var tree: Node = NodeA();
    tree.birthday = date;
    date = date + 1I;
    tree.value = rnd.nextInt32WithBound(max_size);

    var j = 2I;

    while j + 2I <= argc() {
        let n = argv(j).toInt32().getOrPanic();
        let depth = argv(j).toInt32().getOrPanic();

        var i = 0I;
        while i < n {
            let value = rnd.nextInt32WithBound(4I);

            let node = if value == 0I {
                NodeA() as Node
            } else if value == 1I {
                NodeB() as Node
            } else if value == 2I {
                NodeC() as Node
            } else {
                assert(value == 3I);
                NodeD() as Node
            };

            node.birthday = date;
            date = date + 1I;
            node.value = rnd.nextInt32WithBound(max_size);

            tree = Node::insert(Option[Node]::Some(tree), node);
            Node::trunc(date, tree, depth);

            // println("nodes in tree: " + Node::countNodes(tree).toString());

            i = i + 1I;
        }

        j = j + 2I;
    }
}
