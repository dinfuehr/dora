use std::fatalError;
use std::timestamp;
use std::string::Stringable;

fn main() {
    if std::argc() != 3i32 {
        println("usage: splay <seed> <tree-size> <iterations>");
        std::exit(1i32);
    }

    rnd = std::Random::new(std::argv(0i32).toInt64().getOrPanic());
    splayTreeSize = std::argv(1i32).toInt32().getOrPanic();
    let iterations = std::argv(2i32).toInt32().getOrPanic();

    let bench = Benchmark::new();
    bench.setup();

    let mut i = 0i32;
    while i < iterations {
        bench.run();
        i = i + 1i32;
    }

    bench.tearDown();
}

let mut rnd: std::Random = std::Random::new(0i64);

let mut splayTreeSize: Int32 = 0i32;
const splayTreeModifications: Int32 = 1000i32;
const splayTreePayloadDepth: Int32 = 5i32;

class Benchmark {
    splayTree: SplayTree,
    splaySamples: Int32,
    splaySampleTimeStart: Int64,
    splaySumOfSquaredPauses: Int64,
}

impl Benchmark {
    static fn new(): Benchmark {
        Benchmark(
            splayTree = SplayTree::new(),
            splaySamples = 0i32,
            splaySampleTimeStart = 0,
            splaySumOfSquaredPauses = 0,
        )
    }

    fn setup() {
        let mut i = 0i32;
        self.splaySampleTimeStart = timestamp();

        while i < splayTreeSize {
            self.insertNewNode();

            if (i+1i32) % 20i32 == 19i32 {
                // self.updateStats(timestamp());
            }

            i = i + 1i32;
        }
    }

    fn updateStats(time: Int64) {
        let pause = time - self.splaySampleTimeStart;
        self.splaySampleTimeStart = time;
        self.splaySamples = self.splaySamples + 1i32;
        self.splaySumOfSquaredPauses = self.splaySumOfSquaredPauses + pause * pause;
    }

    fn tearDown() {
        let keys = self.splayTree.exportKeys();

        self.splaySamples = 0i32;
        self.splaySumOfSquaredPauses = 0i64;

        let mut length = keys.size();

        if length != splayTreeSize.toInt64() {
            fatalError[()]("Splay tree has wrong sie");
        }

        let mut i = 0i64;

        while i < length - 1i64 {
            if keys(i) >= keys(i+1i64) {
                fatalError[()]("Splay tree not sorted");
            }

            i = i + 1i64;
        }
    }

    fn run() {
        let mut i = 0i32;

        while i < splayTreeModifications {
            let key = self.insertNewNode();
            let greatest = self.splayTree.findGreatestLessThan(key);

            if greatest.isNone() {
                self.splayTree.remove(key);
            } else {
                self.splayTree.remove(greatest.getOrPanic().key);
            }

            i = i + 1i32;
        }

        // self.updateStats(timestamp());
    }

    fn insertNewNode(): Int32 {
        let mut key: Int32 = 0i32;

        while true {
            key = self.generateKey();

            if self.splayTree.find(key).isNone() {
                break;
            }
        }

        let payload = generatePayloadTree(splayTreePayloadDepth, key.toString());
        self.splayTree.insert(key, payload);

        return key;
    }

    fn generateKey(): Int32 {
        return rnd.nextInt32();
    }

    fn rms() {
        let pauses = self.splaySumOfSquaredPauses;
        let samples = self.splaySamples;

        (pauses.toFloat64() / samples.toFloat64());
    }
}

fn generatePayloadTree(depth: Int32, tag: String): PayloadNode {
    if depth == 0i32 {
        let arr = Array[Int32]::zero(10i64);
        let mut i = 0i32;

        while i < 10i32 {
            arr(i.toInt64()) = i;
            i = i + 1i32;
        }

        let text = "String for key " + tag + " in leaf node";
        return PayloadNode(
            array = Option[Array[Int32]]::Some(arr),
            text,
            left = Option[PayloadNode]::None,
            right = Option[PayloadNode]::None);
    } else {
        return PayloadNode(
            array = Option[Array[Int32]]::None,
            text = "",
            left = Option[PayloadNode]::Some(generatePayloadTree(depth-1i32, tag)),
            right = Option[PayloadNode]::Some(generatePayloadTree(depth-1i32, tag)),
        );
    }
}

class SplayTree {
    root: Option[SplayNode],
}

impl SplayTree {
    static fn new(): SplayTree {
        SplayTree(None[SplayNode])
    }

    fn isEmpty(): Bool {
        return self.root.isNone();
    }

    fn insert(key: Int32, value: PayloadNode) {
        if self.isEmpty() {
            self.root = Some(SplayNode::new(key, Some[PayloadNode](value)));
            return;
        }

        self.splay(key);

        let root = self.root.getOrPanic();

        if root.key == key {
            return;
        }

        let node = SplayNode::new(key, Some[PayloadNode](value));

        if key > root.key {
            node.left = self.root;
            node.right = root.right;
            root.right = None;

        } else {
            node.right = self.root;
            node.left = root.left;
            root.left = None;
        }

        self.root = Some(node);
    }

    fn remove(key: Int32): Option[SplayNode] {
        if self.isEmpty() {
            return Option[SplayNode]::None;
        }

        self.splay(key);
        let root = self.root.getOrPanic();

        if root.key != key {
            return None;
        }

        let removed = self.root;

        if root.left.isNone() {
            self.root = root.right;
        } else {
            let right = root.right;
            self.root = root.left;
            self.splay(key);
            self.root.getOrPanic().right = right;
        }

        return removed;
    }

    fn find(key: Int32): Option[SplayNode] {
        if self.isEmpty() {
            return None;
        }

        self.splay(key);
        let root = self.root.getOrPanic();

        if root.key == key {
            return self.root;
        } else {
            return None;
        }
    }

    fn findMax(startNode: Option[SplayNode]): Option[SplayNode] {
        if self.isEmpty() {
            return None;
        }

        let mut current = if startNode.isNone() {
            self.root.getOrPanic()
        } else {
            startNode.getOrPanic()
        };

        while current.right.isSome() {
            current = current.right.getOrPanic();
        }

        Some(current)
    }

    fn findGreatestLessThan(key: Int32): Option[SplayNode] {
        if self.isEmpty() {
            return None;
        }

        self.splay(key);
        let root = self.root.getOrPanic();

        if root.key < key {
            self.root
        } else if root.left.isSome() {
            self.findMax(root.left)
        } else {
            None[SplayNode]
        }
    }

    fn exportKeys(): Vec[Int32] {
        let keys = Vec[Int32]::new();
        exportKeysTraverse(keys, self.root);
        return keys;
    }

    fn splay(key: Int32) {
        if self.isEmpty() {
            return;
        }

        let mut dummy = SplayNode::new(0i32, Option[PayloadNode]::None);
        let mut left = dummy;
        let mut right = dummy;

        let mut current = self.root.getOrPanic();

        while true {
            if key < current.key {
                if current.left.isNone() {
                    break;
                }

                if key < current.left.getOrPanic().key {
                    // Rotate right
                    let tmp = current.left.getOrPanic();
                    current.left = tmp.right;
                    tmp.right = Some[SplayNode](current);
                    current = tmp;

                    if current.left.isNone() {
                        break;
                    }
                }

                // Link right
                right.left = Some[SplayNode](current);
                right = current;
                current = current.left.getOrPanic();
            } else if key > current.key {
                if current.right.isNone() {
                    break;
                }

                if key > current.right.getOrPanic().key {
                    // Rotate left
                    let tmp = current.right.getOrPanic();
                    current.right = tmp.left;
                    tmp.left = Some(current);
                    current = tmp;

                    if current.right.isNone() {
                        break;
                    }
                }

                // Link left
                left.right = Some(current);
                left = current;
                current = current.right.getOrPanic();
            } else {
                break;
            }
        }

        // Assemble
        left.right = current.left;
        right.left = current.right;
        current.left = dummy.right;
        current.right = dummy.left;
        self.root = Some(current);
    }
}

fn exportKeysTraverse(list: Vec[Int32], node: Option[SplayNode]) {
    let mut current = node;

    while current.isSome() {
        let c = current.getOrPanic();
        let mut left = c.left;
        if left.isSome() {
            exportKeysTraverse(list, left);
        }
        list.push(c.key);
        current = c.right;
    }
}

class SplayNode {
    key: Int32,
    value: Option[PayloadNode],
    left: Option[SplayNode],
    right: Option[SplayNode],
}

impl SplayNode {
    static fn new(key: Int32, value: Option[PayloadNode]): SplayNode {
        SplayNode(
            key,
            value,
            left = None[SplayNode],
            right = None[SplayNode]
        )
    }
}

class PayloadNode {
    array: Option[Array[Int32]],
    text: String,
    left: Option[PayloadNode],
    right: Option[PayloadNode],
}
