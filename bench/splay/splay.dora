import std::fatalError;
import std::timestamp;

fun main() {
    if std::argc() != 3I {
        println("usage: splay <seed> <tree-size> <iterations>");
        std::exit(1I);
    }

    rnd = std::Random(std::argv(0I).toInt64().getOrPanic());
    splayTreeSize = std::argv(1I).toInt32().getOrPanic();
    let iterations = std::argv(2I).toInt32().getOrPanic();

    let bench = Benchmark();
    bench.setup();

    var i = 0I;
    while i < iterations {
        bench.run();
        i = i + 1I;
    }

    bench.tearDown();
}

var rnd: std::Random = std::Random(0L);

var splayTreeSize: Int32 = 0I;
const splayTreeModifications: Int32 = 1000I;
const splayTreePayloadDepth: Int32 = 5I;

class Benchmark {
    var splayTree: SplayTree = SplayTree();
    var splaySamples: Int32 = 0I;
    var splaySampleTimeStart: Int64 = 0L;
    var splaySumOfSquaredPauses: Int64 = 0L;

    fun setup() {
        var i = 0I;
        self.splaySampleTimeStart = timestamp();

        while i < splayTreeSize {
            self.insertNewNode();

            if (i+1I) % 20I == 19I {
                // self.updateStats(timestamp());
            }

            i = i + 1I;
        }
    }

    fun updateStats(time: Int64) {
        let pause = time - self.splaySampleTimeStart;
        self.splaySampleTimeStart = time;
        self.splaySamples = self.splaySamples + 1I;
        self.splaySumOfSquaredPauses = self.splaySumOfSquaredPauses + pause * pause;
    }

    fun tearDown() {
        let keys = self.splayTree.exportKeys();

        self.splaySamples = 0I;
        self.splaySumOfSquaredPauses = 0L;

        var length = keys.size();

        if length != splayTreeSize.toInt64() {
            fatalError("Splay tree has wrong sie");
        }

        var i = 0L;

        while i < length - 1L {
            if keys(i) >= keys(i+1L) {
                fatalError("Splay tree not sorted");
            }

            i = i + 1L;
        }
    }

    fun run() {
        var i = 0I;

        while i < splayTreeModifications {
            let key = self.insertNewNode();
            let greatest = self.splayTree.findGreatestLessThan(key);

            if greatest.isNone() {
                self.splayTree.remove(key);
            } else {
                self.splayTree.remove(greatest.getOrPanic().key);
            }

            i = i + 1I;
        }

        // self.updateStats(timestamp());
    }

    fun insertNewNode(): Int32 {
        var key: Int32 = 0I;

        while true {
            key = self.generateKey();

            if self.splayTree.find(key).isNone() {
                break;
            }
        }

        let payload = generatePayloadTree(splayTreePayloadDepth, key.toString());
        self.splayTree.insert(key, payload);

        return key;
    }

    fun generateKey(): Int32 {
        return rnd.nextInt32();
    }

    fun rms() {
        let pauses = self.splaySumOfSquaredPauses;
        let samples = self.splaySamples;

        (pauses.toFloat64() / samples.toFloat64());
    }
}

fun generatePayloadTree(depth: Int32, tag: String): PayloadNode {
    if depth == 0I {
        let arr = Array[Int32]::zero(10L);
        var i = 0I;

        while i < 10I {
            arr(i.toInt64()) = i;
            i = i + 1I;
        }

        let text = "String for key " + tag + " in leaf node";
        return PayloadNode(Option[Array[Int32]]::Some(arr), text, Option[PayloadNode]::None, Option[PayloadNode]::None);
    } else {
        return PayloadNode(
            Option[Array[Int32]]::None,
            "",
            Option[PayloadNode]::Some(generatePayloadTree(depth-1I, tag)),
            Option[PayloadNode]::Some(generatePayloadTree(depth-1I, tag)),
        );
    }
}

class SplayTree {
    var root: Option[SplayNode] = Option[SplayNode]::None;

    fun isEmpty(): Bool {
        return self.root.isNone();
    }

    fun insert(key: Int32, value: PayloadNode) {
        if self.isEmpty() {
            self.root = Option[SplayNode]::Some(SplayNode(key, Option[PayloadNode]::Some(value)));
            return;
        }

        self.splay(key);

        let root = self.root.getOrPanic();

        if root.key == key {
            return;
        }

        let node = SplayNode(key, Option[PayloadNode]::Some(value));

        if key > root.key {
            node.left = self.root;
            node.right = root.right;
            root.right = Option[SplayNode]::None;

        } else {
            node.right = self.root;
            node.left = root.left;
            root.left = Option[SplayNode]::None;
        }

        self.root = Option[SplayNode]::Some(node);
    }

    fun remove(key: Int32): Option[SplayNode] {
        if self.isEmpty() {
            return Option[SplayNode]::None;
        }

        self.splay(key);
        let root = self.root.getOrPanic();

        if root.key != key {
            return Option[SplayNode]::None;
        }

        let removed = self.root;

        if root.left.isNone() {
            self.root = root.right;
        } else {
            let right = root.right;
            self.root = root.left;
            self.splay(key);
            self.root.getOrPanic().right = right;
        }

        return removed;
    }

    fun find(key: Int32): Option[SplayNode] {
        if self.isEmpty() {
            return Option[SplayNode]::None;
        }

        self.splay(key);
        let root = self.root.getOrPanic();

        if root.key == key {
            return self.root;
        } else {
            return Option[SplayNode]::None;
        }
    }

    fun findMax(startNode: Option[SplayNode]): Option[SplayNode] {
        if self.isEmpty() {
            return Option[SplayNode]::None;
        }

        var current = if startNode.isNone() {
            self.root.getOrPanic()
        } else {
            startNode.getOrPanic()
        };

        while current.right.isSome() {
            current = current.right.getOrPanic();
        }

        Option[SplayNode]::Some(current)
    }

    fun findGreatestLessThan(key: Int32): Option[SplayNode] {
        if self.isEmpty() {
            return Option[SplayNode]::None;
        }

        self.splay(key);
        let root = self.root.getOrPanic();

        if root.key < key {
            self.root
        } else if root.left.isSome() {
            self.findMax(root.left)
        } else {
            Option[SplayNode]::None
        }
    }

    fun exportKeys(): Vec[Int32] {
        let keys = Vec[Int32]();
        exportKeysTraverse(keys, self.root);
        return keys;
    }

    fun splay(key: Int32) {
        if self.isEmpty() {
            return;
        }

        var dummy = SplayNode(0I, Option[PayloadNode]::None);
        var left = dummy;
        var right = dummy;

        var current = self.root.getOrPanic();

        while true {
            if key < current.key {
                if current.left.isNone() {
                    break;
                }

                if key < current.left.getOrPanic().key {
                    // Rotate right
                    let tmp = current.left.getOrPanic();
                    current.left = tmp.right;
                    tmp.right = Option[SplayNode]::Some(current);
                    current = tmp;

                    if current.left.isNone() {
                        break;
                    }
                }

                // Link right
                right.left = Option[SplayNode]::Some(current);
                right = current;
                current = current.left.getOrPanic();
            } else if key > current.key {
                if current.right.isNone() {
                    break;
                }

                if key > current.right.getOrPanic().key {
                    // Rotate left
                    let tmp = current.right.getOrPanic();
                    current.right = tmp.left;
                    tmp.left = Option[SplayNode]::Some(current);
                    current = tmp;

                    if current.right.isNone() {
                        break;
                    }
                }

                // Link left
                left.right = Option[SplayNode]::Some(current);
                left = current;
                current = current.right.getOrPanic();
            } else {
                break;
            }
        }

        // Assemble
        left.right = current.left;
        right.left = current.right;
        current.left = dummy.right;
        current.right = dummy.left;
        self.root = Option[SplayNode]::Some(current);
    }
}

fun exportKeysTraverse(list: Vec[Int32], node: Option[SplayNode]) {
    var current = node;

    while current.isSome() {
        let c = current.getOrPanic();
        var left = c.left;
        if left.isSome() {
            exportKeysTraverse(list, left);
        }
        list.push(c.key);
        current = c.right;
    }
}

class SplayNode(var key: Int32, var value: Option[PayloadNode]) {
    var left: Option[SplayNode] = Option[SplayNode]::None;
    var right: Option[SplayNode] = Option[SplayNode]::None;
}

class PayloadNode(let array: Option[Array[Int32]], let text: String, let left: Option[PayloadNode], let right: Option[PayloadNode])
