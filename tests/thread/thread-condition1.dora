//= vm-args "--gc=copy"

const THREADS: Int32 = 10I;
const ITERATIONS: Int32 = 10_000I;
const QUEUE_SIZE: Int64 = 20L;

class SharedQueue[T](let capacity: Int64) {
    let mtx: std::Mutex = std::Mutex();
    let conditionNonEmpty: std::Condition = std::Condition();
    let conditionNonFull: std::Condition = std::Condition();
    let queue: std::Queue[T] = std::Queue[T]();

    fun enqueue(value: T) {
        self.mtx.lock();

        while self.queue.size() == self.capacity {
            self.conditionNonFull.wait(self.mtx);
        }

        self.queue.enqueue(value);
        self.mtx.unlock();
        self.conditionNonEmpty.notifyAll();
    }

    fun dequeue(): T {
        self.mtx.lock();

        while self.queue.size() == 0L {
            self.conditionNonEmpty.wait(self.mtx);
        }

        let value = self.queue.dequeue();
        self.mtx.unlock();
        self.conditionNonFull.notifyAll();
        value
    }
}

class Producer(let queue: SharedQueue[Int32]) extends std::Thread {
    var sum: Int32 = 0I;

    @override fun run() {
        var i = 0I;

        while i < ITERATIONS {
            self.queue.enqueue(i);
            self.sum = self.sum + i;
            i = i + 1I;
        }
    }
}

class Consumer(let queue: SharedQueue[Int32]) extends std::Thread {
    var sum: Int32 = 0I;

    @override fun run() {
        while true {
            let value = self.queue.dequeue();

            if value == -1I {
                break;
            }

            self.sum = self.sum + value;
        }
    }
}

fun main() {
    var i = 0I;
    let queue = SharedQueue[Int32](QUEUE_SIZE);
    let producers = Vec[Producer]();

    while i < THREADS {
        let producer = Producer(queue);
        producer.start();
        producers.push(producer);
        i = i + 1I;
    }

    let consumer = Consumer(queue);
    consumer.start();

    var producerSum = 0I;

    for producer in producers {
        producer.join();
        producerSum = producerSum + producer.sum;
    }

    queue.enqueue(-1I);
    consumer.join();

    assert(consumer.sum == producerSum);
}
