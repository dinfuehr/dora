//= vm-args "--gc-young-size=64M --max-heap-size=128M --gc-verify"

fn main() {
    let mut i = 0i32;

    while i < 4i32 {
        std::thread::spawn(|| {
            allocator();
            println("done");
        });
        i = i + 1i32;
    }

    allocator();
    println("done");
}

fn allocator() {
    let mut i = 0i32;

    while i < 10_000i32 {
        let foo = {
            let v1 = Foo(value=i, next=None[Foo]);
            let v2 = Foo(value=i + 1i32, next=Some[Foo](v1));
            let v3 = Foo(value=i + 2i32, next=Some[Foo](v2));
            let v4 = Foo(value=i + 3i32, next=Some[Foo](v3));
            Foo(value=i + 4i32, next=Some[Foo](v4))
        };

        if i % 500i32 == 0i32 { std::forceMinorCollect(); }

        let mut tmp = foo;
        assert(tmp.value == i + 4i32);
        tmp = tmp.next.getOrPanic();
        assert(tmp.value == i + 3i32);
        tmp = tmp.next.getOrPanic();
        assert(tmp.value == i + 2i32);
        tmp = tmp.next.getOrPanic();
        assert(tmp.value == i + 1i32);
        tmp = tmp.next.getOrPanic();
        assert(tmp.value == i);

        i = i + 1i32;
    }
}

class Foo {
    value: Int32,
    next: Option[Foo],
}
