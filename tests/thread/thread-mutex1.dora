//= vm-args "--gc=copy"

const THREADS: Int32 = 20I;
const ITERATIONS: Int32 = 10_000I;

class MyThread(let mtx: std::Mutex, let value: SharedInt) extends std::Thread {
    @override fun run() {
        var i = 0I;
        var last: Object = Object();

        while i < ITERATIONS {
            Object();

            self.mtx.lock();
            let value = self.value.increment();

            last = Object();

            if value % 100I == 0I {
                std::forceCollect();
            }

            self.mtx.unlock();
            i = i + 1I;
        }
    }
}

class SharedInt(var value: Int32) {
    fun increment(): Int32 {
        let old = self.value;
        self.value = self.value + 1I;
        old
    }
}

fun main() {
    var i = 0I;
    var threads = Vec[MyThread]();
    var mtx = std::Mutex();
    var shared = SharedInt(0I);

    while i < THREADS {
        let thread = MyThread(mtx, shared);
        thread.start();
        threads.push(thread);
        i = i + 1I;
    }

    for thread in threads {
        thread.join();
    }

    assert(shared.value == THREADS * ITERATIONS);
}
