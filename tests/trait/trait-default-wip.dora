//= ignore

trait MyIterator {
    type Item;
    fn next(): Option[Self::Item];

    fn count(): Int64 {
        let mut result = 0;

        while self.next() is Some(_) {
            result += 1;
        }

        result
    }

    fn enumerate(): Enumerate[Self] {
        Enumerate[Self](it = self, idx = 0)
    }

    fn map[R](f: (Self::Item): R): Map[Self, R] {
        Map[Self, R](it = self, fct = f)
    }
}

class Range {
    value: Int
}

impl MyIterator for Range {
    type Item = Int;

    fn next(): Option[Int] {
        if self.value <= 0 {
            None[Int]
        } else {
            let result = self.value;
            self.value -= 1;
            Some[Int](result)
        }
    }
}

class Map[I: MyIterator, R] {
    it: I,
    fct: (I::Item): R,
}

impl[I: MyIterator, R] MyIterator for Map[I, R] {
    type Item = R;

    fn next(): Option[R] {
        if self.it.next() is Some(value) {
            Some[R](self.fct(value))
        } else {
            None[R]
        }
    }
}

class Enumerate[I: MyIterator] {
    it: I,
    idx: Int,
}

impl[I: MyIterator] MyIterator for Enumerate[I] {
    type Item = (Int, I::Item);

    fn next(): Option[(Int, I::Item)] {
        if self.it.next() is Some(value) {
            let idx = self.idx;
            self.idx += 1;
            Some[(Int, I::Item)]((idx, value))
        } else {
            None[(Int, I::Item)]
        }
    }
}

fn main() {
    assert(Range(2).count() == 2);
    assert(Range(7).count() == 7);

    let r = Range(2).enumerate().map[(Int, Int)](|x: (Int, Int)|: (Int, Int) {
        (x.0, x.1 * 2)
    });

    assert(Some[(Int, Int)]((0, 4)) == r.next());
    assert(Some[(Int, Int)]((1, 2)) == r.next());
    assert(r.next().isNone());
}
