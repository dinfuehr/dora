//= stdout file

fun main() {
    let primes = sieve(100L);
    var i = 0L;

    while i < primes.length() {
        println(primes(i).toString());
        i = i + 1L;
    }
}

fun sieve(limit: Int64) -> Vec[Int64] {
    let primes = Vec[Int64]();

    if limit >= 2L {
        let numbers = Array::fill[Bool](limit+1L, true);
        let sqrtLimit = limit.toDouble().sqrt().toInt64();

        var factor = 2L;

        while factor <= sqrtLimit {
            if numbers(factor) {
                var multiple = factor * factor;

                while multiple <= limit {
                    numbers(multiple) = false;
                    multiple = multiple + factor;
                }
            }

            factor = factor + 1L;
        }

        var i = 0L;

        while i <= limit {
            if i >= 2L && numbers(i) {
                primes.push(i);
            }

            i = i + 1L;
        }
    }

    return primes;
}