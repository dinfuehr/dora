fun main() {
    let m = SlowMap::<int, String>();
    assert(m.insert(1, "hello"));
    assert(m.insert(2, "world"));

    assert(m.get(1) == "hello");
    assert(m.get(2) == "world");
    assert(m.get(3) === nil);

    assert(!m.insert(2, "test"));
    assert(m.get(2) == "test");
}

class SlowMap<K: Equals, V> {
    var entries: Vec<SlowMapEntry<K, V> > = Vec::<SlowMapEntry<K, V> >();

    fun insert(key: K, val: V) -> bool {
        var i = 0;
        var len = self.entries.len();

        while i < len {
            let entry = self.entries[i];

            if entry.key.equals(key) {
                entry.value = val;
                return false;
            }

            i = i + 1;
        }

        self.entries.push(SlowMapEntry::<K, V>(key, val));
        return true;
    }

    fun get(key: K) -> V {
        var i = 0;
        var len = self.entries.len();

        while i < len {
            let entry = self.entries[i];

            if entry.key.equals(key) {
                return entry.value;
            }

            i = i + 1;
        }

        return defaultValue::<V>();
    }
}

class SlowMapEntry<K, V>(let key: K, var value: V)