use package::codegen::{CodeGen, STACK_FRAME_ALIGNMENT};
use package::compilation::{CompilationInfo, FunctionCompilationInfo};
use package::graph::{Block, Graph, Inst, InstSet, Location, Op, Operand, Policy, Type};
use package::location::ensureLocationData;

pub fn performStackSlotAllocation(graph: Graph, codegen: CodeGen, ci: CompilationInfo, fct: FunctionCompilationInfo, maxOutgoingArgsSize: Int32) {
    let allocator = StackSlotAllocator(graph, codegen, ci, stackSize = 0i32);
    allocator.run();

    let stack_alignment = STACK_FRAME_ALIGNMENT.toInt32();

    let finalStackSize = allocator.stackSize + maxOutgoingArgsSize;
    let finalAlignedStackSize = (finalStackSize + (stack_alignment - 1i32)) & !(stack_alignment - 1i32);

    graph.setStackSize(finalAlignedStackSize);
}

class StackSlotAllocator {
    graph: Graph,
    codegen: CodeGen,
    ci: CompilationInfo,
    stackSize: Int32,
}

impl StackSlotAllocator {
    fn run() {
        computeStackSlotLiveness(self.graph);

        for block in self.graph.linearOrderIterator() {
            for inst in block.phisIterator() {
                self.ensureStackSlot(inst);
            }

            for inst in block.instructionsIterator() {
                self.ensureStackSlot(inst);
            }
        }
    }

    fn ensureStackSlot(inst: Inst) {
        let loc = inst.getLocationData();

        if inst.op() == Op::AllocateStack {
            self.allocateStackSlot(inst);
        } else if loc.needsSpillSlot() {
            if loc.hasSpillSlot() {
                assert(inst.op() == Op::Arg);
            } else {
                let slot = self.allocateSpillSlot(inst.getValueType());
                loc.setSpillSlot(slot);
            }
        } else if inst.op() == Op::LoadReg {
            let value = inst.getInput(0).getValue();
            let slot = value.getLocationData().getSpillSlot();
            let location = Location::fp(slot);
            inst.getLocationData().initInput(0, Operand::new(Policy::None, location));

        } else if inst.op() == Op::StoreReg {
            unreachable[()]();
        }
    }

    fn allocateStackSlot(inst: Inst) {
        let layout = inst.getRecordLayout();

        let offset = self.allocateStackSpace(layout.size, layout.alignment);
        inst.getLocationData().setStackSlot(offset);
    }

    fn allocateSpillSlot(ty: Type): Int32 {
        let (size, alignment) = match ty {
            Type::Bool | Type::UInt8 => (1i32, 1i32),
            Type::Int32 | Type::Float32 => (4i32, 4i32),
            Type::Address | Type::Ptr | Type::Int64 | Type::Float64 => (8i32, 8i32),
            _ => unreachable[(Int32, Int32)](),
        };

        self.allocateStackSpace(size, alignment)
    }

    fn allocateStackSpace(size: Int32, alignment: Int32): Int32 {
        self.stackSize = (self.stackSize + (alignment - 1i32)) & !(alignment - 1i32);
        self.stackSize = self.stackSize + size;

        -self.stackSize
    }
}

fn computeStackSlotLiveness(graph: Graph) {
    for block in graph.insertionOrderIterator() {
        block.setLiveIn(InstSet::new(graph));
    }

    for block in graph.postOrderIterator() {
        let live = computeLiveIn(block);
        block.getLiveIn().unionWith(live);
    }

    for block in graph.reversePostOrderIterator() {
        if !block.isLoopHeader() {
            continue;
        }

        let loopData = block.getLoopData().getOrPanic();
        let liveLoop = block.getLiveIn();

        // A value which is live-in in the loop header, is also live-in
        // in all blocks of that loop.
        for loopBlockId in loopData.blocks() {
            let loopBlock = graph.getBlockWithId(loopBlockId.toInt32());
            loopBlock.getLiveIn().unionWith(liveLoop);
        }
    }

    for block in graph.postOrderIterator() {
        let liveIn = computeLiveIn(block);
        assert(block.getLiveIn() == liveIn);
    }
}

fn computeLiveIn(block: Block): InstSet {
    let live = InstSet::new(block.getGraph());

    for successorEdge in block.successors {
        let successor = successorEdge.target;
        let idx = successorEdge.targetIdx;

        for phi in successor.phisIterator() {
            let input = phi.getInput(idx);
            let value = input.getValue();

            if value.getLocationData().needsSpillSlot() || value.op() == Op::AllocateStack {
                live.insert(value);
            }
        }

        live.unionWith(successor.getLiveIn());
    }

    for inst in block.reverseInstructionsIterator() {
        for input in inst.getInputs() {
            let value = input.getValue();

            if value.getLocationData().needsSpillSlot() || value.op() == Op::AllocateStack {
                live.insert(value);
            }
        }

        live.remove(inst);
    }

    for phi in block.phisIterator() {
        live.remove(phi);
    }

    live
}
