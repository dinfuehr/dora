use package::graph::{Block, Graph, Inst, Op};

pub fn verifyGraph(graph: Graph) {
    GraphVerifier::new(graph).run()
}

class GraphVerifier {
    graph: Graph,
    currentBlock: Block,
}

impl GraphVerifier {
    static fn new(graph: Graph): GraphVerifier {
        GraphVerifier(
            graph,
            currentBlock = graph.getEntryBlock(),
        )
    }

    fn run() {
        for block in self.graph.insertionOrderIterator() {
            self.verifyBlock(block);
        }
    }

    fn verifyBlock(block: Block) {
        self.currentBlock = block;

        // Block needs to belong to current graph.
        assert(block.getGraph() === self.graph);

        for phi in block.phisIterator() {
            assert(phi.isPhi());
            assert(phi.getInputs().size() == block.predecessors.size());

            self.verifyInst(phi);
        }

        for inst in block.instructionsIterator() {
            self.verifyInst(inst);

            if inst !== block.lastInst() {
                assert(!inst.isTerminator());
            }
        }

        assert(block.lastInst().isTerminator());
    }

    fn verifyInst(inst: Inst) {
        let mut idx = 0i32;

        for input in inst.getInputs() {
            assert(input.getIdx() == idx);
            idx += 1i32;
        }

        match inst.op() {
            Op::Goto => {
                let target = inst.getTargetBlock();
                assert(target.getGraph() === self.graph);
            }

            Op::If => {
                let trueBlock = inst.getTrueBlock();
                assert(self.currentBlock.successors(0).target === trueBlock);
                let falseBlock = inst.getFalseBlock();
                assert(self.currentBlock.successors(1).target === falseBlock);
            }

            _ => (),
        }

        assert(inst.getBlock() === self.currentBlock);
    }
}
