use package::bytecode::{BytecodeType, BytecodeTraitType};
use package::bytecode::opcode as opc;
use package::compilation::SpecializeSelf;
use package::interface::{Address, CodeDescriptor, CommentTable, GcPoint, GcPointTable, InlinedFunction, LazyCompilationData, LazyCompilationSite, LocationTable, InlinedLocation, RelocationKind, RelocationTable};

pub fn encodeCodeDescriptor(writer: ByteWriter, descriptor: CodeDescriptor) {
    encodeArrayUInt8(writer, descriptor.code);
    encodeLazyCompilationTable(writer, descriptor.lazyCompilation);
    encodeGcPointTable(writer, descriptor.gcPoints);
    encodeLocationTable(writer, descriptor.locations);
    encodeCommentTable(writer, descriptor.comments);
    encodeRelocationTable(writer, descriptor.relocations);
    encodeInlinedFunctionTable(writer, descriptor.inlinedFunctions);
}

fn encodeArrayUInt8(writer: ByteWriter, array: Array[UInt8]) {
    writer.emitInt32(array.size().toInt32());

    for value in array {
        writer.emitByte(value);
    }
}

fn encodeCommentTable(writer: ByteWriter, table: CommentTable) {
    writer.emitInt32(table.size().toInt32());

    for (offset, comment) in table {
        writer.emitInt32(offset);
        encodeString(writer, comment);
    }
}

fn encodeRelocationTable(writer: ByteWriter, table: RelocationTable) {
    writer.emitInt32(table.size().toInt32());

    for (offset, kind) in table {
        writer.emitInt32(offset);
        encodeRelocationKind(writer, kind);
    }
}

fn encodeRelocationKind(writer: ByteWriter, kind: RelocationKind) {
    match kind {
        RelocationKind::JumpTableEntry(target) => {
            writer.emitByte(opc::RELOCATION_KIND_JUMP_TABLE_ENTRY.toUInt8());
        },

        RelocationKind::CodeTarget | RelocationKind::Object => unreachable[()](),
    }
}

fn encodeLazyCompilationTable(writer: ByteWriter, table: LazyCompilationData) {
    writer.emitInt32(table.size().toInt32());

    for (offset, site) in table {
        writer.emitInt32(offset);
        encodeLazyCompilationSite(writer, site);
    }
}

fn encodeLazyCompilationSite(writer: ByteWriter, site: LazyCompilationSite) {
    match site {
        LazyCompilationSite::Direct(site) => {
            writer.emitByte(opc::LAZY_COMPILATION_SITE_DIRECT.toUInt8());
            writer.emitInt32(site.fct_id.0);
            encodeBytecodeTypeArray(writer, site.type_params);
            writer.emitInt32(site.const_pool_offset_from_ra);
        },

        LazyCompilationSite::Virtual(site) => {
            writer.emitByte(opc::LAZY_COMPILATION_SITE_VIRTUAL.toUInt8());
            writer.emitBool(site.receiver_is_first);
            encodeBytecodeType(writer, site.trait_object_ty);
            writer.emitInt32(site.vtable_index);
        },

        LazyCompilationSite::Lambda(site) => {
            writer.emitByte(opc::LAZY_COMPILATION_SITE_LAMBDA.toUInt8());
            writer.emitBool(site.receiver_is_first);
            encodeBytecodeTypeArray(writer, site.params);
            encodeBytecodeType(writer, site.return_ty);
        }
    }
}

fn encodeGcPointTable(writer: ByteWriter, table: GcPointTable) {
    writer.emitInt32(table.size().toInt32());

    for (offset, gcpoint) in table {
        writer.emitInt32(offset);
        encodeGcPoint(writer, gcpoint);
    }
}

fn encodeGcPoint(writer: ByteWriter, table: GcPoint) {
    writer.emitInt32(table.size().toInt32());

    for offset in table {
        writer.emitInt32(offset);
    }
}

fn encodeString(writer: ByteWriter, value: String) {
    writer.emitInt32(value.size().toInt32());

    for byte in value.asBytes() {
        writer.emitByte(byte);
    }
}

fn encodeLocationTable(writer: ByteWriter, locations: LocationTable) {
    writer.emitInt32(locations.size().toInt32());

    for (offset, inlined_location) in locations {
        writer.emitInt32(offset);
        encodeInlinedLocation(writer, inlined_location);
    }
}

fn encodeInlinedLocation(writer: ByteWriter, inlined_location: InlinedLocation) {
    if inlined_location.inlined_function_id.isSome() {
        writer.emitBool(true);
        writer.emitInt32(inlined_location.inlined_function_id.getOrPanic().value);
    } else {
        writer.emitBool(false);
    }
    writer.emitInt32(inlined_location.location.line);
    writer.emitInt32(inlined_location.location.column);
}

fn encodeInlinedFunctionTable(writer: ByteWriter, table: Array[InlinedFunction]) {
    writer.emitInt32(table.size().toInt32());

    for inlined in table {
        encodeInlinedFunction(writer, inlined);
    }
}

fn encodeInlinedFunction(writer: ByteWriter, inlined: InlinedFunction) {
    writer.emitInt32(inlined.fct_id.0);
    encodeBytecodeTypeArray(writer, inlined.type_params);
    encodeInlinedLocation(writer, inlined.inlined_location);
}

pub fn encodeBytecodeTypeArray(writer: ByteWriter, types: Array[BytecodeType]) {
    writer.emitInt32(types.size().toInt32());

    for ty in types {
        encodeBytecodeType(writer, ty);
    }
}

pub fn encodeBytecodeType(writer: ByteWriter, ty: BytecodeType) {
    match ty {
        BytecodeType::Unit => {
            writer.emitByte(opc::BYTECODE_TYPE_UNIT.toUInt8());
        },
        BytecodeType::Bool => {
            writer.emitByte(opc::BYTECODE_TYPE_BOOL.toUInt8());
        },
        BytecodeType::Char => {
            writer.emitByte(opc::BYTECODE_TYPE_CHAR.toUInt8());
        },
        BytecodeType::UInt8 => {
            writer.emitByte(opc::BYTECODE_TYPE_U_INT8.toUInt8());
        },
        BytecodeType::Int32 => {
            writer.emitByte(opc::BYTECODE_TYPE_INT32.toUInt8());
        },
        BytecodeType::Int64 => {
            writer.emitByte(opc::BYTECODE_TYPE_INT64.toUInt8());
        },
        BytecodeType::Float32 => {
            writer.emitByte(opc::BYTECODE_TYPE_FLOAT32.toUInt8());
        },
        BytecodeType::Float64 => {
            writer.emitByte(opc::BYTECODE_TYPE_FLOAT64.toUInt8());
        },
        BytecodeType::Ptr => {
            writer.emitByte(opc::BYTECODE_TYPE_PTR.toUInt8());
        },
        BytecodeType::This => {
            writer.emitByte(opc::BYTECODE_TYPE_THIS.toUInt8());
        },
        BytecodeType::Tuple(subtypes) => {
            writer.emitByte(opc::BYTECODE_TYPE_TUPLE.toUInt8());
            encodeBytecodeTypeArray(writer, subtypes);
        },
        BytecodeType::TypeParam(type_param_id) => {
            writer.emitByte(opc::BYTECODE_TYPE_TYPE_PARAM.toUInt8());
            writer.emitInt32(type_param_id);
        },
        BytecodeType::Enum(enum_id, source_type_array) => {
            writer.emitByte(opc::BYTECODE_TYPE_ENUM.toUInt8());
            writer.emitInt32(enum_id.0);
            encodeBytecodeTypeArray(writer, source_type_array);
        },
        BytecodeType::Struct(struct_id, source_type_array) => {
            writer.emitByte(opc::BYTECODE_TYPE_STRUCT.toUInt8());
            writer.emitInt32(struct_id.0);
            encodeBytecodeTypeArray(writer, source_type_array);
        },
        BytecodeType::Class(class_id, source_type_array) => {
            writer.emitByte(opc::BYTECODE_TYPE_CLASS.toUInt8());
            writer.emitInt32(class_id.0);
            encodeBytecodeTypeArray(writer, source_type_array);
        },
        BytecodeType::TraitObject(trait_id, source_type_array, assoc_types) => {
            writer.emitByte(opc::BYTECODE_TYPE_TRAIT_OBJECT.toUInt8());
            writer.emitInt32(trait_id.0);
            encodeBytecodeTypeArray(writer, source_type_array);
            encodeBytecodeTypeArray(writer, assoc_types);
        },
        BytecodeType::Lambda(params, ret) => {
            writer.emitByte(opc::BYTECODE_TYPE_LAMBDA.toUInt8());
            encodeBytecodeTypeArray(writer, params);
            encodeBytecodeType(writer, ret);
        },
        BytecodeType::Assoc(ty, traitTy, assocId) => {
            writer.emitByte(opc::BYTECODE_TYPE_ASSOC.toUInt8());
            encodeBytecodeType(writer, ty);
            encodeBytecodeTraitType(writer, traitTy);
            writer.emitInt32(assocId.0);
        }
        BytecodeType::TypeAlias(..) => {
            unimplemented[()]();
        }
    }
}

pub fn encodeBytecodeTraitType(writer: ByteWriter, traitTy: BytecodeTraitType) {
    writer.emitInt32(traitTy.trait_id.0);
    encodeBytecodeTypeArray(writer, traitTy.typeParams);
    writer.emitInt32(traitTy.bindings.size().toInt32());

    for (aliasId, ty) in traitTy.bindings {
        writer.emitInt32(aliasId.0);
        encodeBytecodeType(writer, ty);
    }
}

pub fn encodeOptionalSpecializeSelf(writer: ByteWriter, specializeSelf: Option[SpecializeSelf]) {
    if specializeSelf is Some(specializeSelf) {
        writer.emitBool(true);
        writer.emitInt32(specializeSelf.impl_id.0);
        writer.emitInt32(specializeSelf.container_type_params.toInt32());
        encodeBytecodeTraitType(writer, specializeSelf.trait_ty);
        encodeBytecodeType(writer, specializeSelf.extended_ty);
    } else {
        writer.emitBool(false);
    }
}

pub class ByteWriter {
    buffer: Vec[UInt8],
}

impl ByteWriter {
    pub static fn new(): ByteWriter {
        ByteWriter(
            buffer = Vec[UInt8]::new(),
        )
    }

    pub fn emitAddress(address: Address) {
        self.emitInt64(address.toInt64());
    }

    pub fn emitInt64(value: Int64) {
        self.emitInt32(value.toInt32());
        self.emitInt32((value >> 32i32).toInt32());
    }

    pub fn emitInt32(value: Int32) {
        self.buffer.push(value.toUInt8());
        self.buffer.push((value >> 8i32).toUInt8());
        self.buffer.push((value >> 16i32).toUInt8());
        self.buffer.push((value >> 24i32).toUInt8());
    }

    pub fn emitByte(value: UInt8) {
        self.buffer.push(value);
    }

    pub fn emitBool(value: Bool) {
        let value = if value { 1u8 } else { 0u8 };
        self.emitByte(value);
    }

    pub fn toArray(): Array[UInt8] {
        self.buffer.toArray()
    }

    pub fn clear() {
        self.buffer.clear();
    }
}
