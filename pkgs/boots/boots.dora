use std::collections::HashMap;

use package::assembler::{RegMap, RegSet, Register};
use package::codegen::CodeGen;
use package::codegen::{createCodeGen, generateCode};
use package::compilation::{CompilationInfo, FunctionCompilationInfo};
use package::dead_code_elimination::performDeadCodeElimination;
use package::dominator::buildDominatorTree;
use package::graph::{createGotoInst, Block, Edge, Graph, Inst, LocationData, Op, opName};
use package::graph::dump::dumpGraph;
use package::graph::html::{createHtmlWriter, HtmlWriter};
use package::graph::verifier::verifyGraph;
use package::graph_builder::{analyzeBytecode, createGraph};
use package::inlining::performInlining;
use package::interface::{Architecture, CodeDescriptor, CommentTable, GcPointTable, GcPoint, LazyCompilationData, LazyCompilationSite, LazyCompilationSiteDirect, LazyCompilationSiteVirtual, LazyCompilationSiteLambda, LocationTable};
use package::interface::{compile, config, getFunctionAddressRaw, getFieldOffsetRaw, getClassSizeRaw, getClassPointerRaw, getFunctionVtableIndexRaw, getClassPointerForLambdaRaw, getGlobalValueAddressRaw, getGlobalStateAddressRaw, hasGlobalInitialValueRaw};
use package::load_elimination::performLoadElimination;
use package::prepare_regalloc::prepareRegisterAllocation;
use package::regalloc::performRegisterAllocation;
use package::sparse_set::SparseSet;
use package::specialize::specializeTy;
use package::cfg_simplification::performCfgSimplification;
use package::liveness::computeLiveness;

mod assembler;
mod bytecode;
mod cfg_simplification;
mod codegen;
mod compilation;
mod dead_code_elimination;
mod deserializer;
mod dominator;
mod graph;
mod graph_builder;
mod inlining;
mod interface;
mod liveness;
mod load_elimination;
mod location;
mod prepare_regalloc;
mod regalloc;
mod resolver;
mod serializer;
mod sparse_set;
mod specialize;
mod stackalloc;

pub fn compileFunction(ci: CompilationInfo, fct: FunctionCompilationInfo): CodeDescriptor {
    let htmlWriter: Option[HtmlWriter] = if ci.emitHtml {
        Some[HtmlWriter](createHtmlWriter(ci))
    } else {
        None[HtmlWriter]
    };

    // Analyze basic block starts/ends.
    let analysis = analyzeBytecode(fct.bc);

    if htmlWriter.isSome() {
        htmlWriter.getOrPanic().dumpBytecode(fct.bc, analysis);
    }

    let graph = createGraph(ci, fct, analysis);

    let codegen = createCodeGen(ci, graph);

    if htmlWriter.isSome() {
        htmlWriter.getOrPanic().dumpGraph("after graph building", graph);
    }

    if ci.emitGraphAfterEachPass {
        println("Function ${ci.getDisplayName()}: after graph building");
        println(dumpGraph(Some[CompilationInfo](ci), graph, config.architecture));
    }

    verify(graph);

    if ci.optimize {
        performInlining(ci, graph);

        if htmlWriter.isSome() {
            htmlWriter.getOrPanic().dumpGraph("after inlining", graph);
        }

        if ci.emitGraphAfterEachPass {
            println("Function ${ci.getDisplayName()}: after inlining");
            println(dumpGraph(Some[CompilationInfo](ci), graph, config.architecture));
        }

        verify(graph);

        performCfgSimplification(ci, graph);

        if htmlWriter.isSome() {
            htmlWriter.getOrPanic().dumpGraph("after cfg simplification", graph);
        }

        if ci.emitGraphAfterEachPass {
            println("Function ${ci.getDisplayName()}: after cfg simplification");
            println(dumpGraph(Some[CompilationInfo](ci), graph, config.architecture));
        }

        verify(graph);

        performLoadElimination(ci, graph);

        if htmlWriter.isSome() {
            htmlWriter.getOrPanic().dumpGraph("after load elimination", graph);
        }

        if ci.emitGraphAfterEachPass {
            println("Function ${ci.getDisplayName()}: after load elimination");
            println(dumpGraph(Some[CompilationInfo](ci), graph, config.architecture));
        }

        verify(graph);

        performDeadCodeElimination(ci, graph);

        if htmlWriter.isSome() {
            htmlWriter.getOrPanic().dumpGraph("after dead code elimination", graph);
        }

        if ci.emitGraphAfterEachPass {
            println("Function ${ci.getDisplayName()}: after dead code elimination");
            println(dumpGraph(Some[CompilationInfo](ci), graph, config.architecture));
        }

        if ci.emitFinalGraph {
            println("Function ${ci.getDisplayName()}: final");
            println(dumpGraph(Some[CompilationInfo](ci), graph, config.architecture));
        }

        verify(graph);
    }

    prepareRegisterAllocation(graph, codegen, ci);

    splitCriticalEdges(graph);
    buildDominatorTree(graph);
    computeLiveness(graph, ci);

    if htmlWriter.isSome() {
        htmlWriter.getOrPanic().dumpGraph("before register allocation", graph);
    }

    if ci.emitGraphAfterEachPass {
        println("Function: ${ci.getDisplayName()}: before register allocation");
        println(dumpGraph(Some[CompilationInfo](ci), graph, config.architecture));
    }

    performRegisterAllocation(graph, codegen, ci, fct);

    if htmlWriter.isSome() {
        htmlWriter.getOrPanic().dumpGraph("after register allocation", graph);
        htmlWriter.getOrPanic().close();
    }

    if ci.emitGraphAfterEachPass {
        println("${ci.getDisplayName()} after register allocation:");
        println(dumpGraph(Some[CompilationInfo](ci), graph, config.architecture));
    }

    generateCode(graph, codegen, ci)
}

fn verify(graph: Graph) {
    if config.isDebug {
        verifyGraph(graph);
    }
}

fn splitCriticalEdges(graph: Graph) {
    for block in graph.insertionOrderIterator() {
        if block.predecessors.size() <= 1 {
            continue;
        }

        for predecessorEdge in block.predecessors {
            let predecessor = predecessorEdge.source;

            if predecessor.successors.size() == 1 {
                continue;
            }

            // Found a critical edge.
            splitCriticalEdge(graph, predecessorEdge);
        }
    }
}

fn splitCriticalEdge(graph: Graph, edge: Edge) {
    let predecessor = edge.source;
    let successor = edge.target;

    let intermediate = Block::new();
    graph.addBlock(intermediate);

    let inst = createGotoInst(successor);
    intermediate.appendInst(inst);

    let terminator = predecessor.lastInst();
    assert(terminator.op() == Op::If);
    if terminator.getTrueBlock() === successor {
        terminator.setTrueBlock(intermediate);
    } else {
        assert(terminator.getFalseBlock() === successor);
        terminator.setFalseBlock(intermediate);
    }

    let predecessorToIntermediateEdge = Edge::new(edge.source, edge.sourceIdx, intermediate, 0);
    assert(intermediate.predecessors.isEmpty());
    intermediate.predecessors.push(predecessorToIntermediateEdge);

    predecessor.successors(edge.sourceIdx) = predecessorToIntermediateEdge;

    let intermediateToSuccessorEdge = edge;
    intermediateToSuccessorEdge.source = intermediate;
    intermediateToSuccessorEdge.sourceIdx = 0;
    assert(intermediate.successors.isEmpty());
    intermediate.successors.push(intermediateToSuccessorEdge);
}
