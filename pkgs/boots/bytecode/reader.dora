use std::traits::Iterator;

use package::bytecode::{BytecodeInstruction, BytecodeRegister, GlobalId, ConstPoolId, ConstId};
use package::bytecode::opcode as opc;

pub fn decodeBytecode(code: Array[UInt8]): Array[BytecodeInstructionInfo] {
    let result = Vec[BytecodeInstructionInfo]::new();

    for instInfo in BytecodeIterator::new(code) {
        result.push(instInfo);
    }

    result.toArray()
}

pub class BytecodeIterator {
    data: Array[UInt8],
    pos: Int64,
    length: Int64,
}

impl BytecodeIterator {
    pub static fn new(data: Array[UInt8]): BytecodeIterator {
        BytecodeIterator(data, pos = 0, length = data.size())
    }

    pub static fn newAtPos(data: Array[UInt8], pos: Int64): BytecodeIterator {
        BytecodeIterator(data, pos, length = data.size())
    }

    fn readInstruction(): BytecodeInstructionInfo {
        let start = self.pos;
        let opcode = self.readOpcode();

        let inst = if opcode == opc::BYTECODE_OPCODE_ADD {
            let dest = self.readRegister();
            let lhs = self.readRegister();
            let rhs = self.readRegister();
            BytecodeInstruction::Add(dest, lhs, rhs)

        } else if opcode == opc::BYTECODE_OPCODE_SUB {
            let dest = self.readRegister();
            let lhs = self.readRegister();
            let rhs = self.readRegister();
            BytecodeInstruction::Sub(dest, lhs, rhs)

        } else if opcode == opc::BYTECODE_OPCODE_NEG {
            let dest = self.readRegister();
            let src = self.readRegister();
            BytecodeInstruction::Neg(dest, src)

        } else if opcode == opc::BYTECODE_OPCODE_MUL {
            let dest = self.readRegister();
            let lhs = self.readRegister();
            let rhs = self.readRegister();
            BytecodeInstruction::Mul(dest, lhs, rhs)

        } else if opcode == opc::BYTECODE_OPCODE_DIV {
            let dest = self.readRegister();
            let lhs = self.readRegister();
            let rhs = self.readRegister();
            BytecodeInstruction::Div(dest, lhs, rhs)

        } else if opcode == opc::BYTECODE_OPCODE_MOD {
            let dest = self.readRegister();
            let lhs = self.readRegister();
            let rhs = self.readRegister();
            BytecodeInstruction::Mod(dest, lhs, rhs)

        } else if opcode == opc::BYTECODE_OPCODE_AND {
            let dest = self.readRegister();
            let lhs = self.readRegister();
            let rhs = self.readRegister();
            BytecodeInstruction::And(dest, lhs, rhs)

        } else if opcode == opc::BYTECODE_OPCODE_OR {
            let dest = self.readRegister();
            let lhs = self.readRegister();
            let rhs = self.readRegister();
            BytecodeInstruction::Or(dest, lhs, rhs)

        } else if opcode == opc::BYTECODE_OPCODE_XOR {
            let dest = self.readRegister();
            let lhs = self.readRegister();
            let rhs = self.readRegister();
            BytecodeInstruction::Xor(dest, lhs, rhs)

        } else if opcode == opc::BYTECODE_OPCODE_NOT {
            let dest = self.readRegister();
            let src = self.readRegister();
            BytecodeInstruction::Not(dest, src)

        } else if opcode == opc::BYTECODE_OPCODE_SHL {
            let dest = self.readRegister();
            let lhs = self.readRegister();
            let rhs = self.readRegister();
            BytecodeInstruction::Shl(dest, lhs, rhs)

        } else if opcode == opc::BYTECODE_OPCODE_SHR {
            let dest = self.readRegister();
            let lhs = self.readRegister();
            let rhs = self.readRegister();
            BytecodeInstruction::Shr(dest, lhs, rhs)

        } else if opcode == opc::BYTECODE_OPCODE_SAR {
            let dest = self.readRegister();
            let lhs = self.readRegister();
            let rhs = self.readRegister();
            BytecodeInstruction::Sar(dest, lhs, rhs)

        } else if opcode == opc::BYTECODE_OPCODE_MOV {
            let dest = self.readRegister();
            let src = self.readRegister();
            BytecodeInstruction::Mov(dest, src)

        } else if opcode == opc::BYTECODE_OPCODE_LOAD_FIELD {
            let dest = self.readRegister();
            let obj = self.readRegister();
            let idx = self.readConstPoolId();
            BytecodeInstruction::LoadField(dest, obj, idx)
        } else if opcode == opc::BYTECODE_OPCODE_STORE_FIELD {
            let src = self.readRegister();
            let obj = self.readRegister();
            let idx = self.readConstPoolId();
            BytecodeInstruction::StoreField(src, obj, idx)
        } else if opcode == opc::BYTECODE_OPCODE_LOAD_ENUM_VARIANT {
            let dest = self.readRegister();
            let src = self.readRegister();
            let idx = self.readConstPoolId();
            BytecodeInstruction::LoadEnumVariant(dest, src, idx)
        } else if opcode == opc::BYTECODE_OPCODE_LOAD_ENUM_ELEMENT {
            let dest = self.readRegister();
            let src = self.readRegister();
            let idx = self.readConstPoolId();
            BytecodeInstruction::LoadEnumElement(dest, src, idx)

        } else if opcode == opc::BYTECODE_OPCODE_LOAD_GLOBAL {
            let dest = self.readRegister();
            let glob = self.readGlobalId();
            BytecodeInstruction::LoadGlobal(dest, glob)
        } else if opcode == opc::BYTECODE_OPCODE_STORE_GLOBAL {
            let src = self.readRegister();
            let glob = self.readGlobalId();
            BytecodeInstruction::StoreGlobal(src, glob)

        } else if opcode == opc::BYTECODE_OPCODE_LOAD_CONST {
            let dest = self.readRegister();
            let glob = self.readConstId();
            BytecodeInstruction::LoadConst(dest, glob)

        } else if opcode == opc::BYTECODE_OPCODE_PUSH_REGISTER {
            let src = self.readRegister();
            BytecodeInstruction::PushRegister(src)

        } else if opcode == opc::BYTECODE_OPCODE_CONST_TRUE {
            let dest = self.readRegister();
            BytecodeInstruction::ConstTrue(dest)
        } else if opcode == opc::BYTECODE_OPCODE_CONST_FALSE {
            let dest = self.readRegister();
            BytecodeInstruction::ConstFalse(dest)
        } else if opcode == opc::BYTECODE_OPCODE_CONST_UINT8 {
            let dest = self.readRegister();
            let value = self.readByte().toUInt8();
            BytecodeInstruction::ConstUInt8(dest, value)
        } else if opcode == opc::BYTECODE_OPCODE_CONST_CHAR {
            let dest = self.readRegister();
            let idx = self.readConstPoolId();
            BytecodeInstruction::ConstChar(dest, idx)
        } else if opcode == opc::BYTECODE_OPCODE_CONST_INT32 {
            let dest = self.readRegister();
            let idx = self.readConstPoolId();
            BytecodeInstruction::ConstInt32(dest, idx)
        } else if opcode == opc::BYTECODE_OPCODE_CONST_INT64 {
            let dest = self.readRegister();
            let idx = self.readConstPoolId();
            BytecodeInstruction::ConstInt64(dest, idx)
        } else if opcode == opc::BYTECODE_OPCODE_CONST_FLOAT32{
            let dest = self.readRegister();
            let idx = self.readConstPoolId();
            BytecodeInstruction::ConstFloat32(dest, idx)
        } else if opcode == opc::BYTECODE_OPCODE_CONST_FLOAT64 {
            let dest = self.readRegister();
            let idx = self.readConstPoolId();
            BytecodeInstruction::ConstFloat64(dest, idx)
        } else if opcode == opc::BYTECODE_OPCODE_CONST_STRING {
            let dest = self.readRegister();
            let idx = self.readConstPoolId();
            BytecodeInstruction::ConstString(dest, idx)

        } else if opcode == opc::BYTECODE_OPCODE_TEST_EQ {
            let dest = self.readRegister();
            let lhs = self.readRegister();
            let rhs = self.readRegister();
            BytecodeInstruction::TestEq(dest, lhs, rhs)
        } else if opcode == opc::BYTECODE_OPCODE_TEST_NE {
            let dest = self.readRegister();
            let lhs = self.readRegister();
            let rhs = self.readRegister();
            BytecodeInstruction::TestNe(dest, lhs, rhs)
        } else if opcode == opc::BYTECODE_OPCODE_TEST_GT {
            let dest = self.readRegister();
            let lhs = self.readRegister();
            let rhs = self.readRegister();
            BytecodeInstruction::TestGt(dest, lhs, rhs)
        } else if opcode == opc::BYTECODE_OPCODE_TEST_GE {
            let dest = self.readRegister();
            let lhs = self.readRegister();
            let rhs = self.readRegister();
            BytecodeInstruction::TestGe(dest, lhs, rhs)
        } else if opcode == opc::BYTECODE_OPCODE_TEST_LT {
            let dest = self.readRegister();
            let lhs = self.readRegister();
            let rhs = self.readRegister();
            BytecodeInstruction::TestLt(dest, lhs, rhs)
        } else if opcode == opc::BYTECODE_OPCODE_TEST_LE {
            let dest = self.readRegister();
            let lhs = self.readRegister();
            let rhs = self.readRegister();
            BytecodeInstruction::TestLe(dest, lhs, rhs)
        } else if opcode == opc::BYTECODE_OPCODE_TEST_IDENTITY {
            let dest = self.readRegister();
            let lhs = self.readRegister();
            let rhs = self.readRegister();
            BytecodeInstruction::TestIdentity(dest, lhs, rhs)

        } else if opcode == opc::BYTECODE_OPCODE_LOOP_START {
            BytecodeInstruction::LoopStart

        } else if opcode == opc::BYTECODE_OPCODE_JUMP_LOOP {
            let offset = self.readOffset();
            BytecodeInstruction::JumpLoop(offset)

        } else if opcode == opc::BYTECODE_OPCODE_JUMP_IF_FALSE {
            let opnd = self.readRegister();
            let offset = self.readForwardOffset();
            BytecodeInstruction::JumpIfFalse(opnd, offset)
        } else if opcode == opc::BYTECODE_OPCODE_JUMP_IF_TRUE {
            let opnd = self.readRegister();
            let offset = self.readForwardOffset();
            BytecodeInstruction::JumpIfTrue(opnd, offset)
        } else if opcode == opc::BYTECODE_OPCODE_JUMP {
            let offset = self.readForwardOffset();
            BytecodeInstruction::Jump(offset)
        } else if opcode == opc::BYTECODE_OPCODE_SWITCH {
            let opnd = self.readRegister();
            let idx = self.readConstPoolId();
            BytecodeInstruction::Switch(opnd, idx)

        } else if opcode == opc::BYTECODE_OPCODE_INVOKE_DIRECT {
            let dest = self.readRegister();
            let fct = self.readConstPoolId();
            BytecodeInstruction::InvokeDirect(dest, fct)

        } else if opcode == opc::BYTECODE_OPCODE_INVOKE_VIRTUAL {
            let dest = self.readRegister();
            let fct = self.readConstPoolId();
            BytecodeInstruction::InvokeVirtual(dest, fct)

        } else if opcode == opc::BYTECODE_OPCODE_INVOKE_STATIC {
            let dest = self.readRegister();
            let fct = self.readConstPoolId();
            BytecodeInstruction::InvokeStatic(dest, fct)

        } else if opcode == opc::BYTECODE_OPCODE_INVOKE_LAMBDA {
            let dest = self.readRegister();
            let fct = self.readConstPoolId();
            BytecodeInstruction::InvokeLambda(dest, fct)

        } else if opcode == opc::BYTECODE_OPCODE_INVOKE_GENERIC_DIRECT {
            let dest = self.readRegister();
            let fct = self.readConstPoolId();
            BytecodeInstruction::InvokeGenericDirect(dest, fct)

        } else if opcode == opc::BYTECODE_OPCODE_INVOKE_GENERIC_STATIC {
            let dest = self.readRegister();
            let fct = self.readConstPoolId();
            BytecodeInstruction::InvokeGenericStatic(dest, fct)

        } else if opcode == opc::BYTECODE_OPCODE_NEW_OBJECT {
            let dest = self.readRegister();
            let idx = self.readConstPoolId();
            BytecodeInstruction::NewObject(dest, idx)

        } else if opcode == opc::BYTECODE_OPCODE_NEW_OBJECT_INITIALIZED {
            let dest = self.readRegister();
            let idx = self.readConstPoolId();
            BytecodeInstruction::NewObjectInitialized(dest, idx)

        } else if opcode == opc::BYTECODE_OPCODE_NEW_ARRAY {
            let dest = self.readRegister();
            let length = self.readRegister();
            let idx = self.readConstPoolId();
            BytecodeInstruction::NewArray(dest, length, idx)
        } else if opcode == opc::BYTECODE_OPCODE_NEW_LAMBDA {
            let dest = self.readRegister();
            let idx = self.readConstPoolId();
            BytecodeInstruction::NewLambda(dest, idx)
        } else if opcode == opc::BYTECODE_OPCODE_NEW_TRAIT_OBJECT {
            let dest = self.readRegister();
            let obj = self.readRegister();
            let idx = self.readConstPoolId();
            BytecodeInstruction::NewTraitObject(dest, obj, idx)
        } else if opcode == opc::BYTECODE_OPCODE_NEW_STRUCT {
            let dest = self.readRegister();
            let idx = self.readConstPoolId();
            BytecodeInstruction::NewStruct(dest, idx)
        } else if opcode == opc::BYTECODE_OPCODE_NEW_TUPLE {
            let dest = self.readRegister();
            let idx = self.readConstPoolId();
            BytecodeInstruction::NewTuple(dest, idx)
        } else if opcode == opc::BYTECODE_OPCODE_NEW_ENUM {
            let dest = self.readRegister();
            let idx = self.readConstPoolId();
            BytecodeInstruction::NewEnum(dest, idx)

        } else if opcode == opc::BYTECODE_OPCODE_ARRAY_LENGTH {
            let dest = self.readRegister();
            let arr = self.readRegister();
            BytecodeInstruction::ArrayLength(dest, arr)

        } else if opcode == opc::BYTECODE_OPCODE_LOAD_ARRAY {
            let dest = self.readRegister();
            let arr = self.readRegister();
            let idx = self.readRegister();
            BytecodeInstruction::LoadArray(dest, arr, idx)

        } else if opcode == opc::BYTECODE_OPCODE_STORE_ARRAY {
            let src = self.readRegister();
            let arr = self.readRegister();
            let idx = self.readRegister();
            BytecodeInstruction::StoreArray(src, arr, idx)

        } else if opcode == opc::BYTECODE_OPCODE_LOAD_TRAIT_OBJECT_VALUE {
            let dest = self.readRegister();
            let src = self.readRegister();
            BytecodeInstruction::LoadTraitObjectValue(dest, src)

        } else if opcode == opc::BYTECODE_OPCODE_GET_FIELD_ADDRESS {
            let dest = self.readRegister();
            let obj = self.readRegister();
            let idx = self.readConstPoolId();
            BytecodeInstruction::GetFieldAddress(dest, obj, idx)

        } else if opcode == opc::BYTECODE_OPCODE_STORE_AT_ADDRESS {
            let src = self.readRegister();
            let address = self.readRegister();
            BytecodeInstruction::StoreAtAddress(src, address)

        } else if opcode == opc::BYTECODE_OPCODE_RET {
            let opnd = self.readRegister();
            BytecodeInstruction::Ret(opnd)

        } else {
            std::fatalError[BytecodeInstruction]("unkown opcode ${opcode} ${opc::bytecodeName(opcode)}")
        };

        let size = self.pos - start;
        BytecodeInstructionInfo(start, opcode, size, op = inst)
    }

    fn readRegister(): BytecodeRegister {
        BytecodeRegister(self.readIndex())
    }

    fn readOpcode(): Int32 {
        self.readByte()
    }

    fn readGlobalId(): GlobalId {
        GlobalId(self.readIndex())
    }

    fn readConstId(): ConstId {
        ConstId(self.readIndex())
    }

    fn readConstPoolId(): ConstPoolId {
        ConstPoolId(self.readIndex())
    }

    fn readOffset(): Int32 {
        self.readIndex()
    }

    fn readForwardOffset(): Int32 {
        self.readInt32Fixed()
    }

    fn readIndex(): Int32 {
        self.readInt32Variable()
    }

    fn readByte(): Int32 {
        let value = self.data(self.pos);
        self.pos = self.pos + 1;

        value.toInt32()
    }

    fn readInt32Variable(): Int32 {
        let mut result: Int32 = 0;
        let mut shift: Int32 = 0;

        while true {
            let byte = self.readByte();
            result = result | ((byte & 0x7Fi32) << shift);
            if byte & 0x80i32 == 0i32 {
                break;
            }
            shift = shift + 7i32;
        }

        result
    }

    fn readInt32Fixed(): Int32 {
        let by1 = self.readByte();
        let by2 = self.readByte();
        let by3 = self.readByte();
        let by4 = self.readByte();

        (by4 << 24i32) | (by3 << 16i32) | (by2 << 8i32) | by1
    }
}

impl Iterator for BytecodeIterator {
    type Item = BytecodeInstructionInfo;

    fn next(): Option[BytecodeInstructionInfo] {
        if self.pos < self.length {
            Some[BytecodeInstructionInfo](self.readInstruction())
        } else {
            None[BytecodeInstructionInfo]
        }
    }
}

pub struct BytecodeInstructionInfo {
    pub start: Int64,
    pub opcode: Int32,
    pub size: Int64,
    pub op: BytecodeInstruction,
}
