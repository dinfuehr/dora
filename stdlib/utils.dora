class Vec<T> {
  var array: Array<T> = nil;
  var length: int = 0;

  fun get(idx: int) -> T {
    if idx < 0 || idx >= self.length {
      fatalError("index out of bounds for vector");
    }

    return self.array[idx];
  }

  fun set(idx: int, val: T) {
    if idx < 0 || idx >= self.length {
      fatalError("index out of bounds for vector");
    }

    self.array[idx] = val;
  }

  fun push(val: T) {
    var newcap = self.capacity();

    if self.length == newcap {
      if newcap == 0 {
        newcap = 4;
      } else {
        newcap = newcap * 2;
      }

      let newarray = Array::<T>(newcap);
      arraycopy::<T>(self.array, 0, newarray, 0, self.length);
      self.array = newarray;
    }

    self.array[self.length] = val;
    self.length = self.length + 1;
  }

  fun pop() -> T {
    if self.length == 0 {
      fatalError("no element left to pop");
    }

    let newlength = self.length - 1;
    let temp = self.array[newlength];

    // set popped element to nil so that GC can collect object
    // not necessary for primitive types
    self.array[newlength] = defaultValue::<T>();

    self.length = newlength;

    return temp;
  }

  fun trimToLen() {
    if self.length != self.capacity() {
      if self.length == 0 {
        self.array = nil;
      } else {
        let newarray = Array::<T>(self.length);
        arraycopy::<T>(self.array, 0, newarray, 0, self.length);
        self.array = newarray;
      }
    }
  }

  fun removeAt(var ind: int) -> T {
    assert(ind < self.length);
    let temp = self[ind];
    let len = self.length;

    while ind < len - 1 {
      self[ind] = self[ind+1];
      ind = ind + 1;
    }

    self[ind] = defaultValue::<T>();
    self.length = ind;

    return temp;
  }

  fun len() -> int {
    return self.length;
  }

  fun capacity() -> int {
    if self.array === nil {
      return 0;
    } else {
      return self.array.len();
    }
  }
}

fun removeItem<T: Equals>(vec: Vec<T>, elem: T) {
  var i = 0;

  while i < vec.len() {
    if vec[i].equals(elem) {
      vec.removeAt(i);
    } else {
      i = i + 1;
    }
  }
}

class Queue<T> {
  var elements: Array<T> = Array::<T>(4);
  var front: int = 0;
  var count: int = 0;

  pub fun enqueue(value: T) {
    if self.count == self.elements.len() {
      // copy into larger array
      let newelements = Array::<T>(self.elements.len() * 2);
      let len = self.elements.len() - self.front;
      arraycopy::<T>(self.elements, self.front, newelements, 0, len);
      
      if len < self.count {
        arraycopy::<T>(self.elements, 0, newelements, len, self.count - len);
      }

      self.front = 0;
      // self.count stays the same
      self.elements = newelements;
    }

    let end = self.getEnd();
    self.elements[end] = value;
    self.count = self.count + 1;
  }

  pub fun dequeue() -> T {
    assert(self.count > 0);

    let value = self.elements[self.front];
    self.elements[self.front] = defaultValue::<T>();

    self.moveFront();
    self.count = self.count - 1;

    return value;
  }

  fun moveFront() -> int {
    self.front = self.front + 1;

    if self.front == self.elements.len() {
      self.front = 0;
    }

    return self.front;
  }

  fun getEnd() -> int {
    let end = self.front + self.count;
    
    if end < self.elements.len() {
      return end;
    } else {
      return end - self.elements.len();
    }
  }

  pub fun len() -> int {
    return self.count;
  }

  pub fun isEmpty() -> bool {
    return self.count == 0;
  }
}

fun isValidUtf8(data: Array<byte>) -> bool {
  var i = 0;

  while i < data.len() {
    let by = data[i].toInt();
    var codePoint = 0;
    var nextBytes = 0;
    var min = 0;
    var max = 0;

    if by & 0x80 == 0 {
      nextBytes = 0;
      i = i + 1;
      continue;

    } else if by & 0xC0 == 0x80 {
      return false;

    } else if by & 0xE0 == 0xC0 {
      codePoint = by & 0x1F;
      nextBytes = 1;
      min = 0x80;
      max = 0x7FF;

    } else if by & 0xF0 == 0xE0 {
      codePoint = by & 0xF;
      nextBytes = 2;
      min = 0x800;
      max = 0xFFFF;

    } else if by & 0xF8 == 0xF0 {
      codePoint = by & 0x7;
      nextBytes = 3;
      min = 0x10000;
      max = 0x10FFFF;
    }

    while nextBytes > 0 {
      nextBytes = nextBytes - 1;
      i = i + 1;

      if i >= data.len() {
        return false;
      }

      let by = data[i].toInt();

      if by & 0xC0 != 0x80 {
        return false;
      }

      codePoint = (codePoint << 6) | (by & 0x3F);
    }

    if codePoint < min || codePoint > max {
      return false;
    }

    i = i + 1;
  }

  return true;
}

class StringBuf(var buf: Array<byte>, var length: int) {
  init(): self(Array::<byte>(0), 0) {

  }

  fun len() -> int {
    return self.length;
  }

  fun capacity() -> int {
    return self.buf.len();
  }

  // reserve `elements` bytes
  // (capacity - length >= elements)
  fun reserve(elements: int) {
    if self.capacity() - self.len() >= elements {
      return;
    }

    let newcap = self.newCapacity(elements);
    let newbuf = Array::<byte>(newcap);
    var i = 0;

    while i < self.buf.len() {
      newbuf[i] = self.buf[i];
      i = i + 1;
    }

    self.buf = newbuf;
  }

  fun newCapacity(reserve: int) -> int {
    var len = self.length;

    if len == 0 {
      len = 4;
    }

    let c1 = (len + reserve + 7) & !8;
    let c2 = len * 2;

    if c1 > c2 {
      return c1;
    } else if c2 - c1 > 32 {
      return c1;
    } else {
      return c2;
    }
  }

  fun appendChar(ch: char) -> StringBuf {
    let chLen = ch.lenUtf8();
    self.reserve(chLen);
    ch.encodeUtf8(self.buf, self.length);
    self.length = self.length + chLen;
    return self;
  }

  fun appendString(value: Str) -> StringBuf {
    self.reserve(value.len());
    var i = 0;

    while i < value.len() {
      self.buf[self.length + i] = value.getByte(i);
      i = i + 1;
    }

    self.length = self.length + value.len();
    return self;
  }

  fun toString() -> Str {
    return try! Str::fromBytesPart(self.buf, 0, self.len());
  }
}
