native fun fatalError(msg: String);
native fun abort();
native fun exit(status: int);
fun unreachable() {
  fatalError("unreachable code");
}

fun unimplemented() {
  fatalError("not yet implemented");
}

native fun print(text: String);
native fun println(text: String);
native fun addressOf(object: Object) -> long;
native fun assert(val: bool);
native fun debug();
native fun argc() -> int;
native fun argv(idx: int) -> String;
native fun forceCollect();
native fun forceMinorCollect();

native fun call(fct: String);
native fun throwFromNative(val: bool) throws;
native fun throwFromNativeButNotThrows(val: bool);

native fun timestamp() -> long;

native class bool {
  native fun toInt() -> int;
  fun toString() -> String {
    if self {
      return "true";
    } else {
      return "false";
    }
  }

  native fun equals(rhs: bool) -> bool;
  native fun not() -> bool;
}

native class byte {
  native fun toInt() -> int;
  native fun toLong() -> long;
  native fun toString() -> String;

  native fun equals(rhs: byte) -> bool;
  native fun compareTo(rhs: byte) -> int;
}

native class char {
  native fun toInt() -> int;
  native fun toLong() -> long;
  native fun toString() -> String;

  native fun equals(rhs: char) -> bool;
  native fun compareTo(rhs: char) -> int;

  fun encodeUtf8(bytes: Array<byte>, offset: int) {
    let val = self.toInt();

    if val < 0x80 {
      bytes[offset] = val.toByte();
    } else if val < 0x800 {
      bytes[offset] = (0xC0 | ((val >> 6) & 0x1F)).toByte();
      bytes[offset+1] = (0x80 | (val & 0x3F)).toByte();
    } else if val < 0x10000 {
      bytes[offset] = (0xE0 | ((val >> 12) & 0x0F)).toByte();
      bytes[offset+1] = (0x80 | ((val >> 6) & 0x3F)).toByte();
      bytes[offset+2] = (0x80 | (val & 0x3F)).toByte();
    } else {
      bytes[offset] = (0xF0 | ((val >> 18) & 0x07)).toByte();
      bytes[offset+1] = (0x80 | ((val >> 12) & 0x3F)).toByte();
      bytes[offset+2] = (0x80 | ((val >> 6) & 0x3F)).toByte();
      bytes[offset+3] = (0x80 | (val & 0x3F)).toByte();
    }
  }

  fun lenUtf8() -> int {
    let val = self.toInt();

    if val < 0x80 {
      return 1;
    } else if val < 0x800 {
      return 2;
    } else if val < 0x10000 {
      return 3;
    } else {
      return 4;
    }
  }
}

native class int {
  native fun toByte() -> byte;
  fun toChar() throws -> char {
    if self >= 0 && self <= 0x10FFFF && (self < 0xD800 || self > 0xDFFF) {
      return self.toCharUnchecked();
    } else {
      throw "invalid code point";
    }
  }
  native fun toCharUnchecked() -> char;
  native fun toLong() -> long;
  native fun toString() -> String;

  native fun toFloat() -> float;
  native fun toDouble() -> double;

  native fun asFloat() -> float;

  native fun equals(rhs: int) -> bool;
  native fun compareTo(rhs: int) -> int;

  native fun plus(rhs: int) -> int;
  native fun minus(rhs: int) -> int;
  native fun times(rhs: int) -> int;
  native fun div(rhs: int) -> int;
  native fun mod(rhs: int) -> int;

  native fun bitwiseOr(rhs: int) -> int;
  native fun bitwiseAnd(rhs: int) -> int;
  native fun bitwiseXor(rhs: int) -> int;

  native fun shiftLeft(rhs: int) -> int;
  native fun shiftRight(rhs: int) -> int;
  native fun unsignedShiftRight(rhs: int) -> int;

  native fun unaryPlus() -> int;
  native fun unaryMinus() -> int;
  native fun not() -> int;

  fun abs() -> int {
    if self >= 0 {
      return self;
    } else {
      return -self;
    }
  }

  static fun min(lhs: int, rhs: int) -> int {
    if lhs < rhs {
      return lhs;
    } else {
      return rhs;
    }
  }

  static fun max(lhs: int, rhs: int) -> int {
    if lhs < rhs {
      return rhs;
    } else {
      return lhs;
    }
  }

  static fun max_value() -> int {
    return 2147483647;
  }

  static fun min_value() -> int {
    return -2147483648;
  }
}

native class long {
  native fun toByte() -> byte;
  fun toChar() throws -> char {
    if self >= 0L && self <= 0x10FFFFL && (self < 0xD800L || self > 0xDFFFL) {
      return self.toCharUnchecked();
    } else {
      throw "invalid code point";
    }
  }
  native fun toCharUnchecked() -> char;
  native fun toInt() -> int;
  native fun toString() -> String;

  native fun toFloat() -> float;
  native fun toDouble() -> double;

  native fun asDouble() -> double;

  native fun equals(rhs: long) -> bool;
  native fun compareTo(rhs: long) -> int;

  native fun plus(rhs: long) -> long;
  native fun minus(rhs: long) -> long;
  native fun times(rhs: long) -> long;
  native fun div(rhs: long) -> long;
  native fun mod(rhs: long) -> long;

  native fun bitwiseOr(rhs: long) -> long;
  native fun bitwiseAnd(rhs: long) -> long;
  native fun bitwiseXor(rhs: long) -> long;

  native fun shiftLeft(rhs: long) -> long;
  native fun shiftRight(rhs: long) -> long;
  native fun unsignedShiftRight(rhs: long) -> long;

  native fun unaryPlus() -> long;
  native fun unaryMinus() -> long;
  native fun not() -> long;

  static fun max_value() -> long {
    return 9223372036854775807L;
  }

  static fun min_value() -> long {
    return -9223372036854775808L;
  }

  static fun min(lhs: long, rhs: long) -> long {
    if lhs < rhs {
      return lhs;
    } else {
      return rhs;
    }
  }

  static fun max(lhs: long, rhs: long) -> long {
    if lhs < rhs {
      return rhs;
    } else {
      return lhs;
    }
  }
}

native class float {
  native fun toInt() -> int;
  native fun toLong() -> long;
  native fun toDouble() -> double;
  native fun toString() -> String;

  native fun asInt() -> int;

  native fun equals(rhs: float) -> bool;
  native fun compareTo(rhs: float) -> int;

  native fun plus(rhs: float) -> float;
  native fun minus(rhs: float) -> float;
  native fun times(rhs: float) -> float;
  native fun div(rhs: float) -> float;

  native fun unaryPlus() -> float;
  native fun unaryMinus() -> float;

  native fun isNan() -> bool;
  native fun sqrt() -> float;
}

native class double {
  native fun toInt() -> int;
  native fun toLong() -> long;
  native fun toFloat() -> float;
  native fun toString() -> String;

  native fun asLong() -> long;

  native fun equals(rhs: double) -> bool;
  native fun compareTo(rhs: double) -> int;

  native fun plus(rhs: double) -> double;
  native fun minus(rhs: double) -> double;
  native fun times(rhs: double) -> double;
  native fun div(rhs: double) -> double;

  native fun unaryPlus() -> double;
  native fun unaryMinus() -> double;

  native fun isNan() -> bool;
  native fun sqrt() -> double;
}

class Object

native class Array<T> {
  init() {}
  init(len: int) {}
  init(len: int, value: T) {
    var i = 0;

    while i < len {
      self[i] = value;
      i = i + 1;
    }
  }

  native fun len() -> int;
  native fun get(idx: int) -> T;
  native fun set(idx: int, val: T);

  fun contains(value: T /* : Equals*/) -> bool {
    var i = 0;

    while i < self.len() {
      let x = self[i];
      if /*x.equals(value) ||*/ x === value {
        return true;
      }
      i = i + 1;
    }

    return false;
  }

  fun has(value: T) -> bool {
    var i = 0;

    while i < self.len() {
      if self[i] === value {
        return true;
      }
      i = i + 1;
    }

    return false;
  }
}

fun arraycopy<T>(src: Array<T>, srcPos: int, dest: Array<T>, destPos: int, len: int) {
  var i = 0;

  while i < len {
    dest[destPos+i] = src[srcPos+i];
    i = i + 1;
  }
}

trait Equals {
  fun equals(other: Self) -> bool;
}

impl Equals for int {
  native fun equals(other: int) -> bool;
}

trait Comparable {
  fun compareTo(other: Self) -> int;
}

impl Comparable for int {
  native fun compareTo(other: int) -> int;
}

fun bubbleSort<T: Comparable>(array: Array<T>) {
  let len = array.len();
  var swapped = true;

  while swapped {
    swapped = false;

    var i = 1;

    while i < len {
      if array[i-1].compareTo(array[i]) > 0 {
        let temp = array[i-1];
        array[i-1] = array[i];
        array[i] = temp;

        swapped = true;
      }

      i = i + 1;
    }
  }

}

native fun defaultValue<T>() -> T;

native fun loadFunction(name: String) -> long;
native fun call0(fct: long) -> long;
native fun call1(fct: long, arg0: long) -> long;
native fun call2(fct: long, arg0: long, arg1: long) -> long;
native fun call3(fct: long, arg0: long, arg1: long, arg2: long) -> long;

native fun native_malloc(size: long) -> long;
native fun native_free(address: long);
native fun set_uint8(address: long, val: byte);

fun native_string(val: String) -> long {
  var i = 0;
  let len = val.len();

  let ptr = native_malloc(len.toLong()+1L);

  while i < len {
    set_uint8(ptr+i.toLong(), val.getByte(i));
    i = i + 1;
  }

  set_uint8(ptr+len.toLong(), 0Y);

  return ptr;
}

fun getpid() -> int {
  let fct = loadFunction("getpid");
  return call0(fct).toInt();
}

fun getppid() -> int {
  let fct = loadFunction("getppid");
  return call0(fct).toInt();
}

native fun sleep(seconds: int);

class Exception {
  var msg: String = nil;
  var backtrace: Array<int> = nil;
  var elements: Array<StackTraceElement> = nil;

  init(msg: String) {
    self.retrieveStackTrace();
    self.msg = msg;
  }

  init() {
    self.retrieveStackTrace();
  }

  fun getStackTrace() -> Array<StackTraceElement> {
    if self.elements !== nil {
      return self.elements;
    }

    if self.backtrace === nil {
      self.elements = Array::<StackTraceElement>();
      return self.elements;
    }

    var i = 0;
    let len = self.backtrace.len() / 2;
    self.elements = Array::<StackTraceElement>(len);

    while i < len {
      self.elements[i] = self.getStackTraceElement(i);
      i = i + 1;
    }

    return self.elements;
  }

  fun printStackTrace() {
    if self.msg !== nil {
      println("Exception: " + self.msg);
    } else {
      println("Exception");
    }

    let x = self.getStackTrace();

    var i = 0;

    while i < x.len() {
      println(i.toString() + ": " + x[i].toString());
      i = i + 1;
    }
  }

  native fun retrieveStackTrace();
  native fun getStackTraceElement(idx: int) -> StackTraceElement;
}

class StackTraceElement(let name: String, let line: int) {
  fun toString() -> String {
    return self.name + ": " + self.line.toString();
  }
}

class IntRange(let lower: int, let upper: int) {
  fun makeIterator() -> IntRangeIter {
    return IntRangeIter(self);
  }
}

fun range(lower: int, upper: int) -> IntRange {
  return IntRange(lower, upper);
}

trait Iterator {
  fun hasNext() -> bool;
  fun next() -> int;
}

class IntRangeIter(let range: IntRange) {
  var value: int = range.lower;
}

impl Iterator for IntRangeIter {
  fun hasNext() -> bool {
    return self.value < self.range.upper;
  }

  fun next() -> int {
    let cur = self.value;
    self.value = cur + 1;
    return cur;
  }
}

open abstract class Thread {
  native fun start();

  abstract fun run();
}

